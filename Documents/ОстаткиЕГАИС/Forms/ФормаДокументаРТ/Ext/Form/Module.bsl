
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик механизма "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Обработчик подсистемы "Внешние обработки".
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	
	ОбщегоНазначенияРТ.ЗаполнитьШапкуДокумента(Объект, КартинкаСостоянияДокумента, Элементы.КартинкаСостоянияДокумента.Подсказка, РазрешеноПроведение);
	ОбщегоНазначенияРТКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаКомментарий, Объект.Комментарий);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПроверитьИнформациюОбОшибке();
		ДоступностьЭлементовФормы();
		ЗаполнитьОрганизациюЕГАИС();
	КонецЕсли;
	
	Документы.ОстаткиЕГАИС.УстановитьУсловноеОформлениеСтатусаОбработки(ЭтотОбъект, "СтатусОбработки", "Объект.СтатусОбработки");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПроверитьИнформациюОбОшибке();
	
	ДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если не Объект.Организация.Пустая() и Объект.ОрганизацияЕГАИС.Пустая() Тогда
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = НСтр("ru = 'Для выбранной организации отсутствует настройка обмена с ЕГАИС'");
		СообщениеПользователю.Поле = "Объект.Организация";
		СообщениеПользователю.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	Элементы.СтраницаКорректировкаОстатков.Видимость = ВидимостьКорректировкиОстатков(Объект.ОрганизацияЕГАИС, Объект.ВидДокумента);
	
	ВидимостьСправкиБ = Объект.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросОстатков");
	
	Элементы.ОстаткиПоДаннымЕГАИССправкаБ.Видимость = ВидимостьСправкиБ;
	Элементы.КорректировкаОстатковСправкаБ.Видимость = ВидимостьСправкиБ;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьКартинкуДляКомментария", 0.5, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьОрганизациюЕГАИС();
	
КонецПроцедуры

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	
	ЗаполнитьОрганизациюЕГАИС();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьВЕГАИС(Команда)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Документ не сохранен.'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	
	Если НЕ Объект.Проведен Тогда
		Если НЕ Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ПроведенПередОтправкой", Истина);
	КонецЕсли;
	
	ПараметрыЗапроса = ИнтеграцияЕГАИСКлиентСервер.ПараметрыИсходящегоЗапроса(Объект.ВидДокумента);
	ПараметрыЗапроса.ДокументСсылка = Объект.Ссылка;
	
	ИнтеграцияЕГАИСКлиент.НачатьФормированиеИсходящегоЗапроса(
		Новый ОписаниеОповещения("ВыгрузкаВЕГАИС_Завершение", ЭтотОбъект, ДополнительныеПараметры),
		Объект.ВидДокумента,
		ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРасхождениям(Команда)
	
	Если Объект.СтатусОбработки <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиОстатковЕГАИС.ПолученыОстатки") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Для заполнения по расхождениям требуется получение остатков из ЕГАИС.'"));
		Возврат;
	КонецЕсли;
	
	Если Модифицированность ИЛИ НЕ Объект.Проведен Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Документ не проведен.'"));
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоРасхождениямНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьСвернутьТЧ(Команда)
	РазвернутьСвернутьТЧНаСервере();
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура СтатусОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСРТКлиент.СтатусОбработкиНажатие(ЭтотОбъект, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыгрузкаВЕГАИС_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Результат Тогда
		ЭтотОбъект.Прочитать();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрос на загрузку остатков сформирован.'"));
	ИначеЕсли ДополнительныеПараметры.Свойство("ПроведенПередОтправкой") Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьИнформациюОбОшибке()

	ИнформацияОбОшибке = РегистрыСведений.ПротоколОбменаЕГАИС.ТекстПоследнейОшибки(Объект.Ссылка);
	Элементы.ИнформацияОбОшибке.Видимость = НЕ ПустаяСтрока(ИнформацияОбОшибке);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоРасхождениямНаСервере()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.ЗаполнитьПоРасхождениям();
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ДоступностьЭлементовФормы()
	
	ТолькоПросмотр = Объект.СтатусОбработки = Перечисления.СтатусыОбработкиОстатковЕГАИС.ПереданЗапрос;
	
	ДоступностьКоманды = Объект.Проведен;
	
	Если Объект.СтатусОбработки = Перечисления.СтатусыОбработкиОстатковЕГАИС.ПереданЗапрос
		ИЛИ Объект.СтатусОбработки = Перечисления.СтатусыОбработкиОстатковЕГАИС.ПолученыОстатки Тогда
		ДоступностьКоманды = Ложь;
	КонецЕсли;
	
	Элементы.ФормаВыгрузитьВЕГАИС.Доступность = ДоступностьКоманды;
	
	ПолученыОстатки = Объект.СтатусОбработки = Перечисления.СтатусыОбработкиОстатковЕГАИС.ПолученыОстатки;
	Элементы.ВидДокумента.ТолькоПросмотр = ПолученыОстатки;
	
	Элементы.СтраницаКорректировкаОстатков.Видимость = ВидимостьКорректировкиОстатков(Объект.ОрганизацияЕГАИС, Объект.ВидДокумента);
	
	ВидимостьСправкиБ = Объект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.ЗапросОстатков;
	Элементы.ОстаткиПоДаннымЕГАИССправкаБ.Видимость = ВидимостьСправкиБ;
	Элементы.КорректировкаОстатковСправкаБ.Видимость = ВидимостьСправкиБ;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьКорректировкиОстатков(ОрганизацияЕГАИС, ВидДокумента)
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияЕГАИС) ИЛИ НЕ ЗначениеЗаполнено(ВидДокумента) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияЕГАИС, "УчитыватьОстаткиАлкогольнойПродукции")
		И ВидДокумента = Перечисления.ВидыДокументовЕГАИС.ЗапросОстатков;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_УстановитьКартинкуДляКомментария()
	ОбщегоНазначенияРТКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаКомментарий, Объект.Комментарий);
КонецПроцедуры

&НаСервере
Процедура РазвернутьСвернутьТЧНаСервере()
	
	РазвернутаТЧ = НЕ РазвернутаТЧ;
	
	ВидимостьЭлементов = НЕ РазвернутаТЧ;
	
	ЭтотОбъект.ПоложениеКоманднойПанели = ?(ВидимостьЭлементов,
		ПоложениеКоманднойПанелиФормы.Авто,
		ПоложениеКоманднойПанелиФормы.Нет);
		
	Элементы.Шапка.Видимость               = ВидимостьЭлементов;
	Элементы.СтраницаКомментарий.Видимость = ВидимостьЭлементов;
	Элементы.ГруппаОтветственный.Видимость = ВидимостьЭлементов;
	
	Элементы.РазвернутьСвернутьТЧ.Картинка = ?(ВидимостьЭлементов,
		БиблиотекаКартинок.РазвернутьТабличнуюЧасть,
		БиблиотекаКартинок.СвернутьТабличнуюЧасть);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОрганизациюЕГАИС()
	
	ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка();
	
	Если НЕ Объект.Организация.Пустая() И НЕ Объект.Магазин.Пустая() Тогда
		ТранспортныйМодуль = ИнтеграцияЕГАИСРТ.ИспользуемыйТранспортныйМодуль(Объект.Организация, Объект.Магазин);
		Если ЗначениеЗаполнено(ТранспортныйМодуль) Тогда
			ОрганизацияЕГАИС = ТранспортныйМодуль.ОрганизацияЕГАИС;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ОрганизацияЕГАИС <> ОрганизацияЕГАИС Тогда
		Объект.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#КонецОбласти