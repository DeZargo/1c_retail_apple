#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ);	
	ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,Обработки.РаспределениеТоваровПоСкладам.ПараметрыУказанияСерий(ЭтотОбъект),Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
	ПроверитьЗаполнениеТабличнойЧастиСерийныеНомера(Отказ);
	
	МаркетинговыеАкцииСервер.ПроверитьДвиженияСерийныхНомеров(
		ЭтотОбъект,
		"Товары",
		"СерийныеНомера",
		Отказ
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеТабличнойЧастиСерийныеНомера(Отказ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Товары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	Товары.НомерСтроки,
	|	Товары.КоличествоФакт КАК Количество
	|ПОМЕСТИТЬ ТабТоварыВСЕ
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура,
	|	ТабТовары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	ТабТовары.НомерСтроки,
	|	ТабТовары.Количество
	|ПОМЕСТИТЬ ТабТовары
	|ИЗ
	|	ТабТоварыВСЕ КАК ТабТовары
	|ГДЕ
	|	ТабТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|	И ТабТовары.Номенклатура.ИспользоватьСерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	&СерийныеНомера КАК СерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабТовары.Номенклатура.Наименование КАК Наименование,
	|	ТабТовары.НомерСтроки
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = ТабСерийныеНомера.КлючСвязиСерийныхНомеров
	|ГДЕ
	|	(НЕ ТабСерийныеНомера.КлючСвязиСерийныхНомеров ЕСТЬ NULL )
	|	И ТабСерийныеНомера.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)");
	
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	Запрос.УстановитьПараметр("СерийныеНомера", СерийныеНомера.Выгрузить());
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В номерах подарочных сертификатов номенклатуры %1 (строка №%2) есть пустые номера'"),
			Выборка.Наименование,
			Выборка.НомерСтроки
		);
		
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ЭтотОбъект,
			"Товары[" + (Выборка.НомерСтроки - 1) + "].Номенклатура",
			,
			Отказ
		);
		
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.КлючСвязиСерийныхНомеров
	|ПОМЕСТИТЬ КоличествоСерийныхНомеров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТабСерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТабСерийныеНомера.СерийныйНомер) КАК Количество
	|	ИЗ
	|		ТабСерийныеНомера КАК ТабСерийныеНомера
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТабСерийныеНомера.КлючСвязиСерийныхНомеров) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура.Наименование КАК Наименование,
	|	ТабТовары.НомерСтроки,
	|	ТабТовары.Количество,
	|	ЕСТЬNULL(КоличествоСерийныхНомеров.Количество, 0) КАК КоличествоСерийныхНомеров
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоСерийныхНомеров КАК КоличествоСерийныхНомеров
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = КоличествоСерийныхНомеров.КлючСвязиСерийныхНомеров
	|ГДЕ
	|	(НЕ ТабТовары.Количество = ЕСТЬNULL(КоличествоСерийныхНомеров.Количество, 0))";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номенклатура: %1 (строка №%2). Не совпадает количество номенклатуры - %3 и количество номеров подарочных сертификатов - %4'"),
			Выборка.Наименование,
			Выборка.НомерСтроки,
			Выборка.Количество,
			Выборка.КоличествоСерийныхНомеров
		);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ЭтотОбъект,
			"Товары[" + (Выборка.НомерСтроки - 1) + "].Номенклатура",
			,
			Отказ
		);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
