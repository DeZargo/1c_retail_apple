#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Создает новый документ ОстаткиЕГАИС.
//
Процедура СоздатьДокументЗапросаОстатков(ВидДокумента) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗапросОстатков = Документы.ОстаткиЕГАИС.СоздатьДокумент();
	ЗапросОстатков.Дата = ТекущаяДатаСеанса();
	ЗапросОстатков.Заполнить(Неопределено);
	
	ЗапросОстатков.ВидДокумента = ВидДокумента;
	ЗапросОстатков.СтатусОбработки = Перечисления.СтатусыОбработкиОстатковЕГАИС.Новый;
	ЗапросОстатков.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ЗапросОстатков.Организация = Организация;
	ЗапросОстатков.Магазин = Магазин;
	
	ЗапросОстатков.Записать(РежимЗаписиДокумента.Проведение);
	
	Если ВидДокумента = Перечисления.ВидыДокументовЕГАИС.ЗапросОстатков Тогда
		ОстаткиНаСкладе = ЗапросОстатков.Ссылка;
	ИначеЕсли ВидДокумента = Перечисления.ВидыДокументовЕГАИС.ЗапросОстатковВТорговомЗале Тогда
		ОстаткиВТорговомЗале = ЗапросОстатков.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу учетных остатков на заданную дату.
//
Процедура ЗаполнитьУчетныеОстатки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Магазин", Магазин);
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаУчетныхОстатков);
	Запрос.УстановитьПараметр("КорректироватьОстаткиНемаркируемойПродукции", КорректироватьОстаткиНемаркируемойПродукции);
	
	УсловиеПоДате = ?(ЗначениеЗаполнено(ДатаУчетныхОстатков), "&ДатаОстатков", "");
	
	Если НЕ ИспользуетсяНесколькоСкладов Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
		|	ЕСТЬNULL(ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ) КАК ПродаетсяВРозлив,
		|	ТоварыНаСкладахОстатки.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ,
		|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))) КАК АлкогольнаяПродукция,
		|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.ТипПродукции, ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.ПустаяСсылка))) КАК ТипПродукции,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток * ВЫБОР
		|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
		|			ТОГДА ТоварыНаСкладахОстатки.Номенклатура.ОбъемДАЛ
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ОстатокТорговыйЗал,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК ОстатокТорговыйЗалБазЕд,
		|	0 КАК ОстатокСклад,
		|	0 КАК ОстатокСкладБазЕд
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			" + УсловиеПоДате + ",
		|			Склад.Магазин = &Магазин
		|				И Номенклатура.АлкогольнаяПродукция
		|				И ВЫБОР
		|					КОГДА НЕ ЕСТЬNULL(Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
		|						ТОГДА &КорректироватьОстаткиНемаркируемойПродукции
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК ТоварыНаСкладахОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|		ПО ТоварыНаСкладахОстатки.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
		|			И ТоварыНаСкладахОстатки.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
		|			И (ТоварыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения = СоответствиеНоменклатурыЕГАИС.Упаковка.ЕдиницаИзмерения
		|				ИЛИ СоответствиеНоменклатурыЕГАИС.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыНаСкладахОстатки.Номенклатура,
		|	ТоварыНаСкладахОстатки.Характеристика,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток * ВЫБОР
		|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
		|			ТОГДА ТоварыНаСкладахОстатки.Номенклатура.ОбъемДАЛ
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	ЕСТЬNULL(ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ),
		|	ТоварыНаСкладахОстатки.Номенклатура.ОбъемДАЛ,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура
		|АВТОУПОРЯДОЧИВАНИЕ";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	СУММА(ВложенныйЗапрос.ОстатокСклад) КАК ОстатокСклад,
		|	СУММА(ВложенныйЗапрос.ОстатокТорговыйЗал) КАК ОстатокТорговыйЗал,
		|	СУММА(ВложенныйЗапрос.ОстатокСкладБазЕд) КАК ОстатокСкладБазЕд,
		|	СУММА(ВложенныйЗапрос.ОстатокТорговыйЗалБазЕд) КАК ОстатокТорговыйЗалБазЕд
		|ПОМЕСТИТЬ ТоварыНаСкладах
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
		|		ТоварыНаСкладахОстатки.КоличествоОстаток * ВЫБОР
		|			КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
		|				ТОГДА ТоварыНаСкладахОстатки.Номенклатура.ОбъемДАЛ
		|			ИНАЧЕ 1
		|		КОНЕЦ КАК ОстатокСклад,
		|		ТоварыНаСкладахОстатки.КоличествоОстаток КАК ОстатокСкладБазЕд,
		|		0 КАК ОстатокТорговыйЗал,
		|		0 КАК ОстатокТорговыйЗалБазЕд
		|	ИЗ
		|		РегистрНакопления.ТоварыНаСкладах.Остатки(
		|				" + УсловиеПоДате + ",
		|				Склад.Магазин = &Магазин
		|					И Склад.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.СкладскоеПомещение)
		|					И Номенклатура.АлкогольнаяПродукция
		|					И ВЫБОР
		|						КОГДА НЕ ЕСТЬNULL(Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
		|							ТОГДА &КорректироватьОстаткиНемаркируемойПродукции
		|						ИНАЧЕ ИСТИНА
		|					КОНЕЦ) КАК ТоварыНаСкладахОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТоварыНаСкладахОстатки.Номенклатура,
		|		ТоварыНаСкладахОстатки.Характеристика,
		|		0,
		|		0,
		|		ТоварыНаСкладахОстатки.КоличествоОстаток * ВЫБОР
		|			КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
		|				ТОГДА ТоварыНаСкладахОстатки.Номенклатура.ОбъемДАЛ
		|			ИНАЧЕ 1
		|		КОНЕЦ,
		|		ТоварыНаСкладахОстатки.КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.ТоварыНаСкладах.Остатки(
		|				" + УсловиеПоДате + ",
		|				Склад.Магазин = &Магазин
		|					И Склад.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.ТорговыйЗал)
		|					И Номенклатура.АлкогольнаяПродукция
		|					И ВЫБОР
		|						КОГДА НЕ ЕСТЬNULL(Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
		|							ТОГДА &КорректироватьОстаткиНемаркируемойПродукции
		|						ИНАЧЕ ИСТИНА
		|					КОНЕЦ) КАК ТоварыНаСкладахОстатки) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладах.Характеристика,
		|	ЕСТЬNULL(ТоварыНаСкладах.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ) КАК ПродаетсяВРозлив,
		|	ТоварыНаСкладах.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ,
		|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))) КАК АлкогольнаяПродукция,
		|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.ТипПродукции, ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.ПустаяСсылка))) КАК ТипПродукции,
		|	ТоварыНаСкладах.ОстатокСклад,
		|	ТоварыНаСкладах.ОстатокТорговыйЗал,
		|	ТоварыНаСкладах.ОстатокСкладБазЕд,
		|	ТоварыНаСкладах.ОстатокТорговыйЗалБазЕд
		|ИЗ
		|	ТоварыНаСкладах КАК ТоварыНаСкладах
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|		ПО ТоварыНаСкладах.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
		|			И ТоварыНаСкладах.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
		|			И (ТоварыНаСкладах.Номенклатура.ЕдиницаИзмерения = СоответствиеНоменклатурыЕГАИС.Упаковка.ЕдиницаИзмерения
		|				ИЛИ СоответствиеНоменклатурыЕГАИС.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыНаСкладах.Номенклатура,
		|	ТоварыНаСкладах.Характеристика,
		|	ТоварыНаСкладах.ОстатокСклад,
		|	ТоварыНаСкладах.ОстатокТорговыйЗал,
		|	ЕСТЬNULL(ТоварыНаСкладах.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ),
		|	ТоварыНаСкладах.Номенклатура.ОбъемДАЛ,
		|	ТоварыНаСкладах.ОстатокСкладБазЕд,
		|	ТоварыНаСкладах.ОстатокТорговыйЗалБазЕд
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура
		|АВТОУПОРЯДОЧИВАНИЕ";
	КонецЕсли;
	
	УчетныеОстатки.Загрузить(Запрос.Выполнить().Выгрузить());
	
	
	
КонецПроцедуры

// Обновляет данные алкогольной продукции в таблице учетных остатков.
//
Процедура ОбновитьДанныеУчетныхОстатков() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетныеОстатки", УчетныеОстатки.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетныеОстатки.НомерСтроки КАК НомерСтроки,
	|	ВЫРАЗИТЬ(УчетныеОстатки.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	УчетныеОстатки.Характеристика КАК Характеристика,
	|	ВЫРАЗИТЬ(УчетныеОстатки.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция,
	|	УчетныеОстатки.ОстатокСкладБазЕд КАК ОстатокСкладБазЕд,
	|	УчетныеОстатки.ОстатокТорговыйЗалБазЕд КАК ОстатокТорговыйЗалБазЕд
	|ПОМЕСТИТЬ УчетныеОстатки
	|ИЗ
	|	&УчетныеОстатки КАК УчетныеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетныеОстатки.НомерСтроки КАК НомерСтроки,
	|	УчетныеОстатки.Номенклатура,
	|	УчетныеОстатки.Характеристика,
	|	ЕСТЬNULL(УчетныеОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ) КАК ПродаетсяВРозлив,
	|	УчетныеОстатки.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	УчетныеОстатки.АлкогольнаяПродукция,
	|	УчетныеОстатки.АлкогольнаяПродукция.ТипПродукции КАК ТипПродукции,
	|	УчетныеОстатки.ОстатокСкладБазЕд,
	|	УчетныеОстатки.ОстатокТорговыйЗалБазЕд,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетныеОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|			ТОГДА УчетныеОстатки.ОстатокСкладБазЕд * УчетныеОстатки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ УчетныеОстатки.ОстатокСкладБазЕд
	|	КОНЕЦ КАК ОстатокСклад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетныеОстатки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|			ТОГДА УчетныеОстатки.ОстатокТорговыйЗалБазЕд * УчетныеОстатки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ УчетныеОстатки.ОстатокТорговыйЗалБазЕд
	|	КОНЕЦ КАК ОстатокТорговыйЗал
	|ИЗ
	|	УчетныеОстатки КАК УчетныеОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	УчетныеОстатки.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// Заполняет таблицу остатков по данным ЕГАИС.
//
Процедура ЗаполнитьОстаткиЕГАИС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОстаткиНаСкладе", ОстаткиНаСкладе);
	Запрос.УстановитьПараметр("ОстаткиВТорговомЗале", ОстаткиВТорговомЗале);
	Запрос.УстановитьПараметр("КорректироватьОстаткиНемаркируемойПродукции", КорректироватьОстаткиНемаркируемойПродукции);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ВложенныйЗапрос.ОстатокСклад) КАК ОстатокСклад,
	|	СУММА(ВложенныйЗапрос.ОстатокТорговыйЗал) КАК ОстатокТорговыйЗал
	|ПОМЕСТИТЬ ОстаткиЕГАИС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|		СУММА(ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество) КАК ОстатокСклад,
	|		0 КАК ОстатокТорговыйЗал
	|	ИЗ
	|		Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	|	ГДЕ
	|		ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &ОстаткиНаСкладе
	|		И ВЫБОР
	|				КОГДА НЕ ЕСТЬNULL(ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция.ВидПродукции.Маркируемый, ЛОЖЬ)
	|					ТОГДА &КорректироватьОстаткиНемаркируемойПродукции
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция,
	|		0,
	|		СУММА(ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество)
	|	ИЗ
	|		Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	|	ГДЕ
	|		ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &ОстаткиВТорговомЗале
	|		И ВЫБОР
	|				КОГДА НЕ ЕСТЬNULL(ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция.ВидПродукции.Маркируемый, ЛОЖЬ)
	|					ТОГДА &КорректироватьОстаткиНемаркируемойПродукции
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))) КАК Номенклатура,
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))) КАК Характеристика,
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)) КАК ПродаетсяВРозлив,
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.Номенклатура.ОбъемДАЛ, 0)) КАК ОбъемДАЛ,
	|	ОстаткиЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиЕГАИС.АлкогольнаяПродукция.ТипПродукции КАК ТипПродукции,
	|	ОстаткиЕГАИС.ОстатокСклад КАК ОстатокСклад,
	|	ОстаткиЕГАИС.ОстатокТорговыйЗал КАК ОстатокТорговыйЗал
	|ИЗ
	|	ОстаткиЕГАИС КАК ОстаткиЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ОстаткиЕГАИС.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиЕГАИС.АлкогольнаяПродукция,
	|	ОстаткиЕГАИС.ОстатокСклад,
	|	ОстаткиЕГАИС.ОстатокТорговыйЗал,
	|	ОстаткиЕГАИС.АлкогольнаяПродукция.ТипПродукции
	|
	|УПОРЯДОЧИТЬ ПО
	|	АлкогольнаяПродукция
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ОстаткиЕГАИС.Загрузить(Запрос.Выполнить().Выгрузить());
	
	
КонецПроцедуры

// Рассчитывает корректировочное количество, необходимое для создания документов.
//
Процедура РассчитатьКоличествоКорректировки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетныеОстатки", УчетныеОстатки.Выгрузить());
	Запрос.УстановитьПараметр("ОстаткиЕГАИС", ОстаткиЕГАИС.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(УчетныеОстатки.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	УчетныеОстатки.Характеристика КАК Характеристика,
	|	УчетныеОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	УчетныеОстатки.ОстатокСклад КАК ОстатокСклад,
	|	УчетныеОстатки.ОстатокТорговыйЗал КАК ОстатокТорговыйЗал
	|ПОМЕСТИТЬ УчетныеОстатки
	|ИЗ
	|	&УчетныеОстатки КАК УчетныеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ОстаткиЕГАИС.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ОстаткиЕГАИС.Характеристика КАК Характеристика,
	|	ОстаткиЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиЕГАИС.ОстатокСклад КАК ОстатокСклад,
	|	ОстаткиЕГАИС.ОстатокТорговыйЗал КАК ОстатокТорговыйЗал
	|ПОМЕСТИТЬ ОстаткиЕГАИС
	|ИЗ
	|	&ОстаткиЕГАИС КАК ОстаткиЕГАИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.АлкогольнаяПродукция,
	|	СУММА(ВложенныйЗапрос.ОстатокСклад) КАК ОстатокСклад,
	|	СУММА(ВложенныйЗапрос.ОстатокТорговыйЗал) КАК ОстатокТорговыйЗал,
	|	СУММА(ВложенныйЗапрос.ОстатокСкладЕГАИС) КАК ОстатокСкладЕГАИС,
	|	СУММА(ВложенныйЗапрос.ОстатокТорговыйЗалЕГАИС) КАК ОстатокТорговыйЗалЕГАИС
	|ПОМЕСТИТЬ СводныеОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		УчетныеОстатки.Номенклатура КАК Номенклатура,
	|		УчетныеОстатки.Характеристика КАК Характеристика,
	|		УчетныеОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|		УчетныеОстатки.ОстатокСклад КАК ОстатокСклад,
	|		УчетныеОстатки.ОстатокТорговыйЗал КАК ОстатокТорговыйЗал,
	|		0 КАК ОстатокСкладЕГАИС,
	|		0 КАК ОстатокТорговыйЗалЕГАИС
	|	ИЗ
	|		УчетныеОстатки КАК УчетныеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиЕГАИС.Номенклатура,
	|		ОстаткиЕГАИС.Характеристика,
	|		ОстаткиЕГАИС.АлкогольнаяПродукция,
	|		0,
	|		0,
	|		ОстаткиЕГАИС.ОстатокСклад,
	|		ОстаткиЕГАИС.ОстатокТорговыйЗал
	|	ИЗ
	|		ОстаткиЕГАИС КАК ОстаткиЕГАИС) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АлкогольнаяПродукция,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СводныеОстатки.Номенклатура,
	|	СводныеОстатки.Характеристика,
	|	СводныеОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СводныеОстатки.ОстатокСклад,
	|	СводныеОстатки.ОстатокТорговыйЗал,
	|	СводныеОстатки.ОстатокСкладЕГАИС,
	|	СводныеОстатки.ОстатокТорговыйЗалЕГАИС,
	|	ВЫБОР
	|		КОГДА СводныеОстатки.ОстатокСкладЕГАИС - СводныеОстатки.ОстатокСклад > 0
	|			ТОГДА СводныеОстатки.ОстатокСкладЕГАИС - СводныеОстатки.ОстатокСклад
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПередатьВТорговыйЗал,
	|	ВЫБОР
	|		КОГДА СводныеОстатки.ОстатокТорговыйЗалЕГАИС - СводныеОстатки.ОстатокТорговыйЗал < СводныеОстатки.ОстатокСклад - СводныеОстатки.ОстатокСкладЕГАИС
	|			ТОГДА СводныеОстатки.ОстатокТорговыйЗалЕГАИС - СводныеОстатки.ОстатокТорговыйЗал
	|		ИНАЧЕ СводныеОстатки.ОстатокСклад - СводныеОстатки.ОстатокСкладЕГАИС
	|	КОНЕЦ КАК ВернутьИзТорговогоЗала
	|ПОМЕСТИТЬ ПередачаВТорговыйЗал
	|ИЗ
	|	СводныеОстатки КАК СводныеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПередачаВТорговыйЗал.Номенклатура КАК Номенклатура,
	|	ПередачаВТорговыйЗал.Характеристика,
	|	ПередачаВТорговыйЗал.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ПередачаВТорговыйЗал.ОстатокСклад,
	|	ПередачаВТорговыйЗал.ОстатокТорговыйЗал,
	|	ПередачаВТорговыйЗал.ОстатокСкладЕГАИС,
	|	ПередачаВТорговыйЗал.ОстатокТорговыйЗалЕГАИС,
	|	ПередачаВТорговыйЗал.ПередатьВТорговыйЗал,
	|	ПередачаВТорговыйЗал.ВернутьИзТорговогоЗала,
	|	ПередачаВТорговыйЗал.ОстатокТорговыйЗал - ПередачаВТорговыйЗал.ОстатокТорговыйЗалЕГАИС - ВЫБОР
	|		КОГДА ПередачаВТорговыйЗал.ПередатьВТорговыйЗал > 0
	|			ТОГДА ПередачаВТорговыйЗал.ПередатьВТорговыйЗал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоставитьНаБалансВТорговыйЗал,
	|	ПередачаВТорговыйЗал.ПередатьВТорговыйЗал + ПередачаВТорговыйЗал.ОстатокТорговыйЗалЕГАИС - ПередачаВТорговыйЗал.ОстатокТорговыйЗал - ВЫБОР
	|		КОГДА ПередачаВТорговыйЗал.ВернутьИзТорговогоЗала > 0
	|			ТОГДА ПередачаВТорговыйЗал.ВернутьИзТорговогоЗала
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СписатьИзТорговогоЗала,
	|	ПередачаВТорговыйЗал.ОстатокСклад - ПередачаВТорговыйЗал.ОстатокСкладЕГАИС - ВЫБОР
	|		КОГДА ПередачаВТорговыйЗал.ВернутьИзТорговогоЗала > 0
	|			ТОГДА ПередачаВТорговыйЗал.ВернутьИзТорговогоЗала
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоставитьНаБалансНаСклад,
	|	ПередачаВТорговыйЗал.ОстатокСкладЕГАИС - ПередачаВТорговыйЗал.ОстатокСклад - ВЫБОР
	|		КОГДА ПередачаВТорговыйЗал.ПередатьВТорговыйЗал > 0
	|			ТОГДА ПередачаВТорговыйЗал.ПередатьВТорговыйЗал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СписатьСоСклада
	|ПОМЕСТИТЬ ТаблицаКорректировки
	|ИЗ
	|	ПередачаВТорговыйЗал КАК ПередачаВТорговыйЗал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ) КАК ПродаетсяВРозлив,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	ТаблицаКорректировки.ОстатокСклад,
	|	ТаблицаКорректировки.ОстатокТорговыйЗал,
	|	ТаблицаКорректировки.ОстатокСкладЕГАИС,
	|	ТаблицаКорректировки.ОстатокТорговыйЗалЕГАИС,
	|	ВЫБОР
	|		КОГДА ТаблицаКорректировки.ПередатьВТорговыйЗал > 0
	|			ТОГДА ТаблицаКорректировки.ПередатьВТорговыйЗал
	|		КОГДА ТаблицаКорректировки.ВернутьИзТорговогоЗала > 0
	|			ТОГДА -ТаблицаКорректировки.ВернутьИзТорговогоЗала
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПередатьВТорговыйЗал,
	|	ВЫБОР
	|		КОГДА ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал > 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал
	|		КОГДА ТаблицаКорректировки.СписатьИзТорговогоЗала > 0
	|			ТОГДА -ТаблицаКорректировки.СписатьИзТорговогоЗала
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоставитьНаБалансВТорговыйЗал,
	|	ВЫБОР
	|		КОГДА ТаблицаКорректировки.ПоставитьНаБалансНаСклад > 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансНаСклад
	|		КОГДА ТаблицаКорректировки.СписатьСоСклада > 0
	|			ТОГДА -ТаблицаКорректировки.СписатьСоСклада
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоставитьНаБалансНаСклад
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПродаетсяВРозлив УБЫВ,
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ТаблицаКорректировки.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// Создает документы корректировки остатков на основании расчетной таблицы.
//
Процедура СоздатьДокументыКорректировкиОстатков() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаКорректировки", ТаблицаКорректировки.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаКорректировки.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика КАК Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаКорректировки.ПередатьВТорговыйЗал,
	|	ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал,
	|	ТаблицаКорректировки.ПоставитьНаБалансНаСклад
	|ПОМЕСТИТЬ ТаблицаКорректировки
	|ИЗ
	|	&ТаблицаКорректировки КАК ТаблицаКорректировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПередатьВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПередатьВТорговыйЗал
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПередатьВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПередатьВТорговыйЗал
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ) КАК ПродаетсяВРозлив,
	|	ТаблицаКорректировки.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	ТаблицаКорректировки.ПередатьВТорговыйЗал > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	-ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПередатьВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПередатьВТорговыйЗал
	|	КОНЕЦ КАК Количество,
	|	-ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПередатьВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПередатьВТорговыйЗал
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	ТаблицаКорректировки.ПередатьВТорговыйЗал < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	-ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал
	|	КОНЕЦ КАК Количество,
	|	-ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	ТаблицаКорректировки.ПоставитьНаБалансВТорговыйЗал < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансНаСклад / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансНаСклад
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансНаСклад / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансНаСклад
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	ТаблицаКорректировки.ПоставитьНаБалансНаСклад > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКорректировки.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировки.Характеристика,
	|	ТаблицаКорректировки.АлкогольнаяПродукция,
	|	-ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансНаСклад / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансНаСклад
	|	КОНЕЦ КАК Количество,
	|	-ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|				И ТаблицаКорректировки.Номенклатура.ОбъемДАЛ <> 0
	|			ТОГДА ТаблицаКорректировки.ПоставитьНаБалансНаСклад / ТаблицаКорректировки.Номенклатура.ОбъемДАЛ
	|		ИНАЧЕ ТаблицаКорректировки.ПоставитьНаБалансНаСклад
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ТаблицаКорректировки.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ) КАК ПродаетсяВРозлив,
	|	ТаблицаКорректировки.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ
	|ИЗ
	|	ТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	ТаблицаКорректировки.ПоставитьНаБалансНаСклад < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ОстаткиНаСкладе);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество КАК Количество,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.СправкаБ КАК СправкаБ,
	|	ЕСТЬNULL(ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.СправкаБ.ДокументОснование.ДатаТТН, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаТТН
	|ИЗ
	|	Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	|ГДЕ
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	АлкогольнаяПродукция,
	|	ДатаТТН";
	
	ОстаткиСправокБ = Запрос.Выполнить().Выгрузить();
	
	Если НЕ МассивРезультатов[1].Пустой() Тогда
		СоздатьПередачуВТорговыйЗал(МассивРезультатов[1], ОстаткиСправокБ);
	КонецЕсли;
	
	Если НЕ МассивРезультатов[2].Пустой() Тогда
		СоздатьВозвратИзТорговогоЗала(МассивРезультатов[2]);
	КонецЕсли;
	
	Если НЕ МассивРезультатов[3].Пустой() Тогда
		СоздатьАктПостановкиНаБалансВТорговыйЗал(МассивРезультатов[3]);
	КонецЕсли;
	
	Если НЕ МассивРезультатов[4].Пустой() Тогда
		СоздатьАктСписанияИзТорговогоЗала(МассивРезультатов[4]);
	КонецЕсли;
	
	Если НЕ МассивРезультатов[5].Пустой() Тогда
		СоздатьАктПостановкиНаБалансНаСклад(МассивРезультатов[5]);
	КонецЕсли;
	
	Если НЕ МассивРезультатов[6].Пустой() Тогда
		СоздатьАктСписанияСоСклада(МассивРезультатов[6], ОстаткиСправокБ);
	КонецЕсли;
	
КонецПроцедуры

// Создает документ передачи в торговый зал ЕГАИС.
//
Процедура СоздатьПередачуВТорговыйЗал(РезультатЗапроса, ОстаткиСправокБ)
	
	Док = Документы.ПередачаВТорговыйЗалЕГАИС.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Заполнить(ЭтотОбъект);
	
	Док.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	Док.Магазин = Магазин;
	Док.Организация = Организация;
	Док.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "СкладПродажи");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПередатьВТорговыйЗал = ?(Выборка.ПродаетсяВРозлив, Выборка.Количество * Выборка.ОбъемДАЛ, Выборка.Количество);
		
		МассивСправок = ОстаткиСправокБ.НайтиСтроки(Новый Структура("АлкогольнаяПродукция", Выборка.АлкогольнаяПродукция));
		Для Каждого СтрокаСправки Из МассивСправок Цикл
			ТекКоличество = Мин(СтрокаСправки.Количество, ПередатьВТорговыйЗал);
			Если ТекКоличество > 0 Тогда
				СтрокаТЧ = Док.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
				СтрокаТЧ.Количество = ?(Выборка.ПродаетсяВРозлив, ТекКоличество / Выборка.ОбъемДАЛ, ТекКоличество);
				СтрокаТЧ.КоличествоУпаковок = ?(Выборка.ПродаетсяВРозлив, ТекКоличество / Выборка.ОбъемДАЛ, ТекКоличество);
				СтрокаТЧ.СправкаБ = СтрокаСправки.СправкаБ;
				
				ПередатьВТорговыйЗал = ПередатьВТорговыйЗал - ТекКоличество;
				СтрокаСправки.Количество = СтрокаСправки.Количество - ТекКоличество;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаписатьДокументКорректировкиОстатков(Док);
	
КонецПроцедуры

// Создает документ возврата из торгового зала ЕГАИС.
//
Процедура СоздатьВозвратИзТорговогоЗала(РезультатЗапроса)
	
	Док = Документы.ВозвратИзТорговогоЗалаЕГАИС.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Заполнить(ЭтотОбъект);
	
	Док.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	Док.Магазин = Магазин;
	Док.Организация = Организация;
	Док.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "СкладПоступления");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = Док.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
	КонецЦикла;
	
	ЗаписатьДокументКорректировкиОстатков(Док);
	
КонецПроцедуры

// Создает акт постановки на баланс в торговый зал ЕГАИС.
//
Процедура СоздатьАктПостановкиНаБалансВТорговыйЗал(РезультатЗапроса)
	
	Док = Документы.АктПостановкиНаБалансЕГАИС.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Заполнить(ЭтотОбъект);
	
	Док.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВТорговомЗале;
	Док.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.ЗакупкаДо2016;
	
	Док.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	Док.Магазин = Магазин;
	Док.Организация = Организация;
	Док.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "СкладПродажи");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = Док.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
	КонецЦикла;
	
	ЗаписатьДокументКорректировкиОстатков(Док);
	
КонецПроцедуры

// Создает акт списания из торгового зала ЕГАИС.
//
Процедура СоздатьАктСписанияИзТорговогоЗала(РезультатЗапроса)
	
	Док = Документы.АктСписанияЕГАИС.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Заполнить(ЭтотОбъект);
	
	Док.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзТорговогоЗала;
	Док.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Реализация;
	
	Док.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	Док.Магазин = Магазин;
	Док.Организация = Организация;
	Док.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "СкладПродажи");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = Док.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
	КонецЦикла;
	
	ЗаписатьДокументКорректировкиОстатков(Док);
	
КонецПроцедуры

// Создает акт постановки на баланс на склад ЕГАИС.
//
Процедура СоздатьАктПостановкиНаБалансНаСклад(РезультатЗапроса)
	
	Док = Документы.АктПостановкиНаБалансЕГАИС.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Заполнить(ЭтотОбъект);
	
	Док.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБаланс;
	Док.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.ЗакупкаДо2016;
	
	Док.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	Док.Магазин = Магазин;
	Док.Организация = Организация;
	Док.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "СкладПоступления");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = Док.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
	КонецЦикла;
	
	ЗаписатьДокументКорректировкиОстатков(Док);
	
КонецПроцедуры

// Создает акт списания со склада ЕГАИС.
//
Процедура СоздатьАктСписанияСоСклада(РезультатЗапроса, ОстаткиСправокБ)
	
	Док = Документы.АктСписанияЕГАИС.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Заполнить(ЭтотОбъект);
	
	Док.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписания;
	Док.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Недостача;
	
	Док.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	Док.Магазин = Магазин;
	Док.Организация = Организация;
	Док.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "СкладПоступления");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СписатьСоСклада = ?(Выборка.ПродаетсяВРозлив, Выборка.Количество * Выборка.ОбъемДАЛ, Выборка.Количество);
		
		МассивСправок = ОстаткиСправокБ.НайтиСтроки(Новый Структура("АлкогольнаяПродукция", Выборка.АлкогольнаяПродукция));
		Для Каждого СтрокаСправки Из МассивСправок Цикл
			ТекКоличество = Мин(СтрокаСправки.Количество, СписатьСоСклада);
			Если ТекКоличество > 0 Тогда
				СтрокаТЧ = Док.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
				СтрокаТЧ.Количество = ?(Выборка.ПродаетсяВРозлив, ТекКоличество / Выборка.ОбъемДАЛ, ТекКоличество);
				СтрокаТЧ.КоличествоУпаковок = ?(Выборка.ПродаетсяВРозлив, ТекКоличество / Выборка.ОбъемДАЛ, ТекКоличество);
				СтрокаТЧ.СправкаБ = СтрокаСправки.СправкаБ;
				
				СписатьСоСклада = СписатьСоСклада - ТекКоличество;
				СтрокаСправки.Количество = СтрокаСправки.Количество - ТекКоличество;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаписатьДокументКорректировкиОстатков(Док);
	
КонецПроцедуры

// Записывает документ корректировки остатков.
//
Процедура ЗаписатьДокументКорректировкиОстатков(Док)
	
	Док.Записать();
	
	СтрокаТЧ = СозданныеДокументы.Добавить();
	СтрокаТЧ.ДокументСсылка = Док.Ссылка;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли         		