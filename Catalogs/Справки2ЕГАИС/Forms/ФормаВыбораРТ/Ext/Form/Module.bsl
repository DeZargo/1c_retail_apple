
#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОповещениеЗапросСправкиЕГАИСНачало(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросСправкиБ");
		
		ВходныеПараметры = ИнтеграцияЕГАИСКлиентСервер.ПараметрыИсходящегоЗапроса(ВидДокумента);
		ВходныеПараметры.РегистрационныйНомер = Результат;
		
		ДополнительныеПараметры = Новый Структура("РегистрационныйНомер", Результат);
		
		ИнтеграцияЕГАИСКлиент.НачатьФормированиеИсходящегоЗапроса(
			Новый ОписаниеОповещения("ОповещениеЗапросСправкиЕГАИСЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			ВидДокумента,
			ВходныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеЗапросСправкиЕГАИСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		НоваяСправка = СоздатьНовуюСправкуНаСервере(Результат, ДополнительныеПараметры);
		ОповеститьОбИзменении(НоваяСправка);
		ПозиционироватьНаСправке(НоваяСправка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.ОрганизацияЕГАИС.Пустая() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ОрганизацияЕГАИС", Параметры.ОрганизацияЕГАИС);
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("АлкогольнаяПродукция") Тогда
		Заголовок = НСтр("ru = 'Справки 2 для'") + " " + Строка(Параметры.Отбор.АлкогольнаяПродукция);
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("СправкаА") Тогда
		УстановитьОтборПоСправкеА();
	ИначеЕсли Параметры.Свойство("Номенклатура")
		И Параметры.Свойство("Характеристика") Тогда
		УстановитьОтборПоНоменклатуре();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапросСправкиЕГАИС(Команда)
	
	НомерСправки = "";
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеЗапросСправкиЕГАИСНачало", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВводСтроки(ОписаниеОповещения, НомерСправки, НСтр("ru = 'Введите регистрационный номер справки Б'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоСправкеА()
	
	Если ЗначениеЗаполнено(Параметры.СправкаА) Тогда
		НомерСправкиА = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.СправкаА, "РегистрационныйНомер");
		
		ОбщегоНазначенияРТКлиентСервер.ИзменитьЭлементОтбораСписка(
			Список,
			"НомерСправкиА",
			НомерСправкиА,
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто);
			
		Элементы.НадписьОтбораПоНоменклатуре.Видимость = Истина;
		СтрокаОтбораПоНоменклатуре = НСтр("ru = 'Отбор по справке А: %1'");
		НадписьОтбораПоНоменклатуре = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаОтбораПоНоменклатуре, НомерСправкиА);
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоНоменклатуре()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СоответствиеЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	               |ИЗ
	               |	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеЕГАИС
	               |ГДЕ
	               |	СоответствиеЕГАИС.Номенклатура = &Номенклатура
	               |	И СоответствиеЕГАИС.Характеристика = &Характеристика";
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Параметры.Характеристика);
	МассивПродукции = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("АлкогольнаяПродукция");
	
	Если МассивПродукции.Количество() > 0 Тогда
		ОбщегоНазначенияРТКлиентСервер.ИзменитьЭлементОтбораСписка(
			Список,
			"АлкогольнаяПродукция",
			МассивПродукции,
			Истина,
			ВидСравненияКомпоновкиДанных.ВСписке,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
			
		Элементы.НадписьОтбораПоНоменклатуре.Видимость = Истина;
		СтрокаОтбораПоНоменклатуре = НСтр("ru = 'Соответствующие номенклатуре: %1'");
		Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
			ПредставлениеНоменклатуры = Параметры.Номенклатура + " / " + Параметры.Характеристика;
		Иначе
			ПредставлениеНоменклатуры = Параметры.Номенклатура;
		КонецЕсли;
		НадписьОтбораПоНоменклатуре = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаОтбораПоНоменклатуре, ПредставлениеНоменклатуры);
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Функция СоздатьНовуюСправкуНаСервере(Результат, ДополнительныеПараметры)
	
	ДанныеСправки = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправкиБ();
	ДанныеСправки.РегистрационныйНомер = ДополнительныеПараметры.РегистрационныйНомер;
	ДанныеСправки.Наименование = ДополнительныеПараметры.РегистрационныйНомер;
	
	ТекстОшибки = "";
	НоваяСправка = ИнтеграцияЕГАИС.СоздатьСправку(ДанныеСправки, Перечисления.ВидыДокументовЕГАИС.СправкаБ, Неопределено, ТекстОшибки);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Возврат НоваяСправка;
	
КонецФункции

&НаКлиенте
Процедура ПозиционироватьНаСправке(НоваяСправка)
	
	Элементы.Список.ТекущаяСтрока = НоваяСправка;
	
	Если Элементы.Список.ТекущаяСтрока <> НоваяСправка Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Справка с указанным кодом не соответствует установленному отбору'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

