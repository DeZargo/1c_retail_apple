
#Область ПрограммныйИнтерфейс

// См. ОбменСКонтрагентамиПереопределяемый.ПолучитьАктуальныеВидыЭД
// Заполняет массив актуальными видами электронных документов для прикладного решения.
//
// Параметры:
//  Массив - Массив - виды актуальных ЭД.
//
Процедура ПолучитьАктуальныеВидыЭД(Массив) Экспорт
	
	Массив.Добавить(Перечисления.ВидыЭД.АктЗаказчик);
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12Покупатель);
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12Продавец);
	Массив.Добавить(Перечисления.ВидыЭД.СчетФактура);
	Массив.Добавить(Перечисления.ВидыЭД.КорректировочныйСчетФактура);
	Массив.Добавить(Перечисления.ВидыЭД.КаталогТоваров);
	Массив.Добавить(Перечисления.ВидыЭД.ПрайсЛист);
	Массив.Добавить(Перечисления.ВидыЭД.ЗаказТовара);
	Массив.Добавить(Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара);
	Массив.Добавить(Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьПараметрыЭДПоИсточнику
// Определяет параметры электронного документа по типу владельца.
//
// Параметры:
//  Источник - Объект, ЛюбаяСсылка - документ или справочник источника.
//  ПараметрыЭД - Структура - параметры источника, необходимых для определения
//                настроек обмена ЭД. Обязательные параметры: НаправлениеЭД, ВидЭД,
//                Контрагент, СоглашениеЭД или Организация.
//  ФорматCML - Булево - если истина, то для формирования ЭД будут использоваться схемы CML (не ФНС),
//    в параметрах должны быть указаны соответствующие виды ЭД.
//
Процедура ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД, ФорматCML = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипИсточника = ТипЗнч(Источник);
	ЭтоСсылка = ОбщегоНазначения.ЭтоСсылка(ТипИсточника);
	
	Если ТипИсточника = Тип("ДокументСсылка.ПоступлениеТоваров")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ПоступлениеТоваров") Тогда
		
		Если ФорматCML Тогда
			ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		Иначе
			ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель;
		КонецЕсли;
		НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		Если ЭтоСсылка Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, Контрагент");
			Организация = ЗначенияРеквизитов.Организация;
			Контрагент = ЗначенияРеквизитов.Контрагент;
		Иначе
			Организация = Источник.Организация;
			Контрагент = Источник.Контрагент;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураВыданный")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Если ЭтоСсылка Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, 
				"Организация, Контрагент, ДокументОснование");
			Организация = ЗначенияРеквизитов.Организация;
			Контрагент  = ЗначенияРеквизитов.Контрагент;
			ДокументОснование = ЗначенияРеквизитов.ДокументОснование;
		Иначе
			Организация = Источник.Организация;
			Контрагент  = Источник.Контрагент;
			ДокументОснование = Источник.ДокументОснование;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураПолученный")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		
		Если ЭтоСсылка Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СчетФактураПолученный.Организация КАК Организация
			|ИЗ
			|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
			|ГДЕ
			|	СчетФактураПолученный.Ссылка = &Источник
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СчетФактураПолученныйДокументыОснования.ДокументОснование КАК ДокументОснование
			|ИЗ
			|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
			|ГДЕ
			|	СчетФактураПолученныйДокументыОснования.Ссылка = &Источник";
			Запрос.УстановитьПараметр("Источник", Источник);
			РезультатыЗапроса = Запрос.ВыполнитьПакет();
			Реквизиты = РезультатыЗапроса[0].Выбрать();
			Реквизиты.Следующий();
			
			Организация = Реквизиты.Организация;
			ДокументыОснования = РезультатыЗапроса[1].Выгрузить();
		Иначе
			Организация = Источник.Организация;
			ДокументыОснования = Источник.ДокументыОснования;
		КонецЕсли;
		
		Для Каждого Строка Из ДокументыОснования Цикл
			Если ЗначениеЗаполнено(Строка.ДокументОснование) Тогда
				
				РеквизитыОснования = Новый Структура("Контрагент");
				ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.ДокументОснование, РеквизитыОснования);
				Контрагент = ЗначенияРеквизитов.Контрагент;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаказПоставщику")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаказПоставщику") Тогда
		
		ВидЭД = Перечисления.ВидыЭД.ЗаказТовара;
		НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Если ЭтоСсылка Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, Контрагент");
			Организация = ЗначенияРеквизитов.Организация;
			Контрагент = ЗначенияРеквизитов.Контрагент;
		Иначе
			Организация = Источник.Организация;
			Контрагент = Источник.Контрагент;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ОтчетКомитентуОПродажах") Тогда
		
		ВидЭД = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара;
		НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Если ЭтоСсылка Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, Контрагент");
			Организация = ЗначенияРеквизитов.Организация;
			Контрагент = ЗначенияРеквизитов.Контрагент;
		Иначе
			Организация = Источник.Организация;
			Контрагент = Источник.Контрагент;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ОтчетКомитентуОСписании")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ОтчетКомитентуОСписании") Тогда
		
		ВидЭД = Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара;
		НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Если ЭтоСсылка Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, Контрагент");
			Организация = ЗначенияРеквизитов.Организация;
			Контрагент = ЗначенияРеквизитов.Контрагент;
		Иначе
			Организация = Источник.Организация;
			Контрагент = Источник.Контрагент;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		
		Если ФорматCML Тогда
			ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		Иначе
			ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец;
		КонецЕсли;
		
		НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Если ЭтоСсылка Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, Контрагент");
			Организация = ЗначенияРеквизитов.Организация;
			Контрагент = ЗначенияРеквизитов.Контрагент;
		Иначе
			Организация = Источник.Организация;
			Контрагент = Источник.Контрагент;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыЭД.ВидЭД = ВидЭД;
	ПараметрыЭД.НаправлениеЭД = НаправлениеЭД;
	ПараметрыЭД.Организация = Организация;
	ПараметрыЭД.Контрагент = Контрагент;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ОпределитьДокументЯвляетсяСчетомФактурой
// Определяет является ли документ информационной базы счет-фактурой.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - документ информационной базы.
//  Результат - Булево - является ли документ счет-фактурой.
//
Процедура ОпределитьДокументЯвляетсяСчетомФактурой(ДокументСсылка, Результат) Экспорт
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный")
			ИЛИ ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Результат = Истина;
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Определение соответствий объектов библиотеки ЭД и прикладного решения.

// См. ОбменСКонтрагентамиПереопределяемый.ОпределитьИмяРеквизитаВладельцаНоменклатурыПоставщиков
// Определяет имя реквизита владельца справочника НоменклатураПоставщика.
//
// Параметры:
//  ИмяРеквизитаВладельца - строка - имя реквизита владельца.
//
Процедура ОпределитьИмяРеквизитаВладельцаНоменклатурыПоставщиков(ИмяРеквизитаВладельца) Экспорт
	
	ИмяРеквизитаВладельца = "Контрагент";
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПолучитьСоответствиеПеречислений
// Получает значение перечисления по имени объектов метаданных.
// 
// Параметры:
//  СоответствиеПеречислений - Соответствие - соответствие библиотечных и прикладных перечислений.
//
Процедура ПолучитьСоответствиеПеречислений(СоответствиеПеречислений) Экспорт
	
	СоответствиеПеречислений.Вставить("НДС", "СтавкиНДС");
	СоответствиеПеречислений.Вставить("ЮрФизЛицо", "ЮрФизЛицо");
	СоответствиеПеречислений.Вставить("ФормыОплаты", "ФормыОплаты");
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьСоответствиеСтавокНДС
// В процедуре указывается соответствие строковых представлений ставок НДС (используемые в БЭД)
// с прикладными значениями этих ставок.
//
// Параметры:
//   Соответствие - Соответствие - заполняемое соответствие ставок НДС.
//
// Пример:
//   Соответствие.Вставить("0",       Перечисления.СтавкиНДС.НДС0);
//   Соответствие.Вставить("10",      Перечисления.СтавкиНДС.НДС10);
//   Соответствие.Вставить("18",      Перечисления.СтавкиНДС.НДС18);
//   Соответствие.Вставить("10/110",  Перечисления.СтавкиНДС.НДС10_110);
//   Соответствие.Вставить("18/118",  Перечисления.СтавкиНДС.НДС18_118);
//   Соответствие.Вставить("20",      Перечисления.СтавкиНДС.НДС20);
//   Соответствие.Вставить("20/120",  Перечисления.СтавкиНДС.НДС20_120);
//   Соответствие.Вставить("без НДС", Перечисления.СтавкиНДС.БезНДС);
//
Процедура ЗаполнитьСоответствиеСтавокНДС(Соответствие) Экспорт
	
	Соответствие.Вставить("0",       Перечисления.СтавкиНДС.НДС0);
	Соответствие.Вставить("10",      Перечисления.СтавкиНДС.НДС10);
	Соответствие.Вставить("18",      Перечисления.СтавкиНДС.НДС18);
	Соответствие.Вставить("20",      Перечисления.СтавкиНДС.НДС20);
	Соответствие.Вставить("10/110",  Перечисления.СтавкиНДС.НДС10_110);
	Соответствие.Вставить("18/118",  Перечисления.СтавкиНДС.НДС18_118);
	Соответствие.Вставить("20/120",  Перечисления.СтавкиНДС.НДС20_120);
	Соответствие.Вставить("без НДС", Перечисления.СтавкиНДС.БезНДС);
	Соответствие.Вставить("НДС исчисляется налоговым агентом", Перечисления.СтавкиНДС.БезНДС);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Настройка обмена ЭД.

// См. ОбменСКонтрагентамиПереопределяемый.ДополнительнаяАналитикаКонтрагентовСправочникПартнеры
// Процедура возвращает признак использования справочника Партнеров в качестве
// дополнительной аналитики к справочнику Контрагенты.
//
// Параметры:
//  ИспользуетсяСправочникПартнеры - Булево - флаг использования в библиотеке справочника Партнеры.
//
Процедура ДополнительнаяАналитикаКонтрагентовСправочникПартнеры(ИспользуетсяСправочникПартнеры) Экспорт
	
	ИспользуетсяСправочникПартнеры = Ложь;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ДополнительнаяАналитикаСправочникХарактеристикиНоменклатуры
// Процедура возвращает признак использования справочника "Характеристики номенклатуры" в качестве
// дополнительной аналитики к справочнику Номенклатура.
//
// Параметры:
//  ИспользуетсяСправочникХарактеристикиНоменклатуры - Булево - флаг использования справочника "Характеристики номенклатуры".
//
Процедура ДополнительнаяАналитикаСправочникХарактеристикиНоменклатуры(ИспользуетсяСправочникХарактеристикиНоменклатуры) Экспорт
	
	ИспользуетсяСправочникХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ДополнительнаяАналитикаСправочникУпаковкиНоменклатуры
// Процедура возвращает признак использования справочника "Упаковка номенклатуры" в качестве
// дополнительной аналитики к справочнику Номенклатура.
//
// Параметры:
//  ИспользуетсяСправочникУпаковкиНоменклатуры - Булево - флаг использования справочника "Упаковки номенклатуры".
//
Процедура ДополнительнаяАналитикаСправочникУпаковкиНоменклатуры(ИспользуетсяСправочникУпаковкиНоменклатуры) Экспорт
	
	ИспользуетсяСправочникУпаковкиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПодготовитьСтруктуруОбъектовКомандЭДО
// Заполняет список команд ЭДО в прикладном решении.
// 
// Параметры:
//  СоставКомандЭДО - Структура - структура состава команд ЭДО.
//    Исходящие - Массив - состав объектов, например "Документ.РеализацияТоваровУслуг".
//    Входящие - Массив - состав объектов.
//    БезПодписи - Массив - состав объектов для обмена без ЭП.
//    Интеркампани - Массив - состав объектов Интеркампани.
//
Процедура ПодготовитьСтруктуруОбъектовКомандЭДО(СоставКомандЭДО) Экспорт
	
	СоставКомандЭДО.Исходящие.Добавить("Документ.ЗаказПоставщику");
	СоставКомандЭДО.Исходящие.Добавить("Документ.ВозвратТоваровПоставщику");
	СоставКомандЭДО.Исходящие.Добавить("Документ.ОтчетКомитентуОПродажах");
	СоставКомандЭДО.Исходящие.Добавить("Документ.ОтчетКомитентуОСписании");
	СоставКомандЭДО.Исходящие.Добавить("Документ.СчетФактураВыданный");
	
	СоставКомандЭДО.Входящие.Добавить("Документ.ПоступлениеТоваров");
	СоставКомандЭДО.Входящие.Добавить("Документ.СчетФактураПолученный");
	
	СоставКомандЭДО.БезПодписи.Добавить("Документ.ЗаказПоставщику");
	СоставКомандЭДО.БезПодписи.Добавить("Документ.ВозвратТоваровПоставщику");
	СоставКомандЭДО.БезПодписи.Добавить("Документ.ПоступлениеТоваров");
	
КонецПроцедуры


#Область СопоставлениеНоменклатуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриОпределенииСтруктурыНоменклатурыИнформационнойБазы.
// Определяет взаимосвязь между частями номенклатуры информационной базы.
// Устанавливает зависимость характеристик и упаковок от номенклатуры.
// Используется при заполнении свойства "СвязиПараметровВыбора" в элементах формы.
//
// Параметры:
//  СтруктураНоменклатуры - Структура - описание структуры номенклатуры информационной базы (для изменения):
//   * ИмяПараметраСвязиХарактеристики - Строка - имя параметра связи выбора характеристики по номенклатуре. По умолчанию "Отбор.Владелец".
//   * ИмяПараметраСвязиУпаковки - Строка - имя параметра связи выбора упаковки по номенклатуре. По умолчанию "Отбор.Владелец".
//
Процедура ПриОпределенииСтруктурыНоменклатурыИнформационнойБазы(СтруктураНоменклатуры) Экспорт
	
	СтруктураНоменклатуры.ИмяПараметраСвязиХарактеристики = "Номенклатура";
	СтруктураНоменклатуры.ИмяПараметраСвязиУпаковки 	  = "Номенклатура";
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриОпределенииСвойствНоменклатурыИнформационнойБазы.
// Определяет свойства экземпляра номенклатуры информационной базы.
// Устанавливает обязательность использования характеристик и упаковок номенклатуры.
//
// Параметры:
//  НоменклатураИБ - Структура - элемент номенклатуры информационной базы. 
//                               См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//  Свойства - Структура - свойства элемента номенклатуры информационной базы (для изменения):
//   * ИспользоватьХарактеристики - Булево - признак использования характеристик. По умолчанию Ложь.
//   * ИспользоватьУпаковки - Булево - признак использования упаковок. По умолчанию Ложь.
//
Процедура ПриОпределенииСвойствНоменклатурыИнформационнойБазы(Знач НоменклатураИБ, Свойства) Экспорт
	
	Если Не ЗначениеЗаполнено(НоменклатураИБ.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = НоменклатураИБ.Номенклатура;
	
	Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		И ЗначениеЗаполнено(Номенклатура) Тогда
		
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура,"ВидНоменклатуры");
		ИспользоватьХарактеристики = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры,"ИспользоватьХарактеристики");
		Свойства.ИспользоватьХарактеристики = ИспользоватьХарактеристики;
		Свойства.ИспользоватьУпаковки = Ложь;
		
	Иначе
		Свойства.ИспользоватьХарактеристики = Ложь;
		Свойства.ИспользоватьУпаковки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриОпределенииРеквизитовНоменклатурыИнформационнойБазы.
// Определяет реквизиты экземпляра номенклатуры информационной базы.
//
// Параметры:
//  НоменклатураИБ - Структура - элемент номенклатуры информационной базы. 
//                               См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//  Реквизиты - Структура - реквизиты элемента номенклатуры информационной базы (для изменения):
//   * Наименование - Строка - наименование номенклатуры.
//   * Артикул - Строка - артикул номенклатуры.
//   * СтавкаНДС - Произвольный - ставка НДС номенклатуры.  Значение из соответствия, заданного в методе
//                                ОбменСКонтрагентамиПереопределяемый.ЗаполнитьСоответствиеСтавокНДС.
//
Процедура ПриОпределенииРеквизитовНоменклатурыИнформационнойБазы(Знач НоменклатураИБ, Реквизиты) Экспорт
	
	Если Не ЗначениеЗаполнено(НоменклатураИБ.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Значения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураИБ.Номенклатура, "Наименование,Артикул,СтавкаНДС");
	ЗаполнитьЗначенияСвойств(Реквизиты, Значения);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриОпределенииПредставленийСопоставленияНоменклатуры.
// Определяет заголовки форм и полей сопоставления номенклатуры,
// которые могут зависеть от прикладного решения и его настроек.
//
// Параметры:
//  Представления - Структура - набор представлений, которые можно переопределить.
//   * ВладелецНоменклатурыПредставлениеОбъекта - Строка - представление поля владельца номенклатуры.
//                                                         По умолчанию берется из определяемого типа ВладелецНоменклатурыБЭД.
//   * НоменклатураПредставлениеОбъекта - Строка - представление поля номенклатуры.
//                                                 По умолчанию берется из определяемого типа НоменклатураБЭД.
//   * ХарактеристикаПредставлениеОбъекта - Строка - представление поля характеристики номенклатуры.
//                                                   По умолчанию берется из определяемого типа ХарактеристикаНоменклатурыБЭД.
//   * УпаковкаПредставлениеОбъекта - Строка - представление поля упаковки номенклатуры.
//                                             По умолчанию берется из определяемого типа УпаковкаНоменклатурыБЭД.
//   * НоменклатураКонтрагентаПредставлениеСписка - Строка - представление списка номенклатуры контрагентов.
//                                                           По умолчанию "Номенклатура контрагентов".
//   * НоменклатураКонтрагентаПредставлениеОбъекта - Строка - представление объекта номенклатуры контрагентов.
//                                                            По умолчанию "Номенклатура контрагента".
//
// Пример:
//  Представления.НоменклатураКонтрагентаПредставлениеСписка = НСтр("ru = 'Номенклатура партнеров'");
//  Представления.НоменклатураКонтрагентаПредставлениеОбъекта = НСтр("ru = 'Номенклатура партнера'");
//  Представления.ВладелецНоменклатурыПредставлениеОбъекта = НСтр("ru = 'Партнер'");
//
Процедура ПриОпределенииПредставленийСопоставленияНоменклатуры(Представления) Экспорт
	
	Представления.ХарактеристикаПредставлениеОбъекта = НСтр("ru = 'Характеристика'"); // Вместо "Характеристика номенклатуры".
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриПодбореВариантовСопоставленияНоменклатуры.
// Выполняется при подборе вариантов сопоставления номенклатуры контрагента и информационной базы.
//
// Параметры:
//  НаборНоменклатурыКонтрагентов - Массив - набор номенклатуры контрагентов, для которой нужно подобрать варианты.
//                                           См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента
//  Варианты - Массив - варианты сопоставления (для изменения). Элементы представлены структурой:
//   * НоменклатураКонтрагента - Структура - элемент из параметра НаборНоменклатурыКонтрагентов.
//   * НоменклатураИБ - Структура - подобранный вариант номенклатуры ИБ.
//                                  См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//   * Наименование - Строка - наименование номенклатуры в ИБ.
//   * Артикул - Строка - артикул номенклатуры в ИБ.
//   * Штрихкод - Строка - штрихкод номенклатуры в ИБ.
//  СтандартнаяОбработка - Булево - признак использования стандартных (библиотечных) алгоритмов поиска. По умолчанию Истина.
//                                  Поддерживаются алгоритм полнотекстового поиска по наименованию номенклатуры.
//
Процедура ПриПодбореВариантовСопоставленияНоменклатуры(Знач НаборНоменклатурыКонтрагентов, Варианты, СтандартнаяОбработка = Истина) Экспорт
	
	// Замер производительности.
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ОбменСКонтрагентамиРТ.ПриПодбореВариантовСопоставленияНоменклатуры");
	
	// Ищем номенклатуру по штрихкоду.
	
	Штрихкоды = Новый Массив;
	Для Каждого Элемент Из НаборНоменклатурыКонтрагентов Цикл
		Если ЗначениеЗаполнено(Элемент.Штрихкод) Тогда
			НаборШтрихкодов = СтрРазделить(Элемент.Штрихкод, ",");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Штрихкоды, НаборШтрихкодов, Истина);
			Элемент.Вставить("НаборШтрихкодов", НаборШтрихкодов);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Штрихкоды.Владелец КАК Номенклатура,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Владелец.Наименование КАК Наименование,
	|	Штрихкоды.Владелец.Артикул КАК Артикул,
	|	Штрихкоды.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Штрихкод В(&Штрихкоды)
	|	И Штрихкоды.Владелец ССЫЛКА Справочник.Номенклатура";
	Запрос.УстановитьПараметр("Штрихкоды", Штрихкоды);
	
	ТаблицаВариантов = Запрос.Выполнить().Выгрузить();
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, 1, НСтр("ru = 'Запрос штрихкодов'"));
	
	Для Каждого НоменклатураКонтрагента Из НаборНоменклатурыКонтрагентов Цикл
		
		Если Не ЗначениеЗаполнено(НоменклатураКонтрагента.Штрихкод) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборШтрихкодов = НоменклатураКонтрагента.НаборШтрихкодов;
		
		Для каждого СтрокаВарианта Из ТаблицаВариантов Цикл
			
			Если НаборШтрихкодов.Найти(СтрокаВарианта.Штрихкод) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоменклатураИБ = ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы(
				СтрокаВарианта.Номенклатура, СтрокаВарианта.Характеристика, СтрокаВарианта.Упаковка);
			
			Вариант = Новый Структура;
			Вариант.Вставить("НоменклатураКонтрагента", НоменклатураКонтрагента);
			Вариант.Вставить("НоменклатураИБ", НоменклатураИБ);
			Вариант.Вставить("Наименование", СтрокаВарианта.Наименование);
			Вариант.Вставить("Артикул", СтрокаВарианта.Артикул);
			Вариант.Вставить("Штрихкод", СтрокаВарианта.Штрихкод);
			
			Варианты.Добавить(Вариант);
			
		КонецЦикла;
		
	КонецЦикла;
	
	КоличествоПовторяющихсяДанных = НаборНоменклатурыКонтрагентов.Количество();
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных / 10);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриПодбореНоменклатурыИнформационнойБазы.
// Выполняется при подборе недостающих данных о номенклатуре информационной базы по уже имеющимся,
// а также номенклатуре контрагента.
//
// Параметры:
//  НоменклатураКонтрагента - Структура - данные номенклатуры контрагента. 
//                                        См. ОбменСКонтрагентамиСлужебныйКлиентСервер.НоваяНоменклатураКонтрагента.
//  НоменклатураИБ - Структура - данные о номенклатуре информационной базы (имеющиеся или требующие подбора).
//                               См. ОбменСКонтрагентамиСлужебныйКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//
Процедура ПриПодбореНоменклатурыИнформационнойБазы(Знач НоменклатураКонтрагента, НоменклатураИБ) Экспорт
	
	Если ЗначениеЗаполнено(НоменклатураИБ.Номенклатура) Тогда
		Если НЕ ЗначениеЗаполнено(НоменклатураИБ.Упаковка) Тогда
			ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоменклатураИБ.Номенклатура, "ЕдиницаИзмерения");
			Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
				НоменклатураИБ.Упаковка = ЕдиницаИзмерения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоменклатураИБ.Упаковка)  Тогда
		
		Если ЗначениеЗаполнено(НоменклатураКонтрагента.ЕдиницаИзмеренияКод) Тогда
			НоменклатураИБ.Упаковка = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("БазовыеЕдиницыИзмерения", 
				"Код", НоменклатураКонтрагента.ЕдиницаИзмеренияКод);
		ИначеЕсли ЗначениеЗаполнено(НоменклатураКонтрагента.ЕдиницаИзмерения) Тогда
			НоменклатураИБ.Упаковка = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("БазовыеЕдиницыИзмерения", 
				"Наименование", НоменклатураКонтрагента.ЕдиницаИзмерения);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НоменклатураИБ.Номенклатура)
		И Не ЗначениеЗаполнено(НоменклатураИБ.Характеристика)
		И ЗначениеЗаполнено(НоменклатураКонтрагента.Характеристика) Тогда
		
		ЗначенияРеквизитовНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураИБ.Номенклатура, 
			"ВидНоменклатуры");
		ВидНоменклатуры = ЗначенияРеквизитовНоменклатуры.ВидНоменклатуры;
		
		ЗначенияРеквизитовВидаНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидНоменклатуры, 
			"ИспользованиеХарактеристик");
		ИспользованиеХарактеристик = ЗначенияРеквизитовВидаНоменклатуры.ИспользованиеХарактеристик;
		
		Если ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ИндивидуальныеДляНоменклатуры Тогда
			НоменклатураИБ.Характеристика = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("ХарактеристикиНоменклатуры", 
				"Наименование", НоменклатураКонтрагента.Характеристика, НоменклатураИБ.Номенклатура);
		ИначеЕсли ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры Тогда
			НоменклатураИБ.Характеристика = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("ХарактеристикиНоменклатуры", 
				"Наименование", НоменклатураКонтрагента.Характеристика, ВидНоменклатуры);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриЗаполненииФормыНоменклатурыПоДаннымКонтрагента.
// Заполняет форму номенклатуру по данным контрагента.
//
// Параметры:
//  НоменклатураКонтрагента - Структура - данные контрагента для заполнения формы номенклатуры.
//                                        См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента.
//  Форма - УправляемаяФорма, Форма - форма номенклатуры, которую нужно заполнить.
//
Процедура ПриЗаполненииФормыНоменклатурыПоДаннымКонтрагента(Знач НоменклатураКонтрагента, Форма) Экспорт
	
	Форма.Объект.Наименование 		= НоменклатураКонтрагента.Наименование;
	Форма.Объект.НаименованиеПолное = НоменклатураКонтрагента.Наименование;
		
	Если ЗначениеЗаполнено(НоменклатураКонтрагента.Характеристика) Тогда
		
		Форма.Объект.Наименование = Форма.Объект.Наименование 
			+ " (" + НоменклатураКонтрагента.Характеристика + ")";
		
	КонецЕсли;
	
	Форма.Объект.Артикул = НоменклатураКонтрагента.Артикул;
	
	Если ЗначениеЗаполнено(НоменклатураКонтрагента.ЕдиницаИзмеренияКод) Тогда
		Форма.Объект.ЕдиницаИзмерения = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("БазовыеЕдиницыИзмерения", 
			"Код", НоменклатураКонтрагента.ЕдиницаИзмеренияКод);
	ИначеЕсли ЗначениеЗаполнено(НоменклатураКонтрагента.ЕдиницаИзмерения) Тогда
		Форма.Объект.ЕдиницаИзмерения = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("БазовыеЕдиницыИзмерения", 
			"Наименование", НоменклатураКонтрагента.ЕдиницаИзмерения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НоменклатураКонтрагента.СтавкаНДС) Тогда
		СтавкиНДСПоКлючу = Новый Соответствие;
		ЗаполнитьСоответствиеСтавокНДС(СтавкиНДСПоКлючу);
		Для каждого КлючЗначение Из СтавкиНДСПоКлючу Цикл
			Если КлючЗначение.Ключ = НоменклатураКонтрагента.СтавкаНДС Тогда
				Форма.Объект.СтавкаНДС = КлючЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриЗаполненииФормыХарактеристикиПоДаннымКонтрагента.
// Заполняет форму характеристики по данным контрагента.
//
// Параметры:
//  НоменклатураКонтрагента - Структура - данные контрагента для заполнения формы характеристики.
//                                        См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента.
//  Форма - УправляемаяФорма - форма характеристики, которую нужно заполнить.
//
Процедура ПриЗаполненииФормыХарактеристикиПоДаннымКонтрагента(Знач НоменклатураКонтрагента, Форма) Экспорт
	
	Форма.Объект.Наименование = НоменклатураКонтрагента.Характеристика;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриОтраженииВУчетеЭлектронногоДокументаСНоменклатурой.
Процедура ПриОтраженииВУчетеЭлектронногоДокументаСНоменклатурой(Знач Документ, Настройки) Экспорт
	
	Если Документ.СпособОбработки = "НоменклатураПоставщиков" Тогда
		Настройки.ОтражатьТолькоСопоставленные = Ложь;
	КонецЕсли;
	
КонецПроцедуры


// Создает номенклатуру по данным контрагента.
//
// Параметры:
//  НаборНоменклатурыКонтрагентов - Массив - номенклатура контрагентов, по которой нужно создать номенклатуру информационной базы.
//                                           См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента.
//  ЗначенияПоУмолчанию - Структура - значения реквизитов по умолчанию.
//
// Возвращаемое значение:
//  Массив - описания созданной номенклатуры. Элементы представлены структурой:
//   * НоменклатураКонтрагента - Структура - элемент из параметра НаборНоменклатурыКонтрагентов, для которого удалось создать номенклатуру.
//   * НоменклатураИБ - Структура - описание созданной номенклатуры. См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//
Функция СоздатьНоменклатуруПоДаннымКонтрагента(Знач НаборНоменклатурыКонтрагентов, Знач ЗначенияПоУмолчанию) Экспорт
	
	Результат = Новый Массив;
	
	Для каждого НоменклатураКонтрагента Из НаборНоменклатурыКонтрагентов Цикл
		
		НоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НоваяНоменклатура, ЗначенияПоУмолчанию);
		НоваяНоменклатура.Наименование = НоменклатураКонтрагента.Наименование;
		НоваяНоменклатура.НаименованиеПолное = НоменклатураКонтрагента.Наименование;
		НоваяНоменклатура.Артикул = НоменклатураКонтрагента.Артикул;
		ЗначенияРеквизитовВидаНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоваяНоменклатура.ВидНоменклатуры, 
			"ТипНоменклатуры");
		ТипНоменклатуры = ЗначенияРеквизитовВидаНоменклатуры.ТипНоменклатуры;
		НоваяНоменклатура.ТипНоменклатуры = ТипНоменклатуры;
		
		Если Не ЗначениеЗаполнено(НоваяНоменклатура.ЕдиницаИзмерения) Тогда
			Если ЗначениеЗаполнено(НоменклатураКонтрагента.ЕдиницаИзмеренияКод) Тогда
				НоваяНоменклатура.ЕдиницаИзмерения = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("БазовыеЕдиницыИзмерения",
					"Код", НоменклатураКонтрагента.ЕдиницаИзмеренияКод);
			ИначеЕсли ЗначениеЗаполнено(НоменклатураКонтрагента.ЕдиницаИзмерения) Тогда
				НоваяНоменклатура.ЕдиницаИзмерения = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("БазовыеЕдиницыИзмерения",
					"Наименование", НоменклатураКонтрагента.ЕдиницаИзмерения);
			КонецЕсли;
		КонецЕсли;
		
		Если Не НоваяНоменклатура.ПроверитьЗаполнение() Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяНоменклатура.Записать();
		
		НоменклатураИБ = ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы(НоваяНоменклатура.Ссылка);
		
		СозданныйЭлемент = Новый Структура;
		СозданныйЭлемент.Вставить("НоменклатураКонтрагента", НоменклатураКонтрагента);
		СозданныйЭлемент.Вставить("НоменклатураИБ", НоменклатураИБ);
		Результат.Добавить(СозданныйЭлемент);
		
	КонецЦикла;
	
	// Удалим сообщения, возникшие при проверке заполнения номенклатуры.
	// Остальные сообщения выводим как есть.
	НаборСообщений = ПолучитьСообщенияПользователю(Истина);
	Для каждого Сообщение Из НаборСообщений Цикл
		Если Сообщение.КлючДанных <> Справочники.Номенклатура.ПустаяСсылка() Тогда
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти

#Область НоменклатураКонтрагентов

// Заполняет представление номенклатуры контрагента в источнике, 
// используя полученные из него данные о владельце номенклатуры и ее идентификаторе.
//
// Параметры:
//  Источник - Произвольный - объект (или данные формы), в котором нужно заполнить представление номенклатуры контрагента.
//  ПутьКВладельцу - Строка - путь к свойству источника, содержащему информацию о владельце номенклатуры.
//  ПутьКТаблице - Строка - путь к свойству источника, содержащему таблицу с номенклатурой контрагента.
//  ПутьКИдентификатору - Строка - путь к полю таблицы (ПутьКТаблице), содержащему идентификатор номенклатуры контрагента.
//  ПутьКПредставлению - Строка - путь к полю таблицы (ПутьКТаблице), в которое нужно поместить представление номенклатуры контрагента.
//
// Пример:
//  ОбменСКонтрагентамиРТ.ЗаполнитьПредставлениеНоменклатурыКонтрагентовВИсточнике(
//		Объект, "Контрагент", "Товары", "ИдентификаторНоменклатурыПоставщика", "НаименованиеНоменклатурыПоставщика");
//
Процедура ЗаполнитьПредставлениеНоменклатурыКонтрагентовВИсточнике(Источник, ПутьКВладельцу, ПутьКТаблице, ПутьКИдентификатору, ПутьКПредставлению) Экспорт
	
	Если Источник[ПутьКТаблице].Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборНоменклатурыКонтрагентов = Новый Массив;
	Для Каждого СтрокаТаблицы Из Источник[ПутьКТаблице] Цикл
		
		НоменклатураКонтрагента = ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента(
			Источник[ПутьКВладельцу], СтрокаТаблицы[ПутьКИдентификатору]);
			
		НаборНоменклатурыКонтрагентов.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	Отбор = Новый Структура("НоменклатураКонтрагента", НаборНоменклатурыКонтрагентов);
	Сопоставление = ОбменСКонтрагентами.НайтиСоответствиеНоменклатуры(Отбор);
	
	Для Каждого СтрокаТаблицы Из Источник[ПутьКТаблице] Цикл
		
		Для Каждого Элемент Из Сопоставление Цикл
			
			Если СтрокаТаблицы[ПутьКИдентификатору] <> Элемент.НоменклатураКонтрагента.Идентификатор Тогда
				Продолжить;
			КонецЕсли;
			
			Представление = ОбменСКонтрагентамиРТКлиентСервер.ПредставлениеНоменклатурыКонтрагента(Элемент.НоменклатураКонтрагента);
			СтрокаТаблицы[ПутьКПредставлению] = Представление;
			Прервать;
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы[ПутьКИдентификатору]) 
			И Не ЗначениеЗаполнено(СтрокаТаблицы[ПутьКПредставлению]) Тогда
			СтрокаТаблицы[ПутьКПредставлению] = СтрокаТаблицы[ПутьКИдентификатору];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет идентификатор и представление номенклатуры контрагента в источнике,
// используя полученные из него данные о номенклатуре информационной базы.
//
// Параметры:
//  Источник - Произвольный - объект (или данные формы), в котором нужно заполнить данные номенклатуры контрагента.
//  ПутьКВладельцу - Строка - путь к свойству источника, содержащему информацию о владельце номенклатуры.
//  ПутьКТаблице - Строка - путь к свойству источник, содержащему таблицу с номенклатурой контрагента.
//  ПутьКИдентификатору - Строка - путь к полю таблицы (ПутьКТаблице), в которое нужно поместить идентификатор номенклатуры контрагента.
//  ПутьКПредставлению - Строка - путь к полю таблицы (ПутьКТаблице), в которое нужно поместить представление номенклатуры контрагента.
//
// Пример:
//  ОбменСКонтрагентамиРТ.ПерезаполнитьИдентификаторыКонтрагентаВИсточнике(
//		Объект, "Контрагент", "Товары", "ИдентификаторНоменклатурыПоставщика", "НаименованиеНоменклатурыПоставщика");
//
Процедура ПерезаполнитьИдентификаторыКонтрагентаВИсточнике(Источник, ПутьКВладельцу, ПутьКТаблице, ПутьКИдентификатору, ПутьКПредставлению = Неопределено) Экспорт
	
	УстанавливатьПредставление = НЕ (ПутьКПредставлению = Неопределено);
	НаборНоменклатурыИБ = Новый Массив;
	
	Для каждого СтрокаТаблицы Из Источник[ПутьКТаблице] Цикл
		
		НоменклатураИБ = ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы(
			СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Характеристика, СтрокаТаблицы.Упаковка);
		
		НаборНоменклатурыИБ.Добавить(НоменклатураИБ);
		СтрокаТаблицы[ПутьКИдентификатору] = "";
		Если УстанавливатьПредставление Тогда
			СтрокаТаблицы[ПутьКПредставлению] = "";
		КонецЕсли;
		
	КонецЦикла;
	
	Отбор = Новый Структура("Владелец,НоменклатураИБ", Источник[ПутьКВладельцу], НаборНоменклатурыИБ);
	Сопоставление = ОбменСКонтрагентами.НайтиСоответствиеНоменклатуры(Отбор);
	
	Для каждого СтрокаТаблицы Из Источник[ПутьКТаблице] Цикл
		
		ЕстьСопоставление = Ложь;
		
		Для каждого Элемент Из Сопоставление Цикл
			
			Если СтрокаТаблицы.Номенклатура = Элемент.НоменклатураИБ.Номенклатура
				И СтрокаТаблицы.Характеристика = Элемент.НоменклатураИБ.Характеристика
				И СтрокаТаблицы.Упаковка = Элемент.НоменклатураИБ.Упаковка Тогда
				
				Если ЕстьСопоставление Тогда // неоднозначное сопоставление.
					СтрокаТаблицы[ПутьКИдентификатору] = "";
					Если УстанавливатьПредставление Тогда
						СтрокаТаблицы[ПутьКПредставлению] = "";
					КонецЕсли;
					Прервать;
				Иначе
					ЕстьСопоставление = Истина;
					СтрокаТаблицы[ПутьКИдентификатору] = Элемент.НоменклатураКонтрагента.Идентификатор;
					Если УстанавливатьПредставление Тогда
						Представление = ОбменСКонтрагентамиРТКлиентСервер.ПредставлениеНоменклатурыКонтрагента(Элемент.НоменклатураКонтрагента);
						СтрокаТаблицы[ПутьКПредставлению] = Представление;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если УстанавливатьПредставление Тогда
			Если ЗначениеЗаполнено(СтрокаТаблицы[ПутьКИдентификатору]) 
				И Не ЗначениеЗаполнено(СтрокаТаблицы[ПутьКПредставлению]) Тогда
				СтрокаТаблицы[ПутьКПредставлению] = СтрокаТаблицы[ПутьКИдентификатору];
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет идентификатор и представление номенклатуры контрагента в строке источника,
// используя полученные из нее данные о номенклатуре информационной базы.
//
// Параметры:
//  Источник - Произвольный - объект (или данные формы), которому принадлежит заполняемая строка таблицы.
//  СтрокаИсточника - Произвольный - строка таблицы (ДанныеФормыЭлементКоллекции), в котором нужно заполнить данные номенклатуры контрагента.
//  ПутьКВладельцу - Строка - путь к свойству источника, содержащему информацию о владельце номенклатуры.
//  ПутьКИдентификатору - Строка - путь к полю строки (СтрокаИсточника), в которое нужно поместить идентификатор номенклатуры контрагента.
//  ПутьКПредставлению - Строка - путь к полю строки (СтрокаИсточника), в которое нужно поместить представление номенклатуры контрагента.
//
// Пример:
//  ОбменСКонтрагентамиРТ.ЗаполнитьИдентификаторКонтрагентаВСтрокеИсточника(
//		Объект, СтрокаТовара, "Контрагент", "ИдентификаторНоменклатурыПоставщика", "НаименованиеНоменклатурыПоставщика");
//
Процедура ЗаполнитьИдентификаторКонтрагентаВСтрокеИсточника(Источник, СтрокаИсточника, ПутьКВладельцу, ПутьКИдентификатору, ПутьКПредставлению) Экспорт
	
	НоменклатураИБ = ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы(
		СтрокаИсточника.Номенклатура, СтрокаИсточника.Характеристика, СтрокаИсточника.Упаковка);
		
	Отбор = Новый Структура("Владелец,НоменклатураИБ", Источник[ПутьКВладельцу], НоменклатураИБ);
	Сопоставление = ОбменСКонтрагентами.НайтиСоответствиеНоменклатуры(Отбор);
	
	Если Сопоставление.Количество() = 1 Тогда
		Элемент = Сопоставление[0];
		СтрокаИсточника[ПутьКИдентификатору] = Элемент.НоменклатураКонтрагента.Идентификатор;
		Представление = ОбменСКонтрагентамиРТКлиентСервер.ПредставлениеНоменклатурыКонтрагента(Элемент.НоменклатураКонтрагента);
		СтрокаИсточника[ПутьКПредставлению] = Представление;
	ИначеЕсли Сопоставление.Количество() = 0 Тогда
		СтрокаИсточника[ПутьКИдентификатору] = "";
		СтрокаИсточника[ПутьКПредставлению] = "";
	КонецЕсли;
	
КонецПроцедуры

// Проверяет корректность сопоставление с номенклатурой поставщика.
//
// Параметры:
//  ТаблицаТоваров                   - ТаблицаЗначений - Сведения о товарах для проверки сопоставления.
//  ДеревоДокумента                  - ДеревоЗначений - Данные файла электронного документа.
//  НеУдалосьСопоставитьНоменклатуру - Булево - Признак сопоставления номенклатуры. Если истина, то номенклатура не сопоставлена.
//
Процедура ПроверитьСопоставлениеСНоменклатуройПоставщика(ТаблицаТоваров, ДеревоДокумента, НеУдалосьСопоставитьНоменклатуру)  Экспорт 
	
	ПроверенныеСтроки = Новый Массив;
	Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
		
		Если Не ПроверенныеСтроки.Найти(ТекСтрока.Номенклатура) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Если в таблице товаров несколько строк с одинаковым НомерСтроки,
		// то сопоставлено более одной номенклатуры поставщика.
		ОшибкаСопоставления = ТаблицаТоваров.НайтиСтроки(Новый Структура("Номенклатура, НомерСтроки",
			ТекСтрока.Номенклатура, ТекСтрока.НомерСтроки)).Количество() > 1;
			
		Если Не ЗначениеЗаполнено(ТекСтрока.ИдентификаторНоменклатурыПоставщика) Или ОшибкаСопоставления Тогда
			
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ИдентификаторНоменклатурыПоставщика) Тогда
				ШаблонСообщения = НСтр("ru = 'Номенклатура ""%1"" не сопоставлена с номенклатурой контрагента.
				|Выполните сопоставление и повторите попытку.'");
				
			Иначе
				ШаблонСообщения = НСтр("ru = 'Установлено неоднозначное сопоставление номенклатуры ""%1"" с номенклатурой контрагента.
				|Проверьте, что выбранной номенклатуре соответствует только одна номенклатура контрагента.'");
				ПроверенныеСтроки.Добавить(ТекСтрока.Номенклатура);
				
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
				Строка(ТекСтрока.Номенклатура) + ?(ЗначениеЗаполнено(ТекСтрока.Характеристика), " (" + ТекСтрока.Характеристика + ")", ""));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, НеУдалосьСопоставитьНоменклатуру);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеЭлектронныхДокументов

// Заполняет данные участника ЭДО в дерево данных.
//
// Параметры:
//  ДеревоДанных        - ДеревоЗначений - Данные для формирования электронного документа.
//  СведенияОбУчастнике - Структура - Информация об участнике ЭДО.
//  ВидУчастника        - Строка - Роль участника сделки.
//  ФорматCML           - Булево - Признак формирования электронного документа в формате CML.
//  ВидАдреса           - Строка - Вид адреса для заполнения в электронный документ.
//     Принимает значения: ЮридическийАдрес, ФактическийАдрес, ПочтовыйАдрес, ПроизвольныйАдрес.
//
Процедура ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ФорматCML = Ложь, ВидАдреса = "ЮридическийАдрес") Экспорт 
	
	// Подготовим параметры обработки ошибок
	КлючДанных = Неопределено;
	СведенияОбУчастнике.Свойство("Ссылка", КлючДанных);
	
	ПараметрыОбработкиОшибокИНН = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		КлючДанных, "Объект.ИНН");
	ПараметрыОбработкиОшибокКПП = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		КлючДанных, "Объект.КПП");
	ПараметрыОбработкиОшибокНаименование = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		КлючДанных, "Объект.НаименованиеПолное");
	
	// Передаем в дерево данные для заполнения адреса.
	ВидКонтактнойИнформации = Неопределено;
	
	ТипУчастникаОрганизация = ТипЗнч(КлючДанных) = Тип("СправочникСсылка.Организации");
	
	Если ВидАдреса = "ПроизвольныйАдрес"
		И ЗначениеЗаполнено(СведенияОбУчастнике.ПроизвольныйАдресЗначенияПолей) Тогда
		
		СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(СведенияОбУчастнике.ПроизвольныйАдресЗначенияПолей);
		
		Если ФорматCML Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".Адрес.Иностранный.КодСтр", СведенияОбАдресе.КодСтраны);
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".Адрес.Иностранный.АдрТекст", СведенияОбАдресе.Представление);
		Иначе
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".Адрес.Иностранный.КодСтраны", СведенияОбАдресе.КодСтраны);
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".Адрес.Иностранный.АдресТекст", СведенияОбАдресе.Представление);
		КонецЕсли;
		
	ИначеЕсли ВидАдреса = "ПочтовыйАдрес"
		И ЗначениеЗаполнено(СведенияОбУчастнике[ВидАдреса]) Тогда
		
		ВидКонтактнойИнформации = ?(ТипУчастникаОрганизация, Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации,
			Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента);
		
	ИначеЕсли ВидАдреса = "ФактическийАдрес" 
		И ЗначениеЗаполнено(СведенияОбУчастнике[ВидАдреса]) Тогда
		
		ВидКонтактнойИнформации = ?(ТипУчастникаОрганизация, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации,
			Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
		
	Иначе
		
		ВидКонтактнойИнформации = ?(ТипУчастникаОрганизация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации,
			Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
	КонецЕсли;
	
	Если ВидКонтактнойИнформации <> Неопределено Тогда
		
		ПостфиксПоляАдрес = ?(ТипУчастникаОрганизация, "Организации", "Контрагента");
		
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
			СведенияОбУчастнике.Ссылка, "КонтактнаяИнформацияПолеЮрАдрес" + ПостфиксПоляАдрес + ".");
		
		ЭлектронноеВзаимодействие.ДобавитьВРеквизитОбработкуОшибки(ДеревоДанных, ВидУчастника + ".Адрес.АвтоматическиЗаполняемый",
			ПараметрыОбработкиОшибок);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			ВидУчастника + ".Адрес.АвтоматическиЗаполняемый.ОбъектКонтактнойИнформации", КлючДанных);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			ВидУчастника + ".Адрес.АвтоматическиЗаполняемый.ВидКонтактнойИнформации", ВидКонтактнойИнформации);
		
	КонецЕсли;
	
	Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
									СведенияОбУчастнике.ИНН, ПараметрыОбработкиОшибокИНН);
									
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
									СведенияОбУчастнике.КПП, ПараметрыОбработкиОшибокКПП);
									
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование, ПараметрыОбработкиОшибокНаименование);
									
	ИначеЕсли СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
	
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование, ПараметрыОбработкиОшибокНаименование);
									
		Если ЗначениеЗаполнено(СведенияОбУчастнике.ЮридическийАдресXML) Тогда
			
			СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(СведенияОбУчастнике.ЮридическийАдресXML);
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация.Страна",
									СведенияОбАдресе.Страна);
		КонецЕсли;
									
	ИначеЕсли СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель И Не ФорматCML Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.ИНН",
									СведенияОбУчастнике.ИНН, ПараметрыОбработкиОшибокИНН);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.Фамилия",
									СведенияОбУчастнике.Фамилия, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.Имя",
									СведенияОбУчастнике.Имя, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.Отчество",
									СведенияОбУчастнике.Отчество, ПараметрыОбработкиОшибокНаименование);
									
		Если ЗначениеЗаполнено(СведенияОбУчастнике.СвидетельствоСерияНомер) Тогда
			Свидетельство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Свидетельство №%1 от %2'"),
							СведенияОбУчастнике.СвидетельствоСерияНомер,
							Формат(СведенияОбУчастнике.СвидетельствоДатаВыдачи, "ДЛФ=D"));
			
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
				КлючДанных, "Объект.СвидетельствоСерияНомер");
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ИП.СвидетельствоОГосРегистрации",
										Свидетельство, ПараметрыОбработкиОшибок);
		КонецЕсли;
		
	Иначе
		
		ПолныйПуть = ВидУчастника + ".ТипУчастника.ФЛ.ПолноеНаименование";
		Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть,
				СведенияОбУчастнике.ПолноеНаименование, ПараметрыОбработкиОшибокНаименование);
		КонецЕсли;
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.ИНН",
									СведенияОбУчастнике.ИНН, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.Фамилия",
									СведенияОбУчастнике.Фамилия, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.Имя",
									СведенияОбУчастнике.Имя, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.Отчество",
									СведенияОбУчастнике.Отчество, ПараметрыОбработкиОшибокНаименование);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СведенияОбУчастнике.Телефоны)
		И ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника + ".Контакт.Телефон") Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".Контакт.Телефон",
			СведенияОбУчастнике.Телефоны, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета)
		И ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника + ".БанковскийСчет") Тогда
		Банк = "";
		БИК = "";
		
		ПараметрыОбработкиОшибок = Неопределено;
		Если СведенияОбУчастнике.Свойство("БанковскийСчет") Тогда
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
				СведенияОбУчастнике.БанковскийСчетСсылка, "Объект.НомерСчета");
		КонецЕсли;
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскийСчет.НомерСчета",
				НомерСчета, ПараметрыОбработкиОшибок);
				
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			ПараметрыОбработкиОшибок = Неопределено;
			Если СведенияОбУчастнике.Свойство("БанкСсылка") Тогда
				ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
					СведенияОбУчастнике.БанкСсылка, "Объект.Наименование");
			КонецЕсли;
				
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскийСчет.НаимБанк",
										Банк, ПараметрыОбработкиОшибок);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			ПараметрыОбработкиОшибок = Неопределено;
			Если СведенияОбУчастнике.Свойство("БанкСсылка") Тогда
				ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
					СведенияОбУчастнике.БанкСсылка, "Объект.Код");
			КонецЕсли;
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскийСчет.БИК",
										БИК, ПараметрыОбработкиОшибок);
		КонецЕсли;
	КонецЕсли;
	
	ПолныйПуть = ВидУчастника + ".Руководитель";
	Значение = "";
	Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть)
		И СведенияОбУчастнике.Свойство("Руководитель", Значение) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Фамилия", Значение.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Имя", Значение.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Отчество", Значение.Отчество);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Должность", Значение.Должность);
	КонецЕсли;
	
	ПолныйПуть = ВидУчастника + ".КодОКПО";
	Значение = "";
	Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть)
		И СведенияОбУчастнике.Свойство("КодПоОКПО", Значение) Тогда
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(КлючДанных,
			"Объект.КодПоОКПО");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть, Значение, 
			ПараметрыОбработкиОшибок);
	КонецЕсли;

	ПолныйПуть = ВидУчастника + ".Комментарий";
	Значение = "";
	Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть)
		И СведенияОбУчастнике.Свойство("Комментарий", Значение) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть, Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ВидАдреса = "ЮридическийАдрес")
	
	// Подготовим параметры обработки ошибок
	КлючДанных = Неопределено;
	СведенияОбУчастнике.Свойство("Ссылка", КлючДанных);
	
	ПараметрыОбработкиОшибокНаименование = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		КлючДанных, "Объект.НаименованиеПолное");
	ПараметрыОбработкиОшибокИНН = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		КлючДанных, "Объект.ИНН");
	ПараметрыОбработкиОшибокКПП = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		КлючДанных, "Объект.КПП");
		
	Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
			СведенияОбУчастнике.ПолноеНаименование, ПараметрыОбработкиОшибокНаименование);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
			СведенияОбУчастнике.ИНН, ПараметрыОбработкиОшибокИНН);

		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
			СведенияОбУчастнике.КПП, ПараметрыОбработкиОшибокКПП);
		
	ИначеЕсли СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИЛ.НаименованиеОрганизации",
			СведенияОбУчастнике.ПолноеНаименование, ПараметрыОбработкиОшибокНаименование);
		
	Иначе
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.ИНН",
			СведенияОбУчастнике.ИНН, ПараметрыОбработкиОшибокИНН);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.Фамилия",
			СведенияОбУчастнике.Фамилия, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.Имя",
			СведенияОбУчастнике.Имя, ПараметрыОбработкиОшибокНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.Отчество",
			СведенияОбУчастнике.Отчество, ПараметрыОбработкиОшибокНаименование);
		
		Если ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		
			Свидетельство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Свидетельство №%1 от %2'"),
				СведенияОбУчастнике.СвидетельствоСерияНомер,
				Формат(СведенияОбУчастнике.СвидетельствоДатаВыдачи, "ДЛФ=D"));
			
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
				КлючДанных, "Объект.СвидетельствоСерияНомер");
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".ТипУчастника.ИП.СвидетельствоОГосРегистрации",
				Свидетельство, ПараметрыОбработкиОшибок);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Передаем в дерево данные для заполнения адреса.
	
	ВидКонтактнойИнформации = Неопределено;
	
	ТипУчастникаОрганизация = ТипЗнч(КлючДанных) = Тип("СправочникСсылка.Организации");
	
	Если ВидАдреса = "ПроизвольныйАдрес"
		И ЗначениеЗаполнено(СведенияОбУчастнике.ПроизвольныйАдресЗначенияПолей) Тогда
		
		СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(СведенияОбУчастнике.ПроизвольныйАдресЗначенияПолей);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			ВидУчастника + ".Адрес.АдресИнформация.КодСтраны", СведенияОбАдресе.КодСтраны);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			ВидУчастника + ".Адрес.АдресИнформация.АдресТекст", СведенияОбАдресе.Представление);
		
	ИначеЕсли ВидАдреса = "ПочтовыйАдрес"
		И ЗначениеЗаполнено(СведенияОбУчастнике[ВидАдреса]) Тогда
		
		ВидКонтактнойИнформации = ?(ТипУчастникаОрганизация, Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации,
			Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента);
		
	ИначеЕсли ВидАдреса = "ФактическийАдрес" 
		И ЗначениеЗаполнено(СведенияОбУчастнике[ВидАдреса]) Тогда
		
		ВидКонтактнойИнформации = ?(ТипУчастникаОрганизация, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации,
			Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
		
	Иначе
		
		ВидКонтактнойИнформации = ?(ТипУчастникаОрганизация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации,
			Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
	КонецЕсли;
	
	Если ВидКонтактнойИнформации <> Неопределено Тогда
		
		ПостфиксПоляАдрес = ?(ТипУчастникаОрганизация, "Организации", "Контрагента");
		
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
			СведенияОбУчастнике.Ссылка, "КонтактнаяИнформацияПолеЮрАдрес" + ПостфиксПоляАдрес + ".");
		
		ЭлектронноеВзаимодействие.ДобавитьВРеквизитОбработкуОшибки(ДеревоДанных, ВидУчастника + ".Адрес.АвтоматическиЗаполняемый",
			ПараметрыОбработкиОшибок);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ВидУчастника + ".Адрес.АвтоматическиЗаполняемый.ОбъектКонтактнойИнформации", КлючДанных);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ВидУчастника + ".Адрес.АвтоматическиЗаполняемый.ВидКонтактнойИнформации", ВидКонтактнойИнформации);
		
	КонецЕсли;
	
	Телефон = "";
	Если СведенияОбУчастнике.Свойство("Телефоны", Телефон) И ЗначениеЗаполнено(Телефон) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".КонтактныеСведения.Телефон",
			Телефон, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	ЭлектроннаяПочта = "";
	Если СведенияОбУчастнике.Свойство("ЭлектроннаяПочта", ЭлектроннаяПочта) И ЗначениеЗаполнено(ЭлектроннаяПочта) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".КонтактныеСведения.ЭлектроннаяПочта",
			ЭлектроннаяПочта, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
		ПараметрыОбработкиОшибок = Неопределено;
		Если СведенияОбУчастнике.Свойство("БанковскийСчет") Тогда
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
				СведенияОбУчастнике.БанковскийСчетСсылка, "Объект.НомерСчета");
		КонецЕсли;
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".БанковскиеРеквизиты.НомерСчета",
			НомерСчета, ПараметрыОбработкиОшибок);
				
		Банк = ""; БИК = ""; КоррСчет = "";
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			ПараметрыОбработкиОшибок = Неопределено;
			Если СведенияОбУчастнике.Свойство("БанкСсылка") Тогда
				ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
					СведенияОбУчастнике.БанкСсылка, "Объект.Наименование");
			КонецЕсли;
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.НаименованиеБанка",
				Банк, ПараметрыОбработкиОшибок);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			ПараметрыОбработкиОшибок = Неопределено;
			Если СведенияОбУчастнике.Свойство("БанкСсылка") Тогда
				ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
					СведенияОбУчастнике.БанкСсылка, "Объект.Код");
			КонецЕсли;
				
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.БИКБанка",
				БИК, ПараметрыОбработкиОшибок);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
			ПараметрыОбработкиОшибок = Неопределено;
			Если СведенияОбУчастнике.Свойство("БанкСсылка") Тогда
				ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
					СведенияОбУчастнике.БанкСсылка, "Объект.КоррСчет");
			КонецЕсли;
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.КорреспондентскийСчетБанка",
				КоррСчет, ПараметрыОбработкиОшибок);
		КонецЕсли;
	КонецЕсли;
	
	КодПоОКПО = "";
	Если СведенияОбУчастнике.Свойство("КодПоОКПО", КодПоОКПО) И ЗначениеЗаполнено(КодПоОКПО) Тогда
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(КлючДанных,
			"Объект.КодПоОКПО");
					
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ВидУчастника + ".КодОКПО", КодПоОКПО, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для обработки ошибок заполнения обязательных полей.
//
// Параметры:
//  ТаблицаТоваров   - ТаблицаЗначений - сведения о товарах для формирования электронного документа.
//  СсылкаНаДокумент - ДокументСсылка - Ссылка на основание электронного документа.
//
Процедура ЗаполнитьПараметрыОбработкиОшибокВТаблицеТоваровCML(ТаблицаТоваров, СсылкаНаДокумент = Неопределено) Экспорт

	// Обработка ошибки через механизм сообщений пользователю.
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"Артикул", "Номенклатура", "Объект.Артикул");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"Наименование", "Номенклатура", "Объект.Наименование");
	
	Если ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		Если ТаблицаТоваров.Колонки.Найти("Количество") <> Неопределено Тогда
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
				"Количество", СсылкаНаДокумент, "Объект.Товары.Количество",, "НомерСтроки");
		КонецЕсли;
		
		Если ТаблицаТоваров.Колонки.Найти("Цена") <> Неопределено Тогда
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
				"Цена", СсылкаНаДокумент, "Объект.Товары.Количество",, "НомерСтроки");
		КонецЕсли;
		
		Если ТаблицаТоваров.Колонки.Найти("Сумма") <> Неопределено Тогда
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
				"Сумма", СсылкаНаДокумент, "Объект.Товары.Сумма",, "НомерСтроки");
		КонецЕсли;
		
		Если ТаблицаТоваров.Колонки.Найти("СуммаСкидки") <> Неопределено Тогда
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
				"СуммаСкидки", СсылкаНаДокумент, "Объект.Товары.Сумма",, "НомерСтроки");
		КонецЕсли;
		
		Если ТаблицаТоваров.Колонки.Найти("СуммаСкидки") <> Неопределено Тогда
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
				"СуммаСкидки", СсылкаНаДокумент, "Объект.Товары.Сумма",, "НомерСтроки");
		КонецЕсли;
	КонецЕсли;
	
	Если ТаблицаТоваров.Колонки.Найти("ЕдиницаИзмеренияКодПоОКЕИ") <> Неопределено Тогда
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
			"ЕдиницаИзмеренияКодПоОКЕИ", "ЕдиницаИзмерения", "Объект.Код");
		
		// Если единица пустая, надо ссылаться на незаполненность самой единицы, а не ее полей.
		//СтрокиСПустойЕдиницей = ТаблицаТоваров.НайтиСтроки(Новый Структура("ЕдиницаИзмерения", Справочники.ЕдиницыИзмерения.ПустаяСсылка()));
		//Если СтрокиСПустойЕдиницей.Количество() Тогда
		//	Если СсылкаНаДокумент <> Неопределено Тогда
		//		// Для документов берем единицу прямо из документа.
		//		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
		//			"ЕдиницаИзмеренияКодПоОКЕИ", СсылкаНаДокумент, "Объект.Товары.ЕдиницаИзмерения",, "НомерСтроки");
		//	Иначе
		//		// Иначе ссылаемся на незаполненность единицы в самой номенклатуре.
		//		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
		//			"ЕдиницаИзмеренияКодПоОКЕИ", "Номенклатура", "Объект.ЕдиницаИзмерения");
		//	КонецЕсли;
		//КонецЕсли;
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"БазоваяЕдиницаКод", "БазоваяЕдиницаСсылка", "Объект.Код");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"БазоваяЕдиницаМеждународноеСокращение", "БазоваяЕдиницаСсылка", "Объект.МеждународноеСокращение");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"БазоваяЕдиницаНаименование", "БазоваяЕдиницаСсылка", "Объект.Наименование");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"БазоваяЕдиницаНаименованиеПолное", "БазоваяЕдиницаСсылка", "Объект.НаименованиеПолное");
	
	// Если единица пустая, надо ссылаться на незаполненность самой единицы, а не ее полей.
	СтрокиСПустойЕдиницей = ТаблицаТоваров.НайтиСтроки(Новый Структура("БазоваяЕдиницаСсылка", 
		Справочники.БазовыеЕдиницыИзмерения.ПустаяСсылка()));
	
	Если СтрокиСПустойЕдиницей.Количество() Тогда
		//Если СсылкаНаДокумент <> Неопределено Тогда
		//	// Для документов берем единицу прямо из документа.
		//	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
		//		"БазоваяЕдиницаКод", СсылкаНаДокумент, "Объект.Товары.ЕдиницаИзмерения",, "НомерСтроки");
		//	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
		//		"БазоваяЕдиницаМеждународноеСокращение", СсылкаНаДокумент, "Объект.Товары.ЕдиницаИзмерения",, "НомерСтроки");
		//	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
		//		"БазоваяЕдиницаНаименование", СсылкаНаДокумент, "Объект.Товары.ЕдиницаИзмерения",, "НомерСтроки");
		//	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
		//		"БазоваяЕдиницаНаименованиеПолное", СсылкаНаДокумент, "Объект.Товары.ЕдиницаИзмерения",, "НомерСтроки");
		//Иначе
			// Это каталог товаров - для него единица берется из номенклатуры.
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
				"БазоваяЕдиницаКод", "Номенклатура", "Объект.ЕдиницаИзмерения");
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
				"БазоваяЕдиницаМеждународноеСокращение", "Номенклатура", "Объект.ЕдиницаИзмерения");
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
				"БазоваяЕдиницаНаименование", "Номенклатура", "Объект.ЕдиницаИзмерения");
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойЕдиницей,
				"БазоваяЕдиницаНаименованиеПолное", "Номенклатура", "Объект.ЕдиницаИзмерения");
		//КонецЕсли;
	КонецЕсли;
	
	Если ТаблицаТоваров.Колонки.Найти("ВалютаЦены") <> Неопределено Тогда
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
			"ВалютаЦены", "Валюта", "Объект.Код");
	//	
	//	// Если валюта пустая, надо ссылаться на незаполненность самой валюты, а не ее полей.
	//	СтрокиСПустойВалютой = ТаблицаТоваров.НайтиСтроки(Новый Структура("Валюта", Справочники.Валюты.ПустаяСсылка()));
	//	Если СтрокиСПустойВалютой.Количество() И СсылкаНаДокумент <> Неопределено Тогда
	//		Если СсылкаНаДокумент <> Неопределено Тогда
	//			// Для документов берем валюту прямо из документа.
	//			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиСПустойВалютой,
	//				"ВалютаЦены", СсылкаНаДокумент, "Объект.Валюта");
	//		КонецЕсли;
	//	КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьВДеревоДанныеСчета(ДеревоДокумента, ДанныеСчета)
	
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.БанковскийСчет, 
		"Объект.НомерСчета");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.НомерСчета",
		ДанныеСчета.НомерСчета, ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.Банк, 
		"Объект.Наименование");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.Наименование",
		ДанныеСчета.БанкНаименование, ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.Банк, 
		"Объект.Код");	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.БИК",
		ДанныеСчета.БанкБИК, ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.Банк, 
		"Объект.КоррСчет");	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.СчетКорреспондентский",
		ДанныеСчета.БанкСчетКорр, ПараметрыОбработкиОшибок);
	
	Если ЗначениеЗаполнено(ДанныеСчета.БанкКоррНаименование) Тогда
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.БанкДляРасчетов, 
			"Объект.Наименование");	
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.Наименование",
			ДанныеСчета.БанкКоррНаименование, ПараметрыОбработкиОшибок);
			
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.БанкДляРасчетов, 
			"Объект.КоррСчет");	
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента,
			"РасчетныйСчет.БанкКорреспондент.СчетКорреспондентский", ДанныеСчета.БанкКоррСчетКорр, ПараметрыОбработкиОшибок);
		
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ДанныеСчета.БанкДляРасчетов, 
			"Объект.Код");	
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.БИК",
			ДанныеСчета.БанкКоррБИК, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПоКаталогуТоваровCML.
Процедура ЗаполнитьДанныеПоКаталогуТоваровCML(Организация, ТоварыКаталога, ДеревоДанных, Отказ) Экспорт
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержитТолькоИзменения", Истина);
	
	СведенияОбОрганизации = Неопределено;
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(Организация, СведенияОбОрганизации);
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбОрганизации, "Владелец", Истина);
	
	ТоварыКаталога.Колонки.Добавить("Сопоставление");
	Для Каждого СтрокаТовара Из ТоварыКаталога Цикл
		
		Сопоставление = Новый Структура;
		Сопоставление.Вставить("Наименование", СтрокаТовара.Наименование);
		Сопоставление.Вставить("Характеристика", СтрокаТовара.ХарактеристикаНаименование);
		Сопоставление.Вставить("ЕдиницаИзмерения", СтрокаТовара.ЕдиницаНаименование);
		Сопоставление.Вставить("Артикул", СтрокаТовара.Артикул);
		Если ТоварыКаталога.Колонки.Найти("Штрихкоды") <> Неопределено Тогда
			Если ТипЗнч(СтрокаТовара.Штрихкоды) = Тип("ТаблицаЗначений") Тогда
				НаборШтрихкодов = СтрокаТовара.Штрихкоды.ВыгрузитьКолонку("Штрихкод");
				Если ЗначениеЗаполнено(НаборШтрихкодов) Тогда
					Сопоставление.Вставить("Штрихкод", НаборШтрихкодов);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Сопоставление.Вставить("НоменклатураИБ", СтрокаТовара.НоменклатураИБ);
		Сопоставление.Вставить("ХарактеристикаИБ", СтрокаТовара.ХарактеристикаИБ);
		Сопоставление.Вставить("УпаковкаИБ", СтрокаТовара.УпаковкаИБ);
		СтрокаТовара.Сопоставление = Сопоставление;
		
	КонецЦикла;
	
	ЗаполнитьПараметрыОбработкиОшибокВТаблицеТоваровCML(ТоварыКаталога);
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТоварыКаталога, "Товары");
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПоЗаказуТоваров.
Процедура ЗаполнитьДанныеПоЗаказуТоваров(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента, Отказ) Экспорт 
	
	СтруктураДанных = Документы[СсылкаНаОбъект.Метаданные().Имя].ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	ТаблицаТоваров = СтруктураДанных.РезультатПоТабличнойЧасти.Выгрузить();
	
	Если ВыполнятьПроверкуСопоставленияНоменклатуры() Тогда
		ПроверитьСопоставлениеСНоменклатуройПоставщика(ТаблицаТоваров, ДеревоДокумента, Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыШапки = СтруктураДанных.РезультатПоШапке.Выбрать();
	РеквизитыШапки.Следующий();
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", РеквизитыШапки.ВалютаКод);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс", 1);
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить сумму документа. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнена колонка ""Сумма""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", РеквизитыШапки.СуммаДокумента,
		ТекстОшибки);
	
	// Заполняем Организацию
	СведенияОбОрганизации = Неопределено; 
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация, СведенияОбОрганизации);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОбОрганизации, "Покупатель", Истина);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОбОрганизации, "Получатель", Истина);
	
	// Заполняем контрагента
	Если ТипЗнч(СтруктураЭД.Контрагент) = Тип("Структура") Тогда
		// Бизнес-сеть.
		ЗаполнитьДанныеУчастника(ДеревоДокумента, СтруктураЭД.Контрагент, "Продавец");
	Иначе
		СведенияОКонтрагенте = Неопределено; 
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент, СведенияОКонтрагенте);
		ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКонтрагенте, "Продавец", Истина);
	КонецЕсли;
	
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		Сопоставление = Новый Структура;
		Если ЗначениеЗаполнено(СтрокаТовара.ИдентификаторНоменклатурыПоставщика) Тогда
			Сопоставление.Вставить("Идентификатор", СтрокаТовара.ИдентификаторНоменклатурыПоставщика);
		КонецЕсли;
		Сопоставление.Вставить("Наименование", СтрокаТовара.Наименование);
		Сопоставление.Вставить("НоменклатураИБ", СтрокаТовара.Номенклатура);
		Сопоставление.Вставить("ХарактеристикаИБ", СтрокаТовара.Характеристика);
		Сопоставление.Вставить("УпаковкаИБ", СтрокаТовара.ЕдиницаИзмерения);
		СтрокаТовара.Сопоставление = Сопоставление;
	КонецЦикла;
	
	ЗаполнитьПараметрыОбработкиОшибокВТаблицеТоваровCML(ТаблицаТоваров);
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");
	СуммаИтог   =  ТаблицаТоваров.Итог("Сумма");
	СкидкаИтог  =  ТаблицаТоваров.Итог("СуммаСкидки");
	НалогиИтог  =  ТаблицаТоваров.Итог("СуммаНДС");
	// Заполняем банковский счет
	Если ЗначениеЗаполнено(РеквизитыШапки.НомерСчета) Тогда
		ДобавитьВДеревоДанныеСчета(ДеревоДокумента, РеквизитыШапки);
	КонецЕсли;
	
	// заполним строку Итогов
	СуммаИтог = ТаблицаТоваров.Итог("Сумма");
	
	ТекстОшибки = НСтр("ru = 'Не удалось итоговые стоимостные показатели. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнена колонка ""Сумма""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", РеквизитыШапки.СуммаДокумента,
		ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаИтог", 
		СуммаИтог, ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаНалогаИтог",
		НалогиИтог, ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.ЦенаВключаетНДС",
		РеквизитыШапки.ЦенаВключаетНДС);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаБезСкидкиИтог",
		СуммаИтог + СкидкаИтог, ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаСкидкиИтог",
		СкидкаИтог, ТекстОшибки);
	
	// Заполнение данных о доставке.
	АдресДоставки = Новый Структура("АдресДоставки", РеквизитыШапки.АдресДоставки);
	АдресДоставкиЗначенияПолей = Новый Структура("АдресДоставкиЗначенияПолей", РеквизитыШапки.АдресДоставкиЗначенияПолей);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Доставка.СпособДоставки",
		РеквизитыШапки.СпособДоставки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Доставка.АдресДоставки", 
		РеквизитыШапки.АдресДоставки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Доставка.АдресДоставкиЗначенияПолей",
		РеквизитыШапки.АдресДоставкиЗначенияПолей);
	
	// заполним итоги прописью
	ШаблонИтоговаяСтрока = НСтр("ru='Всего наименований %1 на сумму %2'");
	ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИтоговаяСтрока,
																			ТаблицаТоваров.Количество(),
																			РеквизитыШапки.СуммаДокумента);
																			
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтоговаяСтрока);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПоОтчетуОПродажахКомиссионногоТовара.
Процедура ЗаполнитьДанныеПоОтчетуОПродажахКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента, Отказ) Экспорт
	
	ДополнительныеРеквизитыДляТаблицыТоваров = Новый Структура;
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ДатаПродажи");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("СтавкаНДС");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательНаименование");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательИНН");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательКПП");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательФактАдрес", СтруктураАдресаФНС());
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательЮрАдрес", СтруктураАдресаФНС());
	
	СтруктураДанных = Документы[СсылкаНаОбъект.Метаданные().Имя].ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	РезультатПоТоварам = СтруктураДанных.РезультатПоТабличнойЧасти;
	
	РеквизитыШапки = СтруктураДанных.РезультатПоШапке.Выбрать();
	РеквизитыШапки.Следующий();
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ДатаФормирования", РеквизитыШапки.Дата);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", НСтр("ru = '643'"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс",	1);
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить сумму документа. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнена колонка ""Сумма""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", РеквизитыШапки.СуммаДокумента,
		ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "НачалоПериода", РеквизитыШапки.НачалоПериода);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "КонецПериода", РеквизитыШапки.КонецПериода);
	
	// Заполняем Контрагента
	СведенияОКомитенте = Неопределено;
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент, СведенияОКомитенте);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКомитенте, "Комитент", Истина);
	
	// Заполняем Организацию
	СведенияОКомиссионере = Неопределено; 
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация, СведенияОКомиссионере);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКомиссионере, "Комиссионер", Истина);
	
	ТаблицаТоваров = ТаблицаТоваров();
	
	// Пример передачи доп данных
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ДанныеПодписанные = Новый Структура("СрокГодности, Характеристика");
	
	ВыборкаПоТоварам = РезультатПоТоварам.Выбрать();
	СообщениеОбОшибке = "";
	
	// Таблица для контроля совпадений номенклатуры с номенклатурой поставщика
	КонтрольСовпаденийНоменклатуры = РезультатПоТоварам.Выгрузить();
	
	Если ВыполнятьПроверкуСопоставленияНоменклатуры() Тогда
		ПроверитьСопоставлениеСНоменклатуройПоставщика(КонтрольСовпаденийНоменклатуры, ДеревоДокумента, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Пока ВыборкаПоТоварам.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоТоварам);
		
		// Заполним сведения, необходимые для выставления счета-фактуры комитентом
		СтруктураДополнительныхДанных = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(
													ДополнительныеРеквизитыДляТаблицыТоваров);
		
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(ВыборкаПоТоварам.ДатаПродажи);
		СтруктураДополнительныхДанных.Вставить("ДатаПродажи", МассивЗначений);
		
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(ВыборкаПоТоварам.СтавкаНДС);
		СтруктураДополнительныхДанных.Вставить("СтавкаНДС", МассивЗначений);
		НоваяСтрока.ДополнительныеРеквизиты = СтруктураДополнительныхДанных;
		
		// Заполняем доп данные в строке товаров
		ДанныеПодписанные.Характеристика = ВыборкаПоТоварам.Характеристика;
		НоваяСтрока.ДопДанныеПодписанные = ДанныеПодписанные;
		
		Сопоставление = Новый Структура;
		Если ЗначениеЗаполнено(НоваяСтрока.ИдентификаторНоменклатурыПоставщика) Тогда
			Сопоставление.Вставить("Идентификатор", НоваяСтрока.ИдентификаторНоменклатурыПоставщика);
		КонецЕсли;
		Сопоставление.Вставить("НоменклатураИБ", НоваяСтрока.Номенклатура);
		Сопоставление.Вставить("ХарактеристикаИБ", НоваяСтрока.Характеристика);
		Сопоставление.Вставить("УпаковкаИБ", НоваяСтрока.ЕдиницаИзмерения);
		НоваяСтрока.Сопоставление = Сопоставление;
		
	КонецЦикла;
	
	ЗаполнитьПараметрыОбработкиОшибокВТаблицеТоваровCML(ТаблицаТоваров);
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");
	
	КонтрагентРозничныйПокупатель = Константы.КонтрагентРозничныйПокупатель.Получить();
	Если ЗначениеЗаполнено(КонтрагентРозничныйПокупатель) Тогда
		ЧастноеЛицо = Неопределено;
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(КонтрагентРозничныйПокупатель, ЧастноеЛицо);
	Иначе
		ЧастноеЛицо = Новый Структура("Представление, ПолноеНаименование, КодПоОКПО, ИНН, КПП, Телефоны,
			| ЮридическийАдрес, ФактическийАдрес, ЮрФизЛицо, Фамилия, Имя, Отчество, Ссылка, АдрТекст");
		ЧастноеЛицо.ПолноеНаименование = НСтр("ru = 'Частное лицо'");
		ЧастноеЛицо.Представление = НСтр("ru = 'Частное лицо'");
		ЧастноеЛицо.ИНН = НСтр("ru = '123456789110'");
		ЧастноеЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
		ЧастноеЛицо.Ссылка = Справочники.ФизическиеЛица.ПустаяСсылка();
		ЧастноеЛицо.Фамилия = НСтр("ru = 'Частное лицо'");
		ЧастноеЛицо.Имя = НСтр("ru = 'Частное лицо'");
		ЧастноеЛицо.Отчество = НСтр("ru = 'Частное лицо'");
		ЧастноеЛицо.ЮридическийАдрес = НСтр("ru = 'Неопределено'");
		ЧастноеЛицо.ФактическийАдрес = НСтр("ru = 'Неопределено'");
		ЧастноеЛицо.АдрТекст = НСтр("ru = 'Неопределено'");
	КонецЕсли;
	
	СтрокаТаблицаТоваров = ДеревоДокумента.Строки.Найти("Товары", "ПолныйПуть");

	Если ТаблицаТоваров.Количество() Тогда 
		Для Каждого Товар Из СтрокаТаблицаТоваров.Строки Цикл
			ЗаполнитьДанныеУчастника(Товар, ЧастноеЛицо, "Товары.НомерСтроки.Покупатель");
		КонецЦикла;
	КонецЕсли;
	
	// итоговая строка прописью
	Если РеквизитыШапки.СуммаДокумента < 0 Тогда
		ТекстИтоговаяСтрока = НСтр("ru = 'Всего возвращено наименований'");
	Иначе
		ТекстИтоговаяСтрока = НСтр("ru = 'Всего продано наименований'");
	КонецЕсли;
	
	СуммаПредставление = "";
	ФорматСумм(РеквизитыШапки.СуммаДокумента, СуммаПредставление, РеквизитыШапки.ВалютаКод);
	
	ИтоговаяСтрока = ТекстИтоговаяСтрока
		+ " "
		+ ТаблицаТоваров.Количество()
		+ ?(РеквизитыШапки.СуммаДокумента <> 0, НСтр("ru = ', на сумму'") + " ", "")
		+ ?(РеквизитыШапки.СуммаДокумента <> 0, СуммаПредставление, "");
		
	СуммаПрописью = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(РеквизитыШапки.СуммаДокумента);
	
	
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
		
	Если РеквизитыШапки.СуммаВознаграждения <> 0 Тогда
		СуммаВознаграждения = НСтр("ru = 'Сумма комиссионного вознаграждения составила'")
			+ " "
			+ ?(РеквизитыШапки.СуммаВознаграждения < 0,  НСтр("ru = 'минус'") + " ", "")
			+ ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(РеквизитыШапки.СуммаВознаграждения);
	КонецЕсли;

	ИтогиПрописью = ИтоговаяСтрока + Символы.ПС + СуммаВознаграждения;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтогиПрописью);
	
	// итоговые суммы
	ТекстОшибки = НСтр("ru = 'Не удалось итоговые стоимостные показатели. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнена колонка ""Сумма""'");
	СуммаПродажиИтог = ТаблицаТоваров.Итог("СуммаПродажи");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаДокумента",
		СуммаПродажиИтог, ТекстОшибки);
	
	СуммаВознагражденияИтог = ТаблицаТоваров.Итог("СуммаВознаграждения");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаВознаграждения",
		СуммаВознагражденияИтог, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СтавкаНДСВознаграждения", 
		Перечисления.СтавкиНДС.НДС18);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаНДСВознаграждения",
		?(СуммаВознагражденияИтог = 0, 0, 
			ОбработкаТабличнойЧастиТоварыСервер.РассчитатьСуммуНДС(СуммаВознагражденияИтог, Перечисления.СтавкиНДС.НДС18)),
				ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.ЦенаВключаетНДС", Истина);
	
	Если ЗначениеЗаполнено (РеквизитыШапки.ПроцентВознаграждения) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.Процент", 
			РеквизитыШапки.ПроцентВознаграждения);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПоОтчетуОПродажахКомиссионногоТовара.
Процедура ЗаполнитьДанныеПоОтчетуОСписанииКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента, Отказ) Экспорт
	
	ДополнительныеРеквизитыДляТаблицыТоваров = Новый Структура;
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ДатаПродажи");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("СтавкаНДС");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательНаименование");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательИНН");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательКПП");
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательФактАдрес", СтруктураАдресаФНС());
	ДополнительныеРеквизитыДляТаблицыТоваров.Вставить("ПокупательЮрАдрес", СтруктураАдресаФНС());
	
	
	СтруктураДанных = Документы[СсылкаНаОбъект.Метаданные().Имя].ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	РезультатПоТоварам = СтруктураДанных.РезультатПоТабличнойЧасти;
	
	РеквизитыШапки = СтруктураДанных.РезультатПоШапке.Выбрать();
	РеквизитыШапки.Следующий();
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ДатаФормирования",	РеквизитыШапки.Дата);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", НСтр("ru = '643'"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс",	1);
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить сумму документа. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнена колонка ""Сумма""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", РеквизитыШапки.СуммаДокумента,
		ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "НачалоПериода", РеквизитыШапки.НачалоПериода);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "КонецПериода", РеквизитыШапки.КонецПериода);
	
	// Заполняем Контрагента
	СведенияОКомитенте = Неопределено;
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент, СведенияОКомитенте);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКомитенте, "Комитент", Истина);
	
	// Заполняем Организацию
	СведенияОКомиссионере = Неопределено; 
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация, СведенияОКомиссионере);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКомиссионере, "Комиссионер", Истина);
	
	ТаблицаТоваров = ТаблицаТоваров();
	
	// Пример передачи доп данных
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ДанныеПодписанные = Новый Структура("СрокГодности, Характеристика");
	
	ВыборкаПоТоварам = РезультатПоТоварам.Выбрать();
	СообщениеОбОшибке = "";
	
	// Таблица для контроля совпадений номенклатуры с номенклатурой поставщика
	КонтрольСовпаденийНоменклатуры = РезультатПоТоварам.Выгрузить();
	
	Если ВыполнятьПроверкуСопоставленияНоменклатуры() Тогда
		ПроверитьСопоставлениеСНоменклатуройПоставщика(КонтрольСовпаденийНоменклатуры, ДеревоДокумента, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Пока ВыборкаПоТоварам.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоТоварам);
		
		// Заполним сведения, необходимые для выставления счета-фактуры комитентом
		СтруктураДополнительныхДанных = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(
													ДополнительныеРеквизитыДляТаблицыТоваров);
		
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(ВыборкаПоТоварам.ДатаПродажи);
		СтруктураДополнительныхДанных.Вставить("ДатаПродажи", МассивЗначений);
		
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(ВыборкаПоТоварам.СтавкаНДС);
		СтруктураДополнительныхДанных.Вставить("СтавкаНДС", МассивЗначений);
		НоваяСтрока.ДополнительныеРеквизиты = СтруктураДополнительныхДанных;
		
		// Заполняем доп данные в строке товаров
		ДанныеПодписанные.Характеристика = ВыборкаПоТоварам.Характеристика;
		НоваяСтрока.ДопДанныеПодписанные = ДанныеПодписанные;
		
		Сопоставление = Новый Структура;
		Если ЗначениеЗаполнено(НоваяСтрока.ИдентификаторНоменклатурыПоставщика) Тогда
			Сопоставление.Вставить("Идентификатор", НоваяСтрока.ИдентификаторНоменклатурыПоставщика);
		КонецЕсли;
		Сопоставление.Вставить("НоменклатураИБ", НоваяСтрока.Номенклатура);
		Сопоставление.Вставить("ХарактеристикаИБ", НоваяСтрока.Характеристика);
		Сопоставление.Вставить("УпаковкаИБ", НоваяСтрока.ЕдиницаИзмерения);
		НоваяСтрока.Сопоставление = Сопоставление;
		
	КонецЦикла;
	
	ЗаполнитьПараметрыОбработкиОшибокВТаблицеТоваровCML(ТаблицаТоваров);
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");
	
	ТекстИтоговаяСтрока = НСтр("ru = 'Всего списано наименований'");
	
	СуммаПредставление = "";
	ФорматСумм(РеквизитыШапки.СуммаДокумента, СуммаПредставление, РеквизитыШапки.ВалютаКод);
	
	ИтоговаяСтрока = ТекстИтоговаяСтрока
		+ " "
		+ ТаблицаТоваров.Количество()
		+ ?(РеквизитыШапки.СуммаДокумента <> 0, НСтр("ru = ', на сумму'") + " ", "")
		+ ?(РеквизитыШапки.СуммаДокумента <> 0, СуммаПредставление, "");
		
	СуммаПрописью = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(РеквизитыШапки.СуммаДокумента);
	
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	ИтогиПрописью = ИтоговаяСтрока + Символы.ПС;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтогиПрописью);
	
	// итоговые суммы
	ТекстОшибки = НСтр("ru = 'Не удалось итоговые стоимостные показатели. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнена колонка ""Сумма""'");
	СуммаПродажиИтог = ТаблицаТоваров.Итог("СуммаПродажи");
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПередачаТоваровПродавец.
Процедура ЗаполнитьДанныеПередачаТоваровПродавец(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт 
	
	СтруктураДанных = Документы[СсылкаНаОбъект.Метаданные().Имя].ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	ВыборкаШапки.Следующий();
	
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		СсылкаНаОбъект, "Объект.НомерЭД");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерТоварнойНакладной",
		ВыборкаШапки.Номер, ПараметрыОбработкиОшибок);
	
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		СсылкаНаОбъект, "Объект.Дата");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаТоварнойНакладной",
		ВыборкаШапки.Дата, ПараметрыОбработкиОшибок);
	
	// Выводим общие реквизиты шапки
	СведенияОПоставщике = Неопределено;
	Если ЗначениеЗаполнено(ВыборкаШапки.Организация) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Организация, СведенияОПоставщике);
		
		Если ПравоДоступа("Чтение", Метаданные.Справочники.БанковскиеСчетаОрганизаций)
			И ЗначениеЗаполнено(ВыборкаШапки.БанковскийСчетОрганизации) Тогда
			ЗаполнитьРеквизитыБанковскогоСчета(СведенияОПоставщике, ВыборкаШапки.БанковскийСчетОрганизации, 
				"БанковскиеСчетаОрганизаций");
		КонецЕсли;
		
		ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПоставщике, "Поставщик");
	Иначе
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
			"Объект.Организация");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"Поставщик", СведенияОПоставщике, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	СведенияОПокупателе = Неопределено;
	Если ЗначениеЗаполнено(ВыборкаШапки.Контрагент) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Контрагент, СведенияОПокупателе);
		ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПокупателе, "Плательщик");
	Иначе
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
			"Объект.Контрагент");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"Плательщик", СведенияОПокупателе, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаШапки.Грузоотправитель) Тогда
		СведенияОГрузоотправителе = Неопределено; 
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Грузоотправитель, 
			СведенияОГрузоотправителе);
		ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОГрузоотправителе, "Грузоотправитель", , "ФактическийАдрес");
	ИначеЕсли СведенияОПоставщике <> Неопределено Тогда
		ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПоставщике, "Грузоотправитель", , "ФактическийАдрес");
	КонецЕсли;
	
	СведенияОГрузополучателе = Неопределено;
	Если ЗначениеЗаполнено(ВыборкаШапки.Грузополучатель) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Грузополучатель, СведенияОГрузополучателе);
	ИначеЕсли СведенияОПокупателе <> Неопределено Тогда
		СведенияОГрузополучателе = СведенияОПокупателе;
	КонецЕсли;
	
	Если СведенияОГрузополучателе <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ВыборкаШапки.АдресДоставкиЗначенияПолей) Тогда
			ВидАдреса = "ПроизвольныйАдрес";
			СведенияОГрузополучателе.Вставить("ПроизвольныйАдресЗначенияПолей", ВыборкаШапки.АдресДоставкиЗначенияПолей);
		Иначе
			ВидАдреса = "ФактическийАдрес";
		КонецЕсли;
		
		ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОГрузополучателе, "Грузополучатель",,ВидАдреса);
	КонецЕсли;
	
	СтруктураДопДанных = Новый Структура;
	
	Если ЗначениеЗаполнено(ВыборкаШапки.Перевозчик) Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыборкаШапки.Перевозчик, 
			"НаименованиеПолное, ИНН, КПП, ЮрФизЛицо");
		СведенияОПеревозчике = ЗначенияРеквизитов.НаименованиеПолное + ", " + ЗначенияРеквизитов.ИНН
			+ ?(ЗначенияРеквизитов.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо, "/" + ЗначенияРеквизитов.КПП, "");
		СтруктураДопДанных.Вставить("СведенияОПеревозчике", СведенияОПеревозчике);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаШапки.НомерТранспортнойНакладной)
		И ЗначениеЗаполнено(ВыборкаШапки.ДатаТранспортнойНакладной) Тогда
		
		ТранспортнаяНакладная = Новый ТаблицаЗначений;
		ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяНомер");
		ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяДата");
		
		НоваяСтрока = ТранспортнаяНакладная.Добавить();
		НоваяСтрока.ТранспортнаяНакладнаяНомер = ВыборкаШапки.НомерТранспортнойНакладной;
		НоваяСтрока.ТранспортнаяНакладнаяДата  = ВыборкаШапки.ДатаТранспортнойНакладной;
		
		// Дополним таблицу транспортных накладных параметрами обработки ошибок.
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока,
			"ТранспортнаяНакладнаяНомер", ВыборкаШапки.Ссылка, "Объект.НомерТранспортнойНакладной");
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока,
			"ТранспортнаяНакладнаяДата", ВыборкаШапки.Ссылка, "Объект.ДатаТранспортнойНакладной");
		
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТранспортнаяНакладная, "ТранспортнаяНакладная");
		
	КонецЕсли;
	
	// Обработка ошибки с указанием расширенного текста сообщения вместо параметров обработки ошибки.
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", ВыборкаШапки.ВалютаКод,
		ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования", 
		ВыборкаШапки.ДокументОснование);
	
	Если ЗначениеЗаполнено(ВыборкаШапки.ДатаОтгрузки) Тогда
		ДатаОтпуска = ВыборкаШапки.ДатаОтгрузки;
	Иначе
		ДатаОтпуска = ВыборкаШапки.Дата;
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияПоОтпускуГруза.ДатаОтпуска", 
		ДатаОтпуска);
	
	ТаблицаТоваров = Новый ТаблицаЗначений();
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("НаименованиеНоменклатуры");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("НаименованиеХарактеристики");
	ТаблицаТоваров.Колонки.Добавить("Сорт");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("КодТовара");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("ВидУпаковки");
	ТаблицаТоваров.Колонки.Добавить("КоличествоВОдномМесте");
	ТаблицаТоваров.Колонки.Добавить("КоличествоМест");
	ТаблицаТоваров.Колонки.Добавить("МассаБрутто");
	ТаблицаТоваров.Колонки.Добавить("МассаНетто");
	ТаблицаТоваров.Колонки.Добавить("Цена");
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС");
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС");
	ТаблицаТоваров.Колонки.Добавить("ДокументОснование");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеНеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	НомерСтроки = 1;
	ВыборкаТоваров = СтруктураДанных.ВыборкаТоваров;
	
	ЗаполнениеКодаТовара = СтруктураЭД.ВариантыЗаполненияПолей.ТоварКод;
	
	Пока ВыборкаТоваров.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		НоваяСтрока.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		
		НоваяСтрока.Номенклатура                 = ВыборкаТоваров.Номенклатура;
		НоваяСтрока.НаименованиеНоменклатуры     = ВыборкаТоваров.НоменклатураНаименование;
		НоваяСтрока.Характеристика               = ВыборкаТоваров.Характеристика;
		НоваяСтрока.НаименованиеХарактеристики   = ВыборкаТоваров.ХарактеристикаНаименование;
		НоваяСтрока.Артикул                      = ВыборкаТоваров.НоменклатураАртикул;
		НоваяСтрока.КодТовара                    = ВыборкаТоваров.НоменклатураКод;
		НоваяСтрока.ЕдиницаИзмеренияНаименование = ВыборкаТоваров.ЕдиницаИзмеренияНаименование;
		НоваяСтрока.ЕдиницаИзмерения             = ВыборкаТоваров.ЕдиницаИзмерения;
		НоваяСтрока.БазоваяЕдиницаКод            = ВыборкаТоваров.ЕдиницаИзмеренияКод;
		НоваяСтрока.МассаНетто                   = ВыборкаТоваров.МассаНетто;
		НоваяСтрока.Цена                         = ?(ВыборкаТоваров.Количество = 0, 0,
			Окр(ВыборкаТоваров.СуммаБезНДС / ВыборкаТоваров.Количество, 2));
		
		НоваяСтрока.СуммаБезНДС = ВыборкаТоваров.СуммаБезНДС;
		НоваяСтрока.СтавкаНДС   = ВыборкаТоваров.СтавкаНДС;
		НоваяСтрока.СуммаНДС    = ВыборкаТоваров.СуммаНДС;
		НоваяСтрока.СуммаСНДС   = ВыборкаТоваров.СуммаСНДС;
		
		ДопДанныеТовара = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураДопДанных);
		
		ДопДанныеТовара.Вставить("СтавкаНДС",   ВыборкаТоваров.СтавкаНДС);
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.НомерГТД) Тогда
			ДопДанныеТовара.Вставить("НомерТД", ВыборкаТоваров.НомерГТД);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.СтранаПроисхожденияКод) Тогда
			ДопДанныеТовара.Вставить("КодСтраныПроисхождения", ВыборкаТоваров.СтранаПроисхожденияКод);
		КонецЕсли;
		
		НоваяСтрока.ДопДанныеПодписанные = ДопДанныеТовара;
		
		Сопоставление = Новый Структура;
		Сопоставление.Вставить("Наименование", НоваяСтрока.НаименованиеНоменклатуры);
		Если ЗначениеЗаполнено(НоваяСтрока.Характеристика) Тогда
			Сопоставление.Вставить("Характеристика", НоваяСтрока.НаименованиеХарактеристики);
		КонецЕсли;
		Сопоставление.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмеренияНаименование);
		Сопоставление.Вставить("Артикул", НоваяСтрока.Артикул);
		
		ОтборШтрихкодов = Новый Структура("Номенклатура,Характеристика,ЕдиницаИзмерения");
		ЗаполнитьЗначенияСвойств(ОтборШтрихкодов, ВыборкаТоваров);
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.ИдентификаторНоменклатурыПоставщика) Тогда
			Сопоставление.Вставить("Идентификатор", ВыборкаТоваров.ИдентификаторНоменклатурыПоставщика);
		КонецЕсли;
		Сопоставление.Вставить("НоменклатураИБ", ВыборкаТоваров.НоменклатураИБ);
		Сопоставление.Вставить("ХарактеристикаИБ", ВыборкаТоваров.ХарактеристикаИБ);
		Сопоставление.Вставить("УпаковкаИБ", ВыборкаТоваров.УпаковкаИБ);
		
		НоваяСтрока.Сопоставление = Сопоставление;
		
		Если  ЗаполнениеКодаТовара = "Артикул" Тогда
			НоваяСтрока.КодТовара = ВыборкаТоваров.НоменклатураАртикул;
		КонецЕсли;
	
	КонецЦикла;
	
	// Обработка ошибки через механизм сообщений пользователю.
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"КодТовара", "Номенклатура", "Объект.Код");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"НаименованиеНоменклатуры", "Номенклатура", "Объект.НаименованиеПолное");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"МассаНетто", ВыборкаТоваров.Ссылка, "Объект.Товары.Количество",, "НомерСтроки");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"Цена", ВыборкаТоваров.Ссылка, "Объект.Товары.Цена",, "НомерСтроки");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"СуммаБезНДС", ВыборкаТоваров.Ссылка, "Объект.Товары.Сумма",, "НомерСтроки");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"СуммаНДС", ВыборкаТоваров.Ссылка, "Объект.Товары.СуммаНДС",, "НомерСтроки");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"СуммаСНДС", ВыборкаТоваров.Ссылка, "Объект.Товары.Сумма",, "НомерСтроки");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"ЕдиницаИзмеренияНаименование", "ЕдиницаИзмерения", "Объект.Наименование");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров,
		"БазоваяЕдиницаКод", "ЕдиницаИзмерения", "Объект.Код");
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "ТаблицаТоваров");
	
	// Итоговые показатели по документу.
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить итоговые показатели. Возможные причины:
		|	- не заполнена табличная часть ""Товары""
		|	- не заполнены колонки ""Сумма"", ""НДС"", ""Количество""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.КоличествоПорядковыхНомеровЗаписей",
								ТаблицаТоваров.Количество(), ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаНетто",
								ТаблицаТоваров.Итог("МассаНетто"), ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.МассаНетто",
								ТаблицаТоваров.Итог("МассаНетто"), ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаБезНДС",
								ТаблицаТоваров.Итог("СуммаБезНДС"), ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаНДС",
								ТаблицаТоваров.Итог("СуммаНДС"), ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаСНДС",
								ТаблицаТоваров.Итог("СуммаСНДС"), ТекстОшибки);
	
	Если ЗначениеЗаполнено(СтруктураДопДанных) Тогда
		ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(ДеревоДанных, СтруктураДопДанных, Истина);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС.
// Подготавливает данные для электронного документа типа УПД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование) = ТИП(Документ.ПоступлениеТоваров)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ ВТ_Основание
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий.ДокументыОснования КАК ЭлектронныйДокументВходящийДокументыОснования
	|ГДЕ
	|	ЭлектронныйДокументВходящийДокументыОснования.Ссылка = &ВладелецЭД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ПоступлениеТоваровУслуг.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	ПоступлениеТоваровУслуг.Организация.ИНН КАК ОрганизацияИНН,
	|	ПоступлениеТоваровУслуг.Организация.КПП КАК ОрганизацияКПП
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|		ПО ПоступлениеТоваровУслуг.Ссылка = ВТ_Основание.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Дата,
	|	СчетФактураПолученный.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	СчетФактураПолученный.Организация.ИНН КАК ОрганизацияИНН,
	|	СчетФактураПолученный.Организация.КПП КАК ОрганизацияКПП
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|		ПО СчетФактураПолученный.Ссылка = ВТ_Основание.ДокументОснование";

	Запрос.УстановитьПараметр("ВладелецЭД", СтруктураЭД.ВладелецЭД);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДатаПолученияТоваров = ТекущаяДатаСеанса();
	СодержаниеОперации   = "-";
	СоставительДокумента = "-";
	Если НЕ РезультатыЗапроса[1].Пустой() Тогда
		
		Выборка = РезультатыЗапроса[1].Выбрать();
		Выборка.Следующий();
		
		ДатаПолученияТоваров = Выборка.Дата;
		
		СоставительДокумента = ?(ЗначениеЗаполнено(Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН/КПП %2/%3'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН, Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН %2'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН));
		
		СодержаниеОперации = НСтр("ru = 'Товары принял без претензий'");
		
	ИначеЕсли НЕ РезультатыЗапроса[2].Пустой() Тогда
		
		Выборка = РезультатыЗапроса[2].Выбрать();
		Выборка.Следующий();
		
		ДатаПолученияТоваров = Выборка.Дата;
		
		СоставительДокумента = ?(ЗначениеЗаполнено(Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН/КПП %2/%3'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН, Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН %2'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН));
		
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаПолученияТоваров", ДатаПолученияТоваров);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", СодержаниеОперации);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Формирование данных для электронных документов.

// Работа с деревом данных ФНС.

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС.
// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СчетФактураСсылка = Неопределено;
	ТипДокумента = ТипЗнч(СсылкаНаОбъект);
	Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактураСсылка = СсылкаНаОбъект;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	СчетФактураВыданный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|ГДЕ
		|	СчетФактураВыданный.ДокументОснование = &ДокументОснование
		|	И СчетФактураВыданный.Проведен";
		Запрос.УстановитьПараметр("ДокументОснование", СсылкаНаОбъект);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СчетФактураСсылка = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетФактураСсылка) Тогда
		ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС(СчетФактураСсылка, СтруктураЭД, ДеревоДанных, Отказ);
	Иначе
		ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС.
Процедура ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СтруктураДанных = Документы.ВозвратТоваровПоставщику.ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	ВыборкаШапки.Следующий();
	
	// Обработка ошибки с открытием формы по навигационной ссылке.
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект, "НомерЭД");
	ИмяФормы = "Документ.ВозвратТоваровПоставщику.ФормаОбъекта";
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект,
		"Объект.НомерЭД", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ВыборкаШапки.Номер,
		ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект, 
		"Объект.Дата", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ВыборкаШапки.Дата,
		ПараметрыОбработкиОшибок);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияДокументаОтгрузки",
		ВыборкаШапки.ДокументОснование);
	
	ЗаполнитьДеревоДанныхУПД(СтруктураДанных, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СтруктураДанных = Документы.СчетФактураВыданный.ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	
	Если СтруктураДанных = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	ВыборкаШапки.Следующий();
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаСчетаФактуры", СсылкаНаОбъект);
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект, "НомерЭД");
	ИмяФормы = "Документ.ВозвратТоваровПоставщику.ФормаОбъекта";
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект,
		"Объект.НомерЭД", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ВыборкаШапки.Номер,
		ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект, 
		"Объект.Дата", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ВыборкаШапки.Дата,
		ПараметрыОбработкиОшибок);
	
	ВидСчетаФактуры = "Реализация";
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", ВидСчетаФактуры);
	
	МассивОснований = Новый Массив;
	Если СтруктураДанных.Свойство("ВыборкаОснований") Тогда
		
		ВыборкаОснований = СтруктураДанных.ВыборкаОснований;
		Пока ВыборкаОснований.Следующий() Цикл
			МассивОснований.Добавить(ВыборкаОснований.ДокументОснование);
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьДеревоДанныхУПД(СтруктураДанных, СтруктураЭД, ДеревоДанных);
	
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		СсылкаНаОбъект, "Объект.СчетФактураОснование");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры",
		МассивОснований, ПараметрыОбработкиОшибок);
	
	Если СтруктураДанных.Свойство("ВыборкаПРД") Тогда
		
		ПлатежныеДокументы = Новый ТаблицаЗначений;
		ПлатежныеДокументы.Колонки.Добавить("НомерСтроки");
		ПлатежныеДокументы.Колонки.Добавить("НомерПРД");
		ПлатежныеДокументы.Колонки.Добавить("ДатаПРД");
		
		ВыборкаПРД = СтруктураДанных.ВыборкаПРД;
		Пока ВыборкаПРД.Следующий() Цикл
			НоваяСтрока = ПлатежныеДокументы.Добавить();
			НоваяСтрока.НомерСтроки = ВыборкаПРД.НомерСтроки;
			НоваяСтрока.НомерПРД    = ВыборкаПРД.НомерПлатежноРасчетногоДокумента;
			НоваяСтрока.ДатаПРД     = ВыборкаПРД.ДатаПлатежноРасчетногоДокумента;
		КонецЦикла;
		
		// Дополним таблицу платежных документов параметрами обработки ошибок.
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ПлатежныеДокументы,
			"НомерПРД", СсылкаНаОбъект, "Объект.ПлатежноРасчетныеДокументы.НомерПлатежноРасчетногоДокумента",, "НомерСтроки");
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ПлатежныеДокументы,
			"ДатаПРД", СсылкаНаОбъект, "Объект.ПлатежноРасчетныеДокументы.ДатаПлатежноРасчетногоДокумента",, "НомерСтроки");
		
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПлатежныеДокументы, "ПлатежноРасчетныеДокументы");
		
	КонецЕсли;
	
КонецПроцедуры

#Область УПД_2019

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС_2019.
Процедура ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ДокументСсылкаСЧФ = Неопределено;
	ТипДокумента = ТипЗнч(СсылкаНаОбъект);
	Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ДокументСсылкаСЧФ = СсылкаНаОбъект;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	СчетФактураВыданный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|ГДЕ
		|	СчетФактураВыданный.ДокументОснование = &ДокументОснование
		|	И СчетФактураВыданный.Проведен";
		Запрос.УстановитьПараметр("ДокументОснование", СсылкаНаОбъект);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ДокументСсылкаСЧФ = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументСсылкаСЧФ) Тогда
		ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019(ДокументСсылкаСЧФ, СтруктураЭД, ДеревоДанных, Отказ);
	Иначе
		ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019.
Процедура ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СтруктураДанных = Документы.ВозвратТоваровПоставщику.ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	ВыборкаШапки.Следующий();
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект, "НомерЭД");
	ИмяФормы = "Документ.ВозвратТоваровПоставщику.ФормаОбъекта";
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект,
		"Объект.НомерЭД", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ВыборкаШапки.Номер,
		ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект, 
		"Объект.Дата", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ВыборкаШапки.Дата,
		ПараметрыОбработкиОшибок);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияДокументаОтгрузки",
		ВыборкаШапки.ДокументОснование);
	
	ЗаполнитьДеревоДанныхУПД_2019(СтруктураДанных, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019.
// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СтруктураДанных = Документы.СчетФактураВыданный.ПолучитьДанныеДляЭД(СсылкаНаОбъект);
	
	Если СтруктураДанных = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	ВыборкаШапки.Следующий();
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаСчетаФактуры", СсылкаНаОбъект);
	
	// Обработка ошибки с открытием формы по навигационной ссылке.
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект, "НомерЭД");
	ИмяФормы = "Документ.ВозвратТоваровПоставщику.ФормаОбъекта";
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект,
		"Объект.НомерЭД", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ВыборкаШапки.Номер,
		ПараметрыОбработкиОшибок);
		
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(СсылкаНаОбъект, 
		"Объект.Дата", НавигационнаяСсылка, ИмяФормы, ПараметрыФормы);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ВыборкаШапки.Дата,
		ПараметрыОбработкиОшибок);
	
	
	ВидСчетаФактуры = "Реализация";
	ОбстоятельстваФормированияСФ = "1";
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ОбстоятельстваФормированияСФ", ОбстоятельстваФормированияСФ);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", ВидСчетаФактуры);
	
	МассивОснований = Новый Массив;
	Если СтруктураДанных.Свойство("ВыборкаОснований") Тогда
		
		ВыборкаОснований = СтруктураДанных.ВыборкаОснований;
		Пока ВыборкаОснований.Следующий() Цикл
			МассивОснований.Добавить(ВыборкаОснований.ДокументОснование);
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьДеревоДанныхУПД_2019(СтруктураДанных, СтруктураЭД, ДеревоДанных);
	
	ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(
		СсылкаНаОбъект, "Объект.СчетФактураОснование");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры",
		МассивОснований, ПараметрыОбработкиОшибок);
	
	Если СтруктураДанных.Свойство("ВыборкаПРД") Тогда
		
		ПлатежныеДокументы = Новый ТаблицаЗначений;
		ПлатежныеДокументы.Колонки.Добавить("НомерСтроки");
		ПлатежныеДокументы.Колонки.Добавить("НомерПРД");
		ПлатежныеДокументы.Колонки.Добавить("ДатаПРД");
		//ПлатежныеДокументы.Колонки.Добавить("Сумма");
		
		ВыборкаПРД = СтруктураДанных.ВыборкаПРД;
		Пока ВыборкаПРД.Следующий() Цикл
			НоваяСтрока = ПлатежныеДокументы.Добавить();
			НоваяСтрока.НомерСтроки = ВыборкаПРД.НомерСтроки;
			НоваяСтрока.НомерПРД    = ВыборкаПРД.НомерПлатежноРасчетногоДокумента;
			НоваяСтрока.ДатаПРД     = ВыборкаПРД.ДатаПлатежноРасчетногоДокумента;
			//НоваяСтрока.Сумма = ВыборкаПРД.Сумма;
		КонецЦикла;
		
		// Дополним таблицу платежных документов параметрами обработки ошибок.
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ПлатежныеДокументы,
			"НомерПРД", СсылкаНаОбъект, "Объект.ПлатежноРасчетныеДокументы.НомерПлатежноРасчетногоДокумента",, "НомерСтроки");
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ПлатежныеДокументы,
			"ДатаПРД", СсылкаНаОбъект, "Объект.ПлатежноРасчетныеДокументы.ДатаПлатежноРасчетногоДокумента",, "НомерСтроки");
		
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПлатежныеДокументы, "ПлатежноРасчетныеДокументы");
		
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС_2019.
Процедура ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС_2019(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование) = ТИП(Документ.ПоступлениеТоваров)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ ВТ_Основание
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий.ДокументыОснования КАК ЭлектронныйДокументВходящийДокументыОснования
	|ГДЕ
	|	ЭлектронныйДокументВходящийДокументыОснования.Ссылка = &ВладелецЭД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваров.Дата КАК Дата,
	|	ПоступлениеТоваров.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	ПоступлениеТоваров.Организация.ИНН КАК ОрганизацияИНН,
	|	ПоступлениеТоваров.Организация.КПП КАК ОрганизацияКПП,
	|	ПоступлениеТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|		ПО ПоступлениеТоваров.Ссылка = ВТ_Основание.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Дата КАК Дата,
	|	СчетФактураПолученный.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	СчетФактураПолученный.Организация.ИНН КАК ОрганизацияИНН,
	|	СчетФактураПолученный.Организация.КПП КАК ОрганизацияКПП
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|		ПО СчетФактураПолученный.Ссылка = ВТ_Основание.ДокументОснование";
	
	Запрос.УстановитьПараметр("ВладелецЭД", СтруктураЭД.ВладелецЭД);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДатаПолученияТоваров = ТекущаяДатаСеанса();
	СодержаниеОперации   = "-";
	СоставительДокумента = "-";
	КодИтога = "1";
	Если НЕ РезультатыЗапроса[1].Пустой() Тогда
		
		Выборка = РезультатыЗапроса[1].Выбрать();
		Выборка.Следующий();
		
		ДатаПолученияТоваров = Выборка.Дата;
		
		СоставительДокумента = ?(ЗначениеЗаполнено(Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН/КПП %2/%3'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН, Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН %2'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН));
		
		СодержаниеОперации = НСтр("ru = 'Товары принял без претензий'");
		
	ИначеЕсли НЕ РезультатыЗапроса[2].Пустой() Тогда
		
		Выборка = РезультатыЗапроса[2].Выбрать();
		Выборка.Следующий();
		
		ДатаПолученияТоваров = Выборка.Дата;
		
		СоставительДокумента = ?(ЗначениеЗаполнено(Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН/КПП %2/%3'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН, Выборка.ОрганизацияКПП),
			СтрШаблон(НСтр("ru = '%1, ИНН %2'"),
				Выборка.ОрганизацияНаименованиеПолное, Выборка.ОрганизацияИНН));
		
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.ДатаПолученияТоваров", ДатаПолученияТоваров);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.СодержаниеОперации", СодержаниеОперации);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.КодИтога", КодИтога);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.СохранитьДанныеОбъектаВБД.
////////////////////////////////////////////////////////////////////////////////
// Поиск и создание документов
// Сохраняет данные из электронного документа в объект ИБ.
//
// Параметры:
//  СтрокаДляЗагрузки - Строка - параметры для загрузки.
//  ДеревоРазбора     - ДеревоЗначений - структура параметров документа ИБ.
//  ПараметрыОбработки - Структура - дополнительные параметры для обработки объекта.
//    См. ОбменСКонтрагентамиСлужебный.НовыеПараметрыОбработкиСохранитьДанныеОбъектаВБД.
//    * СсылкаНаВладельца - ДокументСсылка - владелец электронного документа (передается при перезаполнении уже
//                                           существующего документа учета). Необязательный.
//    * Записывать - Булево - если Истина, то объект должен быть записан. По умолчанию Истина.
//    * СпособОбработки - Строка - способ сохранения данных в информационной базе. Необязательный.
//      См. ОбменСКонтрагентамиПереопределяемый.СписокОперацийВидаЭД,
//      См. ОбменСКонтрагентамиПереопределяемый.ПриОпределенииСпособовОбработкиПрикладногоВидаЭлектронногоДокумента.
//  НайденныйОбъект - Произвольный - созданный объект.
//
// Возвращаемое значение:
//  НайденныйОбъект - ссылка на объект.
//
Процедура СохранитьДанныеОбъектаВБД(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки = Неопределено,
	НайденныйОбъект = Неопределено) Экспорт
	
	Если СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ПрайсЛист Тогда
		НайденныйОбъект = ЗагрузитьПрайсЛист(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец 
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
		ЗагрузитьКаталогТоваров(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца, 
			ПараметрыОбработки.Записывать);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьДокументПередачаТоваров.
// Поиск и создание документа передачи товаров.
//
// Параметры:
//  ДеревоДанных		 - ДеревоЗначений - дерево данных электронного документа.
//  СсылкаНаВладельца	 - ДокументСсылка - ссылка на документ учета.
//  Записывать			 - Булево - признак записи документа.
//  СпособОбработки		 - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьДокументПередачаТоваров(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = "") Экспорт
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) И ТипЗнч(СсылкаНаВладельца) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		ДокументОбъект.Товары.Очистить();
	Иначе
		ДокументОбъект = Документы.ПоступлениеТоваров.СоздатьДокумент();
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	ДокументОбъект.Организация = ОрганизацияПоДаннымЭД(ДеревоДанных, "Плательщик");
	ДокументОбъект.Контрагент  = КонтрагентПоДаннымЭД(ДеревоДанных, "Поставщик");
	
	ДокументОбъект.НомерВходящегоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
		"НомерТоварнойНакладной");
	ДокументОбъект.ДатаВходящегоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ДатаТоварнойНакладной");
	
	СведенияОТоварах = ДеревоДанных.Строки.Найти("ТаблицаТоваров", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		
		МассаНетто = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.МассаНетто");
		КоличествоМест = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
			"ТаблицаТоваров.НомерСтроки.КоличествоМест");
		НоваяСтрока.КоличествоУпаковок = ?(ЗначениеЗаполнено(МассаНетто), МассаНетто, КоличествоМест);
		СтавкаНДСЧисло = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			"ТаблицаТоваров.НомерСтроки.СтавкаНДС");
		Если СтавкаНДСЧисло = "НДС исчисляется налоговым агентом" Тогда
			НоваяСтрока.Сумма = НоваяСтрока.Сумма;
			НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
				"ТаблицаТоваров.НомерСтроки.СуммаСНДС");
			НоваяСтрока.СуммаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
				"ТаблицаТоваров.НомерСтроки.СуммаНДС");
			НоваяСтрока.СтавкаНДС = НайтиПеречисление("НДС", СтавкаНДСЧисло);
		КонецЕсли;
		
		НомерТаможеннойДекларации = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.ДопДанныеПодписанные.НомерТД", Ложь);
		КодСтраныПроисхождения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.ДопДанныеПодписанные.КодСтраныПроисхождения", Ложь);
		
		Если ЗначениеЗаполнено(НомерТаможеннойДекларации) Тогда
			ТаможеннаяДекларация = Неопределено;
			СтранаПроисхождения = Неопределено;
			ЭлектронноеВзаимодействиеПереопределяемый.НайтиСсылкуНаОбъект("ТаможенныеДекларации", ТаможеннаяДекларация, 
				НомерТаможеннойДекларации);
			ЭлектронноеВзаимодействиеПереопределяемый.НайтиСсылкуНаОбъект("СтраныМира", СтранаПроисхождения, 
				КодСтраныПроисхождения);
			Если НЕ ЗначениеЗаполнено(ТаможеннаяДекларация) Тогда
				НоваяТаможеннаяДекларация = Справочники.НомераГТД.СоздатьЭлемент();
				НоваяТаможеннаяДекларация.Код = НомерТаможеннойДекларации;
				НоваяТаможеннаяДекларация.СтранаПроисхождения = СтранаПроисхождения;
				НоваяТаможеннаяДекларация.Записать();
				ТаможеннаяДекларация = НоваяТаможеннаяДекларация.Ссылка;
			КонецЕсли;
			НоваяСтрока.НомерГТД = ТаможеннаяДекларация;
		КонецЕсли;
		
		Сопоставление = СведенияОТоваре.Строки.Найти(
			"ТаблицаТоваров.НомерСтроки.Сопоставление", "ПолныйПуть", Истина);
		Если Сопоставление <> Неопределено Тогда
			
			Номенклатура = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"ТаблицаТоваров.НомерСтроки.Сопоставление.НоменклатураИБ");
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				НоваяСтрока.Номенклатура = Номенклатура;
			КонецЕсли;
			
			Характеристика = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"ТаблицаТоваров.НомерСтроки.Сопоставление.ХарактеристикаИБ");
			Если ЗначениеЗаполнено(Характеристика) Тогда
				НоваяСтрока.Характеристика = Характеристика;
			КонецЕсли;
			
			ЕдиницаИзмерения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"ТаблицаТоваров.НомерСтроки.Сопоставление.УпаковкаИБ");
			Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
				Если ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.УпаковкиНоменклатуры") Тогда
					НоваяСтрока.Упаковка = ЕдиницаИзмерения;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяСтрока.Упаковка) Тогда
			КоэффициентУпаковки = НоваяСтрока.Упаковка.Коэффициент;
			Если ЗначениеЗаполнено(КоэффициентУпаковки) И КоэффициентУпаковки <> 1 Тогда
				НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок;
			КонецЕсли;
		Иначе
			НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок;
		КонецЕсли;
		
		Если ДокументОбъект.ЦенаВключаетНДС Тогда
			НоваяСтрока.Цена = ?(НоваяСтрока.КоличествоУпаковок = 0, 
									НоваяСтрока.Сумма, 
									НоваяСтрока.Сумма / НоваяСтрока.КоличествоУпаковок);
		КонецЕсли;
		
		Если НоваяСтрока.Цена = 0 Тогда
			НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.Цена");
		КонецЕсли;
		
		
	КонецЦикла;
	
	ЗавершениеЗагрузкиИЗаписьДокумента(СсылкаНаВладельца, ДокументОбъект, Записывать);
	
КонецПроцедуры


// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьУниверсальныйПередаточныйДокумент.
// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     * ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     * СчетФактура       - Строка - способ сохранения счета-фактуры.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУниверсальныйПередаточныйДокумент(ДеревоДанных, СсылкиНаВладельцев = Неопределено,
	Записывать = Истина, СпособОбработки = Неопределено, ОписаниеОшибки = "") Экспорт
	
	Если СсылкиНаВладельцев <> Неопределено Тогда
		Для Каждого СтрокаВладельца Из СсылкиНаВладельцев Цикл
			Если ТипЗнч(СтрокаВладельца) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				СчетФактура = СтрокаВладельца;
			Иначе
				ПервичныйДокумент = СтрокаВладельца;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДокументыУчета = Новый Массив;
	НайтиСоздатьУПДДокументОПередаче(ДеревоДанных, ПервичныйДокумент, Записывать, СпособОбработки, ОписаниеОшибки);
	ДокументыУчета.Добавить(ПервичныйДокумент);
	
	// Заполним основание в СФ.
	ДокументыОснованияСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ЗначениеЗаполнено(ДокументыОснованияСчетаФактуры) Тогда
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
	Иначе
		ДокументыОснованияСчетаФактуры = Новый Массив;
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснованияСчетаФактуры);
	
	НайтиСоздатьУПДСчетФактуру(ДеревоДанных, СчетФактура, Записывать, СпособОбработки, ПервичныйДокумент, ОписаниеОшибки);
	ДокументыУчета.Добавить(СчетФактура);
	
	СсылкиНаВладельцев = ДокументыУчета;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьУПДДокументОПередаче.
// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУПДДокументОПередаче(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) И ТипЗнч(СсылкаНаВладельца) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		ДокументОбъект.Товары.Очистить();
	Иначе
		ДокументОбъект = Документы.ПоступлениеТоваров.СоздатьДокумент();
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	ДокументОбъект.Контрагент  = КонтрагентПоДаннымЭД(ДеревоДанных,  "СведенияОПродавце");
	ДокументОбъект.Организация = ОрганизацияПоДаннымЭД(ДеревоДанных, "СведенияОПокупателе");
	
	ДокументОбъект.НомерВходящегоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента");
	ДокументОбъект.ДатаВходящегоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента");
	
	ИдентификаторСтроки = 0;
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	
	//ГосИС.МОТП
	ШтрихкодыУпаковок = ИнтеграцияМОТП.НоваяТаблицаШтрихкодыУпаковок();
	//Конец ГосИс.МОТП
	
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		ИдентификаторСтроки = ИдентификаторСтроки + 1;
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
		
		КоличествоМест = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
			"СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.КоличествоУпаковок = КоличествоМест;
		Если НоваяСтрока.КоличествоУпаковок = 0 Тогда
			НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		Иначе
			НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
				"СведенияОТоварах.НомерСтроки.СтоимостьТоваровСНалогом");
			НоваяСтрока.Цена = Окр(НоваяСтрока.Сумма / НоваяСтрока.КоличествоУпаковок, 2, 1);
		КонецЕсли;
		
		СтавкаНДСЧисло = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			"СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если СтавкаНДСЧисло = "НДС исчисляется налоговым агентом" Тогда
			НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтрока.СуммаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
				"СведенияОТоварах.НомерСтроки.СуммаНалога");
			НоваяСтрока.СтавкаНДС = НайтиПеречисление("НДС", СтавкаНДСЧисло);
		КонецЕсли;
		
		ОбменСКонтрагентамиРТКлиентСервер.ЗаполнитьИдентификаторСтроки(НоваяСтрока);
		
		Сопоставление = СведенияОТоваре.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.Сопоставление", "ПолныйПуть", Истина);
		Если Сопоставление <> Неопределено Тогда
			
			Номенклатура = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.Сопоставление.НоменклатураИБ");
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				НоваяСтрока.Номенклатура = Номенклатура;
			КонецЕсли;
			
			Характеристика = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.Сопоставление.ХарактеристикаИБ");
			Если ЗначениеЗаполнено(Характеристика) Тогда
				НоваяСтрока.Характеристика = Характеристика;
			КонецЕсли;
			
			ЕдиницаИзмерения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.Сопоставление.УпаковкаИБ");
			Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
				Если ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.УпаковкиНоменклатуры") Тогда
					НоваяСтрока.Упаковка = ЕдиницаИзмерения;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок;
		
		//выполним пересчет количества товара если поступление в упаковках
		КоэффициентУпаковки = ?(ЗначениеЗаполнено(НоваяСтрока.Упаковка),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Упаковка, "Коэффициент"), 1);
		Если КоэффициентУпаковки <> 0 И КоэффициентУпаковки <> 1 Тогда
			НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		КонецЕсли;
		
		//ГосИС.МОТП
		ИнтеграцияМОТП.ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок, СведенияОТоваре);
		//Конец ГосИс.МОТП
		
	КонецЦикла;
	
	//ГосИС.МОТП
	ИнтеграцияМОТП.СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
	ДокументОбъект.ШтрихкодыУпаковок.Загрузить(ШтрихкодыУпаковок);
	//Конец ГосИс.МОТП
	
	ЗавершениеЗагрузкиИЗаписьДокумента(СсылкаНаВладельца, ДокументОбъект, Записывать);
	
КонецПроцедуры


// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьУПДСчетФактуру.
// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУПДСчетФактуру(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ПервичныйДокумент, ОписаниеОшибки = "") Экспорт
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) И ТипЗнч(СсылкаНаВладельца) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		ДокументОбъект.ДатаСоставления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			ДеревоДанных, "ДатаДокумента");
	Иначе
		ДокументОбъект = Документы.СчетФактураПолученный.СоздатьДокумент();
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.ДатаСоставления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			ДеревоДанных, "ДатаДокумента");
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	ДокументОбъект.Контрагент  = КонтрагентПоДаннымЭД(ДеревоДанных,  "СведенияОПродавце");
	ДокументОбъект.Организация = ОрганизацияПоДаннымЭД(ДеревоДанных, "СведенияОПокупателе");
	
	ДокументОбъект.ПолученВЭлектронномВиде = Истина;
	ДокументОбъект.Номер = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента");
	
	ДокументОбъект.НомерИсправления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления");
	Если ЗначениеЗаполнено(ДокументОбъект.НомерИсправления) И ТипЗнч(ДокументОбъект.НомерИсправления) = Тип("Число") Тогда
		ДокументОбъект.Исправление = Истина;
		ДокументОбъект.ДатаИсправления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления");
	КонецЕсли;
	ДокументОбъект.ДокументыОснования.Очистить();
	ДокументыОснованияСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ДокументыОснованияСчетаФактуры");
	Если ЗначениеЗаполнено(ДокументыОснованияСчетаФактуры) Тогда
		ДокументОбъект.ДокументыОснования.Очистить();
		Для Каждого ДокументОснования Из ДокументыОснованияСчетаФактуры Цикл
			Если ТипЗнч(ДокументОснования) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ДокументОбъект.СчетФактураОснование = ДокументОснования;
			Иначе
				СтрокаТаблицы = ДокументОбъект.ДокументыОснования.Добавить();
				СтрокаТаблицы.ДокументОснование = ДокументОснования;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		СтрокаОснования = ДокументОбъект.ДокументыОснования.Найти(ПервичныйДокумент, "ДокументОснование");
		Если СтрокаОснования = Неопределено Тогда
			СтрокаТаблицы = ДокументОбъект.ДокументыОснования.Добавить();
			СтрокаТаблицы.ДокументОснование = ПервичныйДокумент;
		КонецЕсли;
	КонецЕсли;
	
	ЗавершениеЗагрузкиИЗаписьДокумента(СсылкаНаВладельца, ДокументОбъект, Записывать);
	
КонецПроцедуры


// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьУниверсальныйПередаточныйДокумент_2019.
// Сохраняет данные из электронного документа в объекты ИБ (УПД версии 2019).
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     * ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     * СчетФактура       - Строка - способ сохранения счета-фактуры.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУниверсальныйПередаточныйДокумент_2019(ДеревоДанных, СсылкиНаВладельцев = Неопределено, СпособОбработки = Неопределено, ОписаниеОшибки = "") Экспорт
	
	ПервичныйДокумент = Неопределено;
	СчетФактура = Неопределено;
	
	СпособОбработкиПервичногоДокумента = "";
	СпособОбработкиСчетаФактуры = "";
	
	Если ЗначениеЗаполнено(СпособОбработки) И ТипЗнч(СпособОбработки) = Тип("Структура") Тогда
		
		Если СпособОбработки.Свойство("ПервичныйДокумент") Тогда
			СпособОбработкиПервичногоДокумента = СпособОбработки.ПервичныйДокумент;
		КонецЕсли;
		
		Если СпособОбработки.Свойство("СчетФактура") Тогда
			СпособОбработкиСчетаФактуры = СпособОбработки.СчетФактура;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СсылкиНаВладельцев <> Неопределено Тогда
		Для Каждого Строка Из СсылкиНаВладельцев Цикл
			Если ТипЗнч(Строка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				СчетФактура = Строка;
			Иначе
				ПервичныйДокумент = Строка;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	ДокументыУчета = Новый Массив;
	НайтиСоздатьУПДДокументОПередаче_2019(ДеревоДанных, ПервичныйДокумент, Истина, СпособОбработкиПервичногоДокумента, 
		ОписаниеОшибки);
	ДокументыУчета.Добавить(ПервичныйДокумент);
	
	// Заполним основание в СФ.
	ДокументыОснованияСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ДокументыОснованияСчетаФактуры");
	Если ЗначениеЗаполнено(ДокументыОснованияСчетаФактуры) Тогда
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
	Иначе
		ДокументыОснованияСчетаФактуры = Новый Массив;
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		ДокументыОснованияСчетаФактуры);
	
	НайтиСоздатьУПДСчетФактуру_2019(ДеревоДанных, СчетФактура, СпособОбработкиСчетаФактуры, ОписаниеОшибки);
	ДокументыУчета.Добавить(СчетФактура);
	
	СсылкиНаВладельцев = ДокументыУчета;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьУПДДокументОПередаче_2019.
Процедура НайтиСоздатьУПДДокументОПередаче_2019(ДеревоДанных, СсылкаНаВладельца, Записывать, СпособОбработки, ОписаниеОшибки) Экспорт
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Текст = НСтр("ru = 'Операция возможна только для непроведенных документов!'");
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.ПоступлениеТоваров.СоздатьДокумент();
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ДокументОбъект.НомерВходящегоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента");
	ДокументОбъект.ДатаВходящегоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента");
	
	СведенияОПродавце = ДеревоДанных.Строки.Найти("СведенияОПродавце", "ПолныйПуть");
	СведенияОПокупателе = ДеревоДанных.Строки.Найти("СведенияОПокупателе", "ПолныйПуть");
	Если СведенияОПродавце.Строки.Количество() > 1
		ИЛИ СведенияОПокупателе.Строки.Количество() > 1 Тогда
		ТекстИсключения = СтрШаблон(НСтр("ru = 'Ошибка загрузки электронной товарной накладной %1 от %2.'"), 
			ДокументОбъект.НомерВходящегоДокумента, Формат(ДокументОбъект.ДатаВходящегоДокумента,"ДЛФ=D")) + Символы.ПС
			+ НСтр("ru = 'Загрузка сводных накладных не поддерживается.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ОсновнаяСтрокаПродавца = Неопределено;
	Для каждого СтрокаПродавца Из СведенияОПродавце.Строки Цикл
		ДокументОбъект.Контрагент = КонтрагентПоДаннымЭД(СтрокаПродавца, "СведенияОПродавце.НомерСтроки");
		ОсновнаяСтрокаПродавца = СтрокаПродавца;
		Прервать;
	КонецЦикла;
	Для каждого СтрокаПокупателя Из СведенияОПокупателе.Строки Цикл
		ДокументОбъект.Организация = ОрганизацияПоДаннымЭД(СтрокаПокупателя, "СведенияОПокупателе.НомерСтроки");
		Прервать;
	КонецЦикла;
	
	// Поиск и заполнение банковского счета контрагента.
	НомерСчета = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ОсновнаяСтрокаПродавца,
		"СведенияОПродавце.НомерСтроки.БанковскиеРеквизиты.НомерСчета");
	БИКБанка = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ОсновнаяСтрокаПродавца,
		"СведенияОПродавце.НомерСтроки.БанковскиеРеквизиты.БИКБанка");
	ДокументОбъект.БанковскийСчетКонтрагента = БанковскийСчетКонтрагента(
		ДокументОбъект.Контрагент, БИКБанка, НомерСчета);
	
	ДокументОбъект.УстановитьНовыйНомер();
	
	ЕстьТовары = Ложь;
	ЕстьРаботы = Ложь;
	ЕстьПрава  = Ложь;
	
	ОблагаетсяНДСУПокупателя = Ложь;
	
	ДокументОбъект.Товары.Очистить();
	
	СведенияОНоменклатуре = ТаблицаСведенийОНоменклатуреПоставщика();
	
	//ГосИС.МОТП
	ШтрихкодыУпаковок = ИнтеграцияМОТП.НоваяТаблицаШтрихкодыУпаковок();
	//Конец ГосИс.МОТП
	
	// Сведения таблицы.
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		Признак = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Признак");
		
		Если Признак = "1" Тогда
			ЕстьТовары = Истина;
		ИначеЕсли Признак = "2" ИЛИ Признак = "3" Тогда
			ЕстьРаботы = Истина;
		ИначеЕсли Признак = "4" Тогда
			ЕстьПрава  = Истина;
		КонецЕсли;
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		
		НоваяСтрока.Количество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			"СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			"СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			"СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога");
		
		КоличествоМест = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
			"СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.КоличествоУпаковок = КоличествоМест;
		Если НоваяСтрока.КоличествоУпаковок = 0 Тогда
			НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		Иначе
			НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, 
				"СведенияОТоварах.НомерСтроки.СтоимостьТоваровСНалогом");
			НоваяСтрока.Цена = Окр(НоваяСтрока.Сумма / НоваяСтрока.КоличествоУпаковок, 2, 1);
		КонецЕсли;
		
		СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			"СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если СтавкаНДС = "НДС исчисляется налоговым агентом" Тогда
			НоваяСтрока.СуммаСНДС = НоваяСтрока.Сумма;
			ОблагаетсяНДСУПокупателя = Истина;
		Иначе
			НоваяСтрока.СтавкаНДС = СтавкаНДС;
			НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.СуммаНалога");
		КонецЕсли;
		
		СведенияОТаможеннойДекларации = СведенияОТоваре.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации", "ПолныйПуть", Истина);
		
		Если СведенияОТаможеннойДекларации <> Неопределено И СведенияОТаможеннойДекларации.Строки.Количество() > 0 Тогда
			НомерТаможеннойДекларации = ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаВДереве(СведенияОТаможеннойДекларации.Строки[0],
			"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.ТаможеннаяДекларацияНомер");
			КодСтраныПроисхождения = ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаВДереве(СведенияОТаможеннойДекларации.Строки[0],
			"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.СтранаПроисхожденияКод");
			Если ЗначениеЗаполнено(НомерТаможеннойДекларации) Тогда
				ТаможеннаяДекларация = Неопределено;
				СтранаПроисхождения = Неопределено;
				ЭлектронноеВзаимодействиеПереопределяемый.НайтиСсылкуНаОбъект("ТаможенныеДекларации", ТаможеннаяДекларация, 
					НомерТаможеннойДекларации);
				ЭлектронноеВзаимодействиеПереопределяемый.НайтиСсылкуНаОбъект("СтраныМира", СтранаПроисхождения, 
					КодСтраныПроисхождения);
				Если НЕ ЗначениеЗаполнено(ТаможеннаяДекларация) Тогда
					НоваяТаможеннаяДекларация = Справочники.НомераГТД.СоздатьЭлемент();
					НоваяТаможеннаяДекларация.Код = НомерТаможеннойДекларации;
					НоваяТаможеннаяДекларация.СтранаПроисхождения = СтранаПроисхождения;
					НоваяТаможеннаяДекларация.Записать();
					ТаможеннаяДекларация = НоваяТаможеннаяДекларация.Ссылка;
				КонецЕсли;
				НоваяСтрока.НомерГТД = ТаможеннаяДекларация;
			КонецЕсли;
		КонецЕсли;
		
		ОбменСКонтрагентамиРТКлиентСервер.ЗаполнитьИдентификаторСтроки(НоваяСтрока);
		
		Сопоставление = СведенияОТоваре.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.Сопоставление", "ПолныйПуть", Истина);
		Если Сопоставление <> Неопределено Тогда
			
			Номенклатура = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.Сопоставление.НоменклатураИБ");
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				НоваяСтрока.Номенклатура = Номенклатура;
			КонецЕсли;
			
			Характеристика = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.Сопоставление.ХарактеристикаИБ");
			Если ЗначениеЗаполнено(Характеристика) Тогда
				НоваяСтрока.Характеристика = Характеристика;
			КонецЕсли;
			
			ЕдиницаИзмерения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.Сопоставление.УпаковкаИБ");
			Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
				Если ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.УпаковкиНоменклатуры") Тогда
					НоваяСтрока.Упаковка = ЕдиницаИзмерения;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок;
		
		//выполним пересчет количества товара если поступление в упаковках
		КоэффициентУпаковки = ?(ЗначениеЗаполнено(НоваяСтрока.Упаковка),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Упаковка, "Коэффициент"), 1);
		Если КоэффициентУпаковки <> 0 И КоэффициентУпаковки <> 1 Тогда
			НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		КонецЕсли;
		
		Если НоваяСтрока.Количество = 0
			И (Признак = "2" ИЛИ Признак = "3") Тогда
			НоваяСтрока.Количество = 1;
		КонецЕсли;
		
		Если НоваяСтрока.Цена = 0
			И (Признак = "2" ИЛИ Признак = "3") Тогда
			НоваяСтрока.Цена = НоваяСтрока.Сумма / НоваяСтрока.Количество;
		КонецЕсли;
		
		//ГосИС.МОТП
		ШтрихкодыУпаковокСтрокиТЧ = ЭлектронноеВзаимодействиеМОТП.ШтрихкодыУпаковокИзСтрокиДереваЭДО_2019(СведенияОТоваре);
		Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
			
			НоваяСтрокаТЧШтрихкодыУпаковок = ШтрихкодыУпаковок.Добавить();
			НоваяСтрокаТЧШтрихкодыУпаковок.ЗначениеШтрихкода = ЗначениеШтрихкода;
			
		КонецЦикла;
		//Конец ГосИс.МОТП
		
	КонецЦикла;
	
	//ГосИС.МОТП
	ИнтеграцияМОТП.СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
	ДокументОбъект.ШтрихкодыУпаковок.Загрузить(ШтрихкодыУпаковок);
	//Конец ГосИс.МОТП
	
	ДокументОбъект.УчитыватьНДС = ОблагаетсяНДСУПокупателя;
	
	Если ОблагаетсяНДСУПокупателя Тогда
		ДокументОбъект.СуммаДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных,
			"ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога");
	Иначе
		ДокументОбъект.СуммаДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных,
			"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом");
	КонецЕсли;
	
	РежимЗаписиПроведение = Истина;
	
	Если Записывать Тогда
		ЗаписатьДокумент(ДокументОбъект, РежимЗаписиПроведение);
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Иначе // если функция вызвана из формы "Обработки.ОбменСКонтрагентами.ЗагрузкаПросмотрЭлектронногоДокумента"
		СсылкаНаВладельца = ДокументОбъект;
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьУПДСчетФактуру_2019.
Процедура НайтиСоздатьУПДСчетФактуру_2019(ДеревоДанных, СсылкаНаВладельца = Неопределено, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) 
		И ТипЗнч(СсылкаНаВладельца) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
	Иначе
		ДокументОбъект = Документы.СчетФактураПолученный.СоздатьДокумент();
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.Дата = ТекущаяДатаСеанса;
	КонецЕсли;
	
	ДокументОбъект.ПолученВЭлектронномВиде = Истина;
	
	ВидСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры");
	Если ВидСчетаФактуры = "Авансовый" Тогда
		ДокументОбъект.КодВидаОперации = "02";;
	Иначе
		ДокументОбъект.КодВидаОперации = "01";;
	КонецЕсли;
	
	ДокументОбъект.Номер = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента");
	ДокументОбъект.ДатаСоставления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента");
	
	ДокументОбъект.Исправление = Ложь;
	НомерИсправления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления");
	Если ТипЗнч(НомерИсправления) = Тип("Число") Тогда
		Если НомерИсправления > 0 Тогда
			ДокументОбъект.НомерИсправления = НомерИсправления;
			ДокументОбъект.Исправление = Истина;
			ДокументОбъект.ДатаИсправления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				ДеревоДанных, "ДатаИсправления");
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(НомерИсправления) Тогда
		ДокументОбъект.НомерИсправления = НомерИсправления;
		ДокументОбъект.Исправление = Истина;
		ДокументОбъект.ДатаИсправления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			ДеревоДанных, "ДатаИсправления");
	КонецЕсли;
		
	ДокументОбъект.Валюта = "RUB";
	
	СведенияОПродавце = ДеревоДанных.Строки.Найти("СведенияОПродавце", "ПолныйПуть");
	СведенияОПокупателе = ДеревоДанных.Строки.Найти("СведенияОПокупателе", "ПолныйПуть");
	Если СведенияОПродавце.Строки.Количество() > 1
		ИЛИ СведенияОПокупателе.Строки.Количество() > 1 Тогда
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Ошибка загрузки электронного счета-фактуры %1 от %2.'"), 
			ДокументОбъект.НомерЭД, Формат(ДокументОбъект.ДатаЭД,"ДЛФ=D")) + Символы.ПС
			+ НСтр("ru = 'Загрузка сводных счетов-фактур не поддерживается.'");
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаПродавца Из СведенияОПродавце.Строки Цикл
		ДокументОбъект.Контрагент = КонтрагентПоДаннымЭД(СтрокаПродавца, "СведенияОПродавце.НомерСтроки");
		Прервать;
	КонецЦикла;
	Для каждого СтрокаПокупателя Из СведенияОПокупателе.Строки Цикл
		ДокументОбъект.Организация = ОрганизацияПоДаннымЭД(СтрокаПокупателя, "СведенияОПокупателе.НомерСтроки");
		Прервать;
	КонецЦикла;
	
	ОснованияОтгрузки = ДеревоДанных.Строки.Найти("ОснованиеОтгрузкиТоваров", "ПолныйПуть");
	
	ДокументОбъект.ДокументыОснования.Очистить();
	ДокументыОснованияСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ДокументыОснованияСчетаФактуры");
	Если ЗначениеЗаполнено(ДокументыОснованияСчетаФактуры) Тогда
		ДокументОбъект.ДокументыОснования.Очистить();
		Для Каждого ДокументОснования Из ДокументыОснованияСчетаФактуры Цикл
			Если ТипЗнч(ДокументОснования) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ДокументОбъект.СчетФактураОснование = ДокументОснования;
			Иначе
				СтрокаТаблицы = ДокументОбъект.ДокументыОснования.Добавить();
				СтрокаТаблицы.ДокументОснование = ДокументОснования;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РежимЗаписиПроведение = Истина;
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписиПроведение);
	СсылкаНаВладельца = ДокументОбъект.Ссылка;
	
КонецПроцедуры


#КонецОбласти

// См. ОбменСКонтрагентамиПереопределяемый.ПриПроверкеВозможностиСозданияУчетногоДокумента.
// Выполняется при проверке возможности создания учетного документа по электронному документу.
//
// Параметры:
//  Параметры - Структура - параметры электронного документа, отражаемого в учете.
//   * ЭтоСводныйУПД - Булево - электронный документ имеет формат УПД и содержит несколько продавцов, покупателей и т.п.
//  Отказ - Булево - признак отказа от отражения в учете. Если установить Истина, то документ не отражается в учете (для изменения). По умолчанию Ложь.
//  Описание - Строка - описание причины отказа от отражения в учете.
//
Процедура ПриПроверкеВозможностиСозданияУчетногоДокумента(Знач Параметры, Отказ, Описание) Экспорт
	
	Если Параметры.ЭтоСводныйУПД Тогда
		
		Отказ = Истина;
		Описание = НСтр("ru = 'Отражение в учете сводных счетов-фактур не поддерживается.'");
		
	КонецЕсли;
	
КонецПроцедуры

#Область НастройкиФормированияДокумента

// См. ОбменСКонтрагентамиПереопределяемый.ПриОпределенииВариантовЗаполненияПолейЭлектронныхДокументов.
Процедура ПриОпределенииВариантовЗаполненияПолейЭлектронныхДокументов(ВариантыЗаполненияПолей) Экспорт
	
	ВариантыЗаполненияПолей.УПД_ТоварКод.Добавить("Артикул", НСтр("ru = 'Артикул'"));
	ВариантыЗаполненияПолей.ПередачаТоваров_ТоварКод.Добавить("Артикул", НСтр("ru = 'Артикул'"));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьЗначениеРеквизитаДерева(СтрокаДерева, ИмяРеквизита, ВключатьПодчиненные = Ложь, ДеревоРазбора = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если СтрокаДерева.Строки.Количество() > 0 Тогда 
		НайденнаяСтрока = СтрокаДерева.Строки.Найти(ИмяРеквизита, "Реквизит", ВключатьПодчиненные);
	Иначе // Передали строку с реквизитом
		НайденнаяСтрока = СтрокаДерева;
	КонецЕсли;
	
	Если НайденнаяСтрока <> Неопределено Тогда
		Результат = НайденнаяСтрока.ЗначениеРеквизита;
		
		Если ИмяРеквизита = "СтавкаНДС" ИЛИ ИмяРеквизита = "НалСтВел" Тогда
			Результат = НайтиПеречисление("НДС", Результат);
		Иначе
			// Если реквизит ссылочного типа (передали реквизит ДеревоРазбора), тогда нашли всего лишь индекс строки
			Если ЗначениеЗаполнено(ДеревоРазбора) Тогда 
				НайденнаяСтрока = ДеревоРазбора.Строки.Найти(Результат, "ИндексСтроки", Истина);
				Если НайденнаяСтрока <> Неопределено Тогда
					Результат = НайденнаяСтрока.СсылкаНаОбъект;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция НайтиПеречисление(Наименование, ПредставлениеПеречисления)
	
	НайденноеЗначение = Неопределено;
	Если Наименование = "НДС" Тогда
		ИмяПеречисления = "СтавкиНДС";
		Если НЕ ТипЗнч(ПредставлениеПеречисления)=Тип("ПеречислениеСсылка.СтавкиНДС") Тогда // определяем по числу
			Возврат ОбменСКонтрагентамиПовтИсп.СтавкаНДСИзСоответствия(ПредставлениеПеречисления); 
		КонецЕсли;
	ИначеЕсли Наименование = "ТипНоменклатуры" Тогда
		ИмяПеречисления = "ТипыНоменклатуры";
	ИначеЕсли Наименование = "ТипыНалогообложенияНДС" Тогда
		ИмяПеречисления = "ТипыНалогообложенияНДС";	
	ИначеЕсли Наименование = "СпособыРасчетаКомиссионногоВознаграждения" Тогда
		ИмяПеречисления = "СпособыРасчетаКомиссионногоВознаграждения";	
	ИначеЕсли Наименование = "ВариантыОплатыКлиентом" Тогда
		ИмяПеречисления = "ВариантыОплатыКлиентом";	
	ИначеЕсли Наименование = "ВариантыОплатыПоставщику" Тогда
		ИмяПеречисления = "ВариантыОплатыПоставщику";			
	ИначеЕсли Наименование = "ФормыОплаты" Тогда
		ИмяПеречисления = "ФормыОплаты";	
	ИначеЕсли Наименование = "СпособРасчета" Тогда
		ИмяПеречисления = "СпособыРасчетаКомиссионногоВознаграждения";	
		Если ПредставлениеПеречисления = "От суммы" Тогда
			ПредставлениеПеречисления = НСтр("ru = 'Процент от суммы продажи'");
		ИначеЕсли ПредставлениеПеречисления = "От разницы" Тогда
			ПредставлениеПеречисления = НСтр("ru = 'Процент от разности суммы продажи и суммы комитента'");
		КонецЕсли;	
	Иначе
		Возврат НайденноеЗначение;
	КонецЕсли;
	
	Для Каждого ЭлПеречисления Из Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления Цикл
		Если Врег(ЭлПеречисления.Синоним) = Врег(ПредставлениеПеречисления) тогда
			НайденноеЗначение = Перечисления[ИмяПеречисления][ЭлПеречисления.Имя];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденноеЗначение;
	
КонецФункции

Процедура ЗагрузитьКаталогТоваров(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать)
	
	Если СсылкаНаВладельца = Неопределено Тогда
		Контрагент = Неопределено;
	Иначе
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаВладельца, "Контрагент");
	КонецЕсли;
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		СтрокиТЧ = СтрокаДляЗагрузки.Строки.НайтиСтроки(Новый Структура("Реквизит", "СтрокаТЧ"));
		ТаблицаЗагрузки = Новый ТаблицаЗначений;
		ТаблицаЗагрузки.Колонки.Добавить("Идентификатор", ВариантыОтчетов.ОписаниеТиповСтрока(200));
		
		Для Каждого СтрокаТЧ Из СтрокиТЧ Цикл
			
			ИдентификаторТовара = ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаСтрокиДереваРазбора(ДеревоРазбора, 
				СтрокаТЧ, "НоменклатураПоставщика.Ид");
			Если ЗначениеЗаполнено(ИдентификаторТовара) Тогда
				
				НовСтрока = ТаблицаЗагрузки.Добавить();
				НовСтрока.Идентификатор = ИдентификаторТовара;
				
			КонецЕсли;
			
			
		КонецЦикла;
		
		Если ТаблицаЗагрузки.Количество() > 0 Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор
			|ПОМЕСТИТЬ ТаблицаЗагрузки
			|ИЗ
			|	&ТаблицаЗагрузки КАК ТаблицаЗагрузки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Поставщик КАК Поставщик,
			|	&ПустойМагазин КАК Магазин,
			|	НоменклатураПоставщиков.Номенклатура КАК Номенклатура,
			|	НоменклатураПоставщиков.Характеристика КАК Характеристика,
			|	НоменклатураПоставщиков.Упаковка КАК Упаковка,
			|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор,
			|	НоменклатураПоставщиков.Цена КАК Цена,
			|	НоменклатураПоставщиков.ДатаПоследнегоПоступления КАК ДатаПоследнегоПоступления,
			|	НоменклатураПоставщиков.Артикул КАК Артикул
			|ИЗ
			|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПоставщиков КАК НоменклатураПоставщиков
			|		ПО НоменклатураПоставщиков.Поставщик = &Поставщик
			|			И ТаблицаЗагрузки.Идентификатор = НоменклатураПоставщиков.Идентификатор";
			Запрос.УстановитьПараметр("ТаблицаЗагрузки", ТаблицаЗагрузки);
			Запрос.УстановитьПараметр("Поставщик", Контрагент);
			Запрос.УстановитьПараметр("ПустойМагазин", Справочники.Магазины.ПустаяСсылка());
			Выборка = Запрос.Выполнить().Выбрать();
			
			ДеревоКартинок = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДеревоКартинок");
			ЕстьКартинки = ДеревоКартинок <> Неопределено;
			Пока Выборка.Следующий() Цикл
				МенеджерЗаписи = РегистрыСведений.НоменклатураПоставщиков.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
				МенеджерЗаписи.Записать(Истина);
				Если ЕстьКартинки Тогда
					СтрокаТовара = ДеревоКартинок.Строки.Найти(Выборка.Идентификатор, "ИдентификаторТовара");
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗагрузитьПрайсЛист(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ТипОбъекта", "Контрагенты");
	СтрокиКонтрагента = ДеревоРазбора.Строки.НайтиСтроки(СтруктураПоиска);
	Если СтрокиКонтрагента.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если СтрокиКонтрагента[0].Строки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Контрагент = СтрокиКонтрагента[0].Строки[0].СсылкаНаОбъект;
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеДляЗагрузки = Новый Структура;
	Товары = Документы.УстановкаЦенНоменклатуры.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	
	СтрокиТЧ = СтрокаДляЗагрузки.Строки.НайтиСтроки(Новый Структура("Реквизит", "СтрокаТЧ"));
	ТаблицаЗагрузки = Новый ТаблицаЗначений;
	ТаблицаЗагрузки.Колонки.Добавить("Идентификатор", ВариантыОтчетов.ОписаниеТиповСтрока(200));
	ТаблицаЗагрузки.Колонки.Добавить("Цена", ОбщегоНазначенияРТКлиентСервер.ПолучитьОписаниеТиповЧисла(15, 2));

	Для Каждого СтрокаТЧ Из СтрокиТЧ Цикл
		
		ИдентификаторТовара = ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаСтрокиДереваРазбора(ДеревоРазбора, 
			СтрокаТЧ, "Ид");
		Если ЗначениеЗаполнено(ИдентификаторТовара) Тогда
			Цена = ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаСтрокиДереваРазбора(ДеревоРазбора, СтрокаТЧ, "Цена");
			
			НовСтрока = ТаблицаЗагрузки.Добавить();
			НовСтрока.Идентификатор = ИдентификаторТовара;
			НовСтрока.Цена = Цена;
			
		КонецЕсли;
		
		
	КонецЦикла;

	Если ТаблицаЗагрузки.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор,
		|	ТаблицаЗагрузки.Цена КАК Цена
		|ПОМЕСТИТЬ ТаблицаЗагрузки
		|ИЗ
		|	&ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Поставщик КАК Поставщик,
		|	&ПустойМагазин КАК Магазин,
		|	НоменклатураПоставщиков.Номенклатура КАК Номенклатура,
		|	НоменклатураПоставщиков.Характеристика КАК Характеристика,
		|	НоменклатураПоставщиков.Упаковка КАК Упаковка,
		|	ТаблицаЗагрузки.Идентификатор КАК Идентификатор,
		|	ТаблицаЗагрузки.Цена КАК Цена,
		|	НоменклатураПоставщиков.ДатаПоследнегоПоступления КАК ДатаПоследнегоПоступления,
		|	НоменклатураПоставщиков.Артикул КАК Артикул
		|ИЗ
		|	ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО НоменклатураПоставщиков.Поставщик = &Поставщик
		|			И ТаблицаЗагрузки.Идентификатор = НоменклатураПоставщиков.Идентификатор";
		Запрос.УстановитьПараметр("ТаблицаЗагрузки", ТаблицаЗагрузки);
		Запрос.УстановитьПараметр("Поставщик", Контрагент);
		Запрос.УстановитьПараметр("ПустойМагазин", Справочники.Магазины.ПустаяСсылка());
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.НоменклатураПоставщиков.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Контрагент;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение документов

Функция СоздатьПерезаполнитьБанковскийСчетКонтрагента(СтрокаОбъекта, ДеревоРазбора)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(СтрокаОбъекта.СсылкаНаОбъект) Тогда
		НовЭл = СтрокаОбъекта.СсылкаНаОбъект.ПолучитьОбъект();
	Иначе
		НовЭл = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
	КонецЕсли;
	
	НовЭл.НомерСчета = ПолучитьЗначениеРеквизитаДерева(СтрокаОбъекта, "НомерСчета");
	НовЭл.Владелец = ПолучитьЗначениеРеквизитаДерева(СтрокаОбъекта, "Владелец");
	НовЭл.Банк = ПолучитьЗначениеРеквизитаДерева(СтрокаОбъекта, "Банк", , ДеревоРазбора);
	НовЭл.Наименование = СтрокаОбъекта.ОписаниеОбъекта;
	
	НовЭл.ОбменДанными.Загрузка = Истина;
	Попытка
		НовЭл.Записать();
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось создать банковский счет контрагента.'")
			+ Символы.ПС + НСтр("ru = 'Подробности см. в Журнале регистрации.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание элемента справочника Банковские счета.'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			НовЭл.Метаданные(),,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
	КонецПопытки;
	
	СсылкаНаОбъект = НовЭл.Ссылка;
	
	Возврат СсылкаНаОбъект;
	
КонецФункции

Функция ОрганизацияПоДаннымЭД(ДеревоДанных, ВидУчастника)
	
	Организация = Неопределено;
	Если ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ЮЛ" Тогда
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.ИНН");
		КПП = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.КПП");
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Организации.Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И Организации.КПП = &КПП
			|	И НЕ Организации.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Организация = Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ИП" Тогда
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.ИНН");
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Организации.Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И НЕ Организации.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Организация = Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ИЛ" Тогда
		НаименованиеПолное = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
			ВидУчастника + ".ТипУчастника.ИЛ.НаименованиеОрганизации");
		Если ЗначениеЗаполнено(НаименованиеПолное) Тогда
			Запрос = Новый Запрос();
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
				|	Организации.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Организации КАК Организации
				|ГДЕ
				|	Организации.НаименованиеПолное = &НаименованиеПолное
				|	И НЕ Организации.ПометкаУдаления";
			Запрос.УстановитьПараметр("НаименованиеПолное", НаименованиеПолное);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Организация = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Организация;
	
КонецФункции

Функция КонтрагентПоДаннымЭД(ДеревоДанных, ВидУчастника)
	
	Контрагент = Неопределено;
	Если ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ЮЛ" Тогда
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.ИНН");
		КПП = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.КПП");
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &ИНН
			|	И Контрагенты.КПП = &КПП
			|	И НЕ Контрагенты.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Контрагент = Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ИП" Тогда
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.ИНН");
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &ИНН
			|	И НЕ Контрагенты.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Контрагент = Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ИЛ" Тогда
		НаименованиеПолное = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, 
			ВидУчастника + ".ТипУчастника.ИЛ.НаименованиеОрганизации");
		Если ЗначениеЗаполнено(НаименованиеПолное) Тогда
			Запрос = Новый Запрос();
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
				|	Контрагенты.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Контрагенты КАК Контрагенты
				|ГДЕ
				|	Контрагенты.НаименованиеПолное = &НаименованиеПолное
				|	И НЕ Контрагенты.ПометкаУдаления";
			Запрос.УстановитьПараметр("НаименованиеПолное", НаименованиеПолное);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Контрагент = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника") = "ФЛ" Тогда
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ФЛ.ИНН");
		
		Фамилия  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ФЛ.Фамилия");
		Имя      = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ФЛ.Имя");
		Отчество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ФЛ.Отчество");
		
		ЭлементыИмени = Новый Массив;
		Если ЗначениеЗаполнено(Фамилия) Тогда
			ЭлементыИмени.Добавить(Фамилия);
		КонецЕсли;
		Если ЗначениеЗаполнено(Имя) Тогда
			ЭлементыИмени.Добавить(Имя);
		КонецЕсли;
		Если ЗначениеЗаполнено(Отчество) Тогда
			ЭлементыИмени.Добавить(Отчество);
		КонецЕсли;
		ПолноеНаименование = СтрСоединить(ЭлементыИмени, " ");
		
		Если ЗначениеЗаполнено(ИНН) Или ЗначениеЗаполнено(НаименованиеПолное) Тогда 
			Запрос = Новый Запрос();
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
				|	Контрагенты.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Контрагенты КАК Контрагенты
				|ГДЕ
				|	Контрагенты.ИНН = &ИНН";
			Если ЗначениеЗаполнено(ИНН) Тогда
				Запрос.УстановитьПараметр("ИНН", ИНН);
			Иначе
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Контрагенты.ИНН = &ИНН", "Контрагенты.НаименованиеПолное = &НаименованиеПолное");
				Запрос.УстановитьПараметр("НаименованиеПолное", ПолноеНаименование);
			КонецЕсли;
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Контрагент = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	Возврат Контрагент;
	
КонецФункции

Функция БанковскийСчетКонтрагента(Владелец, БИКБанка, НомерСчета)
	
	БанковскийСчет = Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка();
	
	Если НЕ ЗначениеЗаполнено(НомерСчета) Тогда
		Возврат БанковскийСчет;
	КонецЕсли;
	
	БанковскийСчет = Справочники.БанковскиеСчетаКонтрагентов.НайтиПоРеквизиту("НомерСчета", НомерСчета,, Владелец);
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат БанковскийСчет;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БИКБанка) Тогда
		Банк = ЭлектронноеВзаимодействиеРТ.НайтиСсылкуНаОбъектПоРеквизиту("КлассификаторБанков", "Код", БИКБанка);
		Если ЗначениеЗаполнено(Банк) Тогда
			НовыйБанковскийСчет = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
			НовыйБанковскийСчет.Владелец              = Владелец;
			НовыйБанковскийСчет.Банк                  = Банк;
			НовыйБанковскийСчет.НомерСчета            = НомерСчета;
			НовыйБанковскийСчет.Наименование          = НаименованиеБанковскогоСчетаПоУмолчанию(НомерСчета, Строка(Банк));
			
			НовыйБанковскийСчет.ОбменДанными.Загрузка = Истина;
			Попытка
				НовыйБанковскийСчет.Записать();
				БанковскийСчет = НовыйБанковскийСчет.Ссылка;
			Исключение
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать банковский счет контрагента.
					|Номер счета: %1, БИК: %2.'"), НомерСчета, БИКБанка)
					+ Символы.ПС + НСтр("ru = 'Подробности см. в Журнале регистрации.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание элемента справочника Банковские счета контрагентов.'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,
					НовыйБанковскийСчет.Метаданные(),,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ВызватьИсключение;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат БанковскийСчет;
	
КонецФункции

// Заполняет структуру значениями реквизитов банковского счета.
//
// Параметры:
//  Реквизиты		 - Структура - Структура для заполнения данными банковского счета.
//     * БанковскийСчетСсылка - СправочникСсылка.БанковскиеСчета - Ссылка на банковский счет.
//     * БанкСсылка           - СправочникСсылка.КлассификаторБанков - Ссылка на банк.
//     * НомерСчета           - Строка - Номер расчетного счета.
//     * Банк                 - Строка - Наименование банка.
//     * БИК                  - Строка - БИК банка.
//     * АдресБанка           - Строка - Адрес банка.
//     * КоррСчет             - Строка - Корреспондентский счет.
//  БанковскийСчет	 - СправочникСсылка.БанковскиеСчета - Ссылка на банковский счет.
//
Процедура ЗаполнитьРеквизитыБанковскогоСчета(Реквизиты, БанковскийСчет, ИмяСправочника) Экспорт
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК БанковскийСчетСсылка,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.Банк КАК БанкСсылка,
	|	ЕСТЬNULL(БанковскиеСчета.НаименованиеБанка, ЕСТЬNULL(БанковскиеСчета.Банк.Наименование, """")) КАК Банк,
	|	ЕСТЬNULL(БанковскиеСчета.БИКБанка, ЕСТЬNULL(БанковскиеСчета.Банк.Код, """")) КАК БИК,
	|	ЕСТЬNULL(БанковскиеСчета.АдресБанка, ЕСТЬNULL(БанковскиеСчета.Банк.Адрес, """")) КАК АдресБанка,
	|	ЕСТЬNULL(БанковскиеСчета.КоррСчетБанка, ЕСТЬNULL(БанковскиеСчета.Банк.КоррСчет, """")) КАК КоррСчет
	|ИЗ
	|	Справочник."+ИмяСправочника+" КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Реквизиты.Вставить("БанковскийСчетСсылка", Выборка.БанковскийСчетСсылка);
		Реквизиты.Вставить("БанкСсылка",           Выборка.БанкСсылка);
		Реквизиты.Вставить("НомерСчета",           Выборка.НомерСчета);
		Реквизиты.Вставить("Банк",                 Выборка.Банк);
		Реквизиты.Вставить("БИК",                  Выборка.БИК);
		Реквизиты.Вставить("АдресБанка",           Выборка.АдресБанка);
		Реквизиты.Вставить("КоррСчет",             Выборка.КоррСчет);
	КонецЕсли;
	
КонецПроцедуры


// Формирует текст наименования банковского счета по умолчанию.
//
// Параметры:
//  НомерСчета - Строка - номер банковского счета;
//  ПредставлениеБанка - Строка - наименование банка;
//  ПредставлениеВалюты - Строка - наименование валюты;
//  Валютный - Булево - признак валютного счета.
//  Вариант - Число - Вариант формирования наименования;
// 
// Возвращаемое значение:
//  Строка - наименование по умолчанию.
//
Функция НаименованиеБанковскогоСчетаПоУмолчанию(Знач НомерСчета, ПредставлениеБанка) Экспорт
	
	ЭлементыНаименования = Новый Массив;
	
	НомерСчета = СокрЛП(НомерСчета);
	
	Если Не ПустаяСтрока(НомерСчета) Тогда
		ЭлементыНаименования.Добавить(Прав(НомерСчета, 4));
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПредставлениеБанка) Тогда
		ЭлементыНаименования.Добавить(ПредставлениеБанка);
	КонецЕсли;
	
	Наименование = СтрСоединить(ЭлементыНаименования, ", ");
	
	Возврат Лев(Наименование, 100);
	
КонецФункции

Функция ТаблицаСведенийОНоменклатуреПоставщика()
	
	СведенияОНоменклатуре = Новый ТаблицаЗначений;
	СведенияОНоменклатуре.Колонки.Добавить("НомерСтроки",
		Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 0)));
	СведенияОНоменклатуре.Колонки.Добавить("Владелец",
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	СведенияОНоменклатуре.Колонки.Добавить("Идентификатор",
		Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(110)));
	СведенияОНоменклатуре.Колонки.Добавить("Наименование",
		Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(110)));
	СведенияОНоменклатуре.Колонки.Добавить("ЕдиницаИзмеренияКод",
		Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(4)));
	СведенияОНоменклатуре.Колонки.Добавить("ЕдиницаИзмеренияНаименование",
		Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(25)));
		
	Возврат СведенияОНоменклатуре;
	
КонецФункции

Процедура ЗаписатьДокумент(ДокументОбъект, РежимЗаписиПроведение = Истина)
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПроверитьЗаполнениеБезВыводаОшибок");
	
	Если РежимЗаписиПроведение
		И (ДокументОбъект.Проведен ИЛИ (ДокументОбъект.ЭтоНовый() И ДокументОбъект.ПроверитьЗаполнение()))Тогда
		ДокументОбъект.ОбменДанными.Загрузка = Ложь;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Иначе
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Формирование данных для электронных документов.

//См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПоТорг12ПокупательФНС
// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоТорг12ПокупательФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ДатаПолученияТовара = ДатаПолученияТовараПриемкиРабот(СтруктураЭД);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ДатаПолученияГруза", ДатаПолученияТовара);
	
КонецПроцедуры

//См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПоАкт501ЗаказчикФНС
// Подготавливает данные титула заказчика для электронного документа типа Акт выполненных работ формата 5.01.
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоАкт501ЗаказчикФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ДатаПриемкиРабот = ДатаПолученияТовараПриемкиРабот(СтруктураЭД);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"СведенияПоВыполнениюУслуг.ДатаЗаказа", ДатаПриемкиРабот);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Создание элементов справочников.

// См. ОбменСКонтрагентамиПереопределяемый.СоздатьОбъектВБД.
// Создает объект в ИБ по дереву параметров и помещает ссылку на него в "НовыйЭлемент".
//
// Параметры:
//  СтрокаОбъекта - Структура - параметры записываемого объекта.
//  ДеревоРазбора - ДеревоЗначений - результат разбора электронного документа.
//  НовыйЭлемент - СправочникСсылка - в этот параметр необходимо вернуть ссылку на созданный элемент справочника.
//
Процедура СоздатьОбъектВБД(СтрокаОбъекта, ДеревоРазбора, НовыйЭлемент) Экспорт
	
	Если СтрокаОбъекта.ОписаниеТипа = "СправочникСсылка.БанковскиеСчетаКонтрагентов" Тогда
		НовыйЭлемент = СоздатьПерезаполнитьБанковскийСчетКонтрагента(СтрокаОбъекта, ДеревоРазбора);
	КонецЕсли;
	
КонецПроцедуры


//См. ОбменСКонтрагентамиПереопределяемый.СсылкаНаОбъектПоИННКПП
// Поиска элемента справочника по реквизитам ИНН и КПП.
//
// Параметры:
//  ТипОбъекта - Строка - имя справочника в метаданных.
//  ИНН - Строка - ИНН.
//  КПП - Строка - КПП.
//  Организация - СправочникСсылка - ссылка на элемент справочника организации.
//  Ссылка - СправочникСсылка - Ссылка на найденный объект.
//
Процедура СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП, Ссылка, Организация = Неопределено) Экспорт
	
	Ссылка = Неопределено;
	
	ИмяМетаданных = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника(ТипОбъекта);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Выборка.Ссылка
	|ИЗ
	|	Справочник." + ИмяМетаданных + " КАК Выборка
	|ГДЕ
	|	Выборка.ИНН = &ИНН И
	|	Выборка.КПП = &КПП";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Ссылка = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры


//См. ОбменСКонтрагентамиПереопределяемый.СсылкаНаОбъектПоИННКПП
// Заполняет ИД контрагента.
//
// Параметры:
//  Контрагент - ОпределяемыйТип.Контрагент - ссылка на контрагента (Организация или Контрагент).
//  ВидКонтрагента - Строка - вид контрагента.
//  ИдКонтрагента - Строка - значение ИдКонтрагента.
//
Процедура ПолучитьИДКонтрагента(Контрагент, ВидКонтрагента, ИдКонтрагента) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат
	КонецЕсли;
	
	Если ВРег(ВидКонтрагента) = ВРег("Организация") Тогда
		ИдКонтрагента = Контрагент.ИНН + "_" + Контрагент.КПП;
	ИначеЕсли ВРег(ВидКонтрагента) = ВРег("Контрагент") Тогда
		ИдКонтрагента = Контрагент.ИНН + "_" + Контрагент.КПП;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Просмотр электронных документов.

// См. ОбменСКонтрагентамиПереопределяемый.ФорматСумм
// Заполняет текстовое представление суммы.
//
// Параметры:
//  СуммаКПрописи - Число - сумма, по которой надо получить представление.
//  КодВалюты - Число - код используемой валюты.
//  ЧН - Строка - параметр нулевого значения числа.
//  ЧРГ - Строка - разделитель групп целой части числа.
//  Результат - Строка - текстовое представление суммы, которое будет заполнено. 
Процедура ФорматСумм(СуммаКПрописи, Результат, КодВалюты = Неопределено, ЧН = "", ЧРГ = "") Экспорт
	
	Если Не ЗначениеЗаполнено(СуммаКПрописи) Тогда
		Возврат;
	КонецЕсли;
	
	Валюта = "руб.";
	
	Сумма = ?(СуммаКПрописи < 0, -СуммаКПрописи, СуммаКПрописи);
	ФорматнаяСтрока = "ЧЦ=15;ЧДЦ=2"
		+ ?(НЕ ЗначениеЗаполнено(ЧН), "", ";" + "ЧН=" + ЧН)
		+ ?(НЕ ЗначениеЗаполнено(ЧРГ),"", ";" + "ЧРГ=" + ЧРГ);
	Результат = СокрЛ(Формат(Сумма, ФорматнаяСтрока));
	
	Если ЗначениеЗаполнено(Валюта) Тогда
		Результат = Результат + " " + СокрП(Валюта);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЭтоФизЛицо
// Возврат признака физического лица.
//
// Параметры:
//  ДанныеКонтрагента - СправочникСсылка - ссылка на элемент справочника.
//  ПризнакФизЛица - Булево - Истина если физическое лицо.
//
Процедура ЭтоФизЛицо(ДанныеКонтрагента, ПризнакФизЛица) Экспорт
	
	Если ДанныеКонтрагента.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
		ИЛИ ДанныеКонтрагента.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ПризнакФизЛица = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Механизм однократной сделки.

// См. ОбменСКонтрагентамиПереопределяемый.СписокТиповДокументовПоВидуЭД
// Выполняет заполнение списка документов по виду электронного документа.
//
// Параметры:
//  ВидЭД           - Перечисления   - вид электронного документа;
//  СписокВозврата  - СписокЗначений - список ссылок на документы информационной базы.
//
Процедура СписокТиповДокументовПоВидуЭД(ВидЭД, СписокВозврата) Экспорт
	
	Если ВидЭД = Перечисления.ВидыЭД.ТОРГ12
		ИЛИ ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
		
		СписокВозврата.Добавить(Документы.ПоступлениеТоваров.ПустаяСсылка(),
			Метаданные.Документы.ПоступлениеТоваров.Представление());
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетФактура ИЛИ ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		СписокВозврата.Добавить(Документы.СчетФактураПолученный.ПустаяСсылка(),
			Метаданные.Документы.СчетФактураПолученный.Представление());
		
	ИначеЕсли ВРег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
		СписокВозврата.Добавить(Справочники.Контрагенты.ПустаяСсылка(),
			Метаданные.Справочники.Контрагенты.Представление());
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Механизм прямого обмена между организациями.

// См. ОбменСКонтрагентамиПереопределяемый.ИспользоватьОбменЭДМеждуОрганизациями
// Переопределяет разрешение использовать механизм прямого обмена между организациями.
//
// Параметры:
//  Отказ - Булево - Истина, если использование обмена между организациями запрещено;
//    Ложь - в противном случае;
//    Значение по умолчанию - Ложь;
//
// Пример:
//	Если <Выражение> Тогда
//		Отказ = Истина;
//	КонецЕсли;
//
Процедура ИспользоватьОбменЭДМеждуОрганизациями(Отказ) Экспорт
	
	Отказ = Истина;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.СписокОперацийВидаЭД
// Возвращает список имен документов доступных для создания при загрузке электронного документа.
//
// Параметры:
//  ВидЭД			 - Перечисление.ВидыЭД - вид электронного документа
//  СписокСпособовОбработки - Строка - список для добавления операций по отражению электронного документа.
//
Процедура СписокОперацийВидаЭД(ВидЭД,СписокСпособовОбработки) Экспорт 
	
	Если ВидЭД = Перечисления.ВидыЭД.АктИсполнитель Тогда
		
		СписокСпособовОбработки.Добавить("ПоступлениеТоваров", НСтр("ru = 'Поступление товаров'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ТОРГ12 Тогда
		
		СписокСпособовОбработки.Добавить("ПоступлениеТоваров", НСтр("ru = 'Поступление товаров'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
		
		СписокСпособовОбработки.Добавить("ПоступлениеТоваров", НСтр("ru = 'Поступление товаров'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель Тогда
		
		СписокСпособовОбработки.Добавить("ВозвратТоваровПоставщику", НСтр("ru = 'Возврат товаров поставщику'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
		
		СписокСпособовОбработки.Добавить("НоменклатураПоставщиков", НСтр("ru = 'Номенклатура поставщиков'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ПрайсЛист Тогда
		
		СписокСпособовОбработки.Добавить("НоменклатураПоставщиков", НСтр("ru = 'Номенклатура поставщиков'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара Тогда
		
		СписокСпособовОбработки.Добавить("ОтчетКомитентуОПродажах", НСтр("ru = 'Отчет комитенту о продажах'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара Тогда
		
		СписокСпособовОбработки.Добавить("ОтчетКомитентуОСписании", НСтр("ru = 'Отчет комитенту о списании'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ПередачаТоваровМеждуОрганизациями Тогда	
		
		СписокСпособовОбработки.Добавить("ПередачаТоваровМеждуОрганизациями", 
			НСтр("ru = 'Передача товаров между организациями'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетФактура Тогда
		
		СписокСпособовОбработки.Добавить("СчетФактураПолученный", НСтр("ru = 'Счет фактура полученный'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		СписокСпособовОбработки.Добавить("СчетФактураПолученный", 
			НСтр("ru = 'Счет фактура полученный (корректировка)'"), Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.СоответствиеИсходящихВидовЭДДокументамИБ
// Заполняет соответствие исходящих видов электронных документов и представлений документов информационной базы,
// на основании которых они формируются.
//
// Параметры:
//  СоответствиеВидовЭДДокументамИБ - Соответствие - перечень видов электронных документов.
//    - Соответствие - с свойствами:
//    * Ключ             - Перечисление.ВидыЭД - вид электронного документа.
//    * Значение         - Строка - представление документа информационной базы (хоз. операции).
//
Процедура СоответствиеИсходящихВидовЭДДокументамИБ(СоответствиеВидовЭДДокументамИБ) Экспорт 
	
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.СчетФактура, НСтр("ru = 'Счет-фактура выданный'"));
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.КорректировочныйСчетФактура, 
		НСтр("ru = 'Счет-фактура выданный (корректировка)'"));
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ЗаказТовара, НСтр("ru = 'Заказ поставщику'"));
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ТОРГ12Продавец, 
		НСтр("ru = 'Возврат товаров поставщику'"));
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара, 
		НСтр("ru = 'Отчет комитенту о продажах'"));
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара, 
		НСтр("ru = 'Отчет комитенту о списании'"));
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.КаталогТоваров,
		НСтр("ru = 'Справочник Настройка ЭДО'"));
	
КонецПроцедуры


Процедура ЗаполнитьДеревоДанныхУПД(СтруктураДанных, СтруктураЭД, ДеревоДанных)
	
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	
	СведенияОПродавце = Неопределено; 
	Если ЗначениеЗаполнено(ВыборкаШапки.Организация) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Организация, СведенияОПродавце);
		
		Если ТипЗнч(ВыборкаШапки.Ссылка) <> Тип("ДокументСсылка.СчетФактураВыданный")
			И ЗначениеЗаполнено(ВыборкаШапки.БанковскийСчетОрганизации) Тогда
			ЗаполнитьРеквизитыБанковскогоСчета(СведенияОПродавце, ВыборкаШапки.БанковскийСчетОрганизации,
				"БанковскиеСчетаОрганизаций");
		КонецЕсли;
		
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПродавце, "СведенияОПродавце");
		
		СоставительДокументаНаименование = СведенияОПродавце.ПолноеНаименование
			+ ?(ЗначениеЗаполнено(СведенияОПродавце.КПП),
				СтрШаблон(НСтр("ru = ', ИНН/КПП %1/%2'"), СведенияОПродавце.ИНН, СведенияОПродавце.КПП),
				СтрШаблон(НСтр("ru = ', ИНН %1'"), СведенияОПродавце.ИНН));
				
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
			"Объект.Организация");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СоставительДокументаНаименование", СоставительДокументаНаименование, ПараметрыОбработкиОшибок);
	Иначе
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
			"Объект.Организация");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОПродавце", СведенияОПродавце, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	СведенияОПокупателе = Неопределено;
	Если ЗначениеЗаполнено(ВыборкаШапки.Контрагент) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Контрагент, СведенияОПокупателе);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПокупателе, "СведенияОПокупателе");
	Иначе
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
			"Объект.Контрагент");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОПокупателе", СведенияОПокупателе, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	// Обработка ошибки с указанием расширенного текста сообщения вместо параметров обработки ошибки.
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод",
		ВыборкаШапки.ВалютаКод, ТекстОшибки);
	
	Если ЗначениеЗаполнено(ВыборкаШапки.Грузоотправитель) Тогда
		СведенияОГрузоотправителе = Неопределено; 
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Грузоотправитель, 
			СведенияОГрузоотправителе);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОГрузоотправителе, "СведенияОГрузоотправителе.Грузоотправитель", 
			"ФактическийАдрес");
	Иначе
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОГрузоотправителе.ОнЖе", Истина);
	КонецЕсли;
	
	СведенияОГрузополучателе = Неопределено; 
	Если ЗначениеЗаполнено(ВыборкаШапки.Грузополучатель) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Грузополучатель, 
			СведенияОГрузополучателе);
	ИначеЕсли СведенияОПокупателе <> Неопределено Тогда
		СведенияОГрузополучателе = СведенияОПокупателе;
	КонецЕсли;
	
	Если СведенияОГрузополучателе <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ВыборкаШапки.АдресДоставкиЗначенияПолей) Тогда
			ВидАдреса = "ПроизвольныйАдрес";
			СведенияОГрузополучателе.Вставить("ПроизвольныйАдресЗначенияПолей", ВыборкаШапки.АдресДоставкиЗначенияПолей);
		Иначе
			ВидАдреса = "ФактическийАдрес";
		КонецЕсли;
		
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОГрузополучателе, "СведенияОГрузополучателе", ВидАдреса);
		
	КонецЕсли;
	
	Если СтруктураЭД.Функция <> "СЧФ" Тогда 
	
		Если ЗначениеЗаполнено(ВыборкаШапки.Перевозчик) Тогда
			СведенияОПеревозчике = Неопределено; 
			ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Перевозчик, СведенияОПеревозчике);
			ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПеревозчике, "СведенияОПеревозчике", "ПочтовыйАдрес");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.ДатаОтгрузки) Тогда
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
				"Объект.ДатаОтгрузки");
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаОтгрузкиТоваров",
				ВыборкаШапки.ДатаОтгрузки, ПараметрыОбработкиОшибок);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.СведенияОТранспортировкеИГрузе) Тогда
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
				"Объект.СведенияОТранспортировкеИГрузе");
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОТранспортировке",
				ВыборкаШапки.СведенияОТранспортировкеИГрузе, ПараметрыОбработкиОшибок);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.НомерТранспортнойНакладной)
			ИЛИ ЗначениеЗаполнено(ВыборкаШапки.ДатаТранспортнойНакладной) Тогда
			
			ТранспортнаяНакладная = Новый ТаблицаЗначений;
			ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяНомер");
			ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяДата");
			
			НоваяСтрока = ТранспортнаяНакладная.Добавить();
			НоваяСтрока.ТранспортнаяНакладнаяНомер = ВыборкаШапки.НомерТранспортнойНакладной;
			НоваяСтрока.ТранспортнаяНакладнаяДата  = ВыборкаШапки.ДатаТранспортнойНакладной;
			
			// Дополним таблицу транспортных накладных параметрами обработки ошибок.
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока,
				"ТранспортнаяНакладнаяНомер", ВыборкаШапки.Ссылка, "Объект.НомерТранспортнойНакладной");
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока,
				"ТранспортнаяНакладнаяДата", ВыборкаШапки.Ссылка, "Объект.ДатаТранспортнойНакладной");
			
			ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТранспортнаяНакладная, "ТранспортнаяНакладная");
			
		КонецЕсли;
	
	КонецЕсли;
	
	СведенияОТоварах = Новый ТаблицаЗначений;
	СведенияОТоварах.Колонки.Добавить("НомерСтроки");
	СведенияОТоварах.Колонки.Добавить("НомерСтрокиИсходногоДокумента");
	СведенияОТоварах.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	СведенияОТоварах.Колонки.Добавить("ТоварНаименование");
	СведенияОТоварах.Колонки.Добавить("ЕдиницаИзмеренияКод");
	СведенияОТоварах.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	СведенияОТоварах.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	СведенияОТоварах.Колонки.Добавить("ЦенаЗаЕдиницуИзмерения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	СведенияОТоварах.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("НалоговаяСтавка");
	СведенияОТоварах.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("СведенияОТаможеннойДекларации", Новый ОписаниеТипов("ТаблицаЗначений"));
	СведенияОТоварах.Колонки.Добавить("СтранаПроисхожденияНаименование", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	СведенияОТоварах.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
	СведенияОТоварах.Колонки.Добавить("Номенклатура");
	СведенияОТоварах.Колонки.Добавить("ЕдиницаИзмерения");
	СведенияОТоварах.Колонки.Добавить("ТоварНаименованиеНавигационнаяСсылка");
	СведенияОТоварах.Колонки.Добавить("Сопоставление");
	СведенияОТоварах.Колонки.Добавить("ИдентификаторСтроки");
	СведенияОТоварах.Колонки.Добавить("СведенияОМаркировке");
	
	ЕстьТовары = Ложь;
	ЕстьУслуги = Ложь;
	ЕстьРаботы = Ложь;
	ЕстьПрава  = Ложь;
	
	НомерСтроки = 1;
	ВыборкаТоваров = СтруктураДанных.ВыборкаТоваров;
	
	ЗаполнениеКодаТовара = СтруктураЭД.ВариантыЗаполненияПолей.ТоварКод;
	
	//ГосИС.МОТП
	ТаблицаКодовМаркировки = Неопределено;
	СтруктураДанных.Свойство("Маркировка", ТаблицаКодовМаркировки);
	//Конец ГосИС.МОТП
	
	Пока ВыборкаТоваров.Следующий() Цикл
		
		НоваяСтрока = СведенияОТоварах.Добавить();
		НоваяСтрока.НомерСтроки = НомерСтроки;
		НоваяСтрока.НомерСтрокиИсходногоДокумента = ВыборкаТоваров.НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		
		НоваяСтрока.ТоварКод                     = ВыборкаТоваров.НоменклатураКод;
		НоваяСтрока.ТоварНаименование            = ПредставлениеНоменклатурыДляЭД(ВыборкаТоваров);
		НоваяСтрока.ЕдиницаИзмеренияКод          = ВыборкаТоваров.ЕдиницаИзмеренияКод;
		НоваяСтрока.ЕдиницаИзмеренияНаименование = ВыборкаТоваров.ЕдиницаИзмеренияНаименование;
		НоваяСтрока.Количество                   = ВыборкаТоваров.Количество;
		НоваяСтрока.ЦенаЗаЕдиницуИзмерения       = ?(ВыборкаТоваров.Количество = 0, 0,
			Окр(ВыборкаТоваров.СуммаБезНДС / ВыборкаТоваров.Количество, 2));
		НоваяСтрока.СтоимостьТоваровБезНалога    = ВыборкаТоваров.СуммаБезНДС;
		НоваяСтрока.НалоговаяСтавка          = ВыборкаТоваров.СтавкаНДС;
		НоваяСтрока.СуммаНалога              = ВыборкаТоваров.СуммаНДС;
		НоваяСтрока.СтоимостьТоваровСНалогом = ВыборкаТоваров.СуммаСНДС;
		
		НоваяСтрока.Номенклатура = ВыборкаТоваров.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения = ВыборкаТоваров.ЕдиницаИзмерения;
		НоваяСтрока.ТоварНаименованиеНавигационнаяСсылка = ПолучитьНавигационнуюСсылку(
			ВыборкаТоваров.Номенклатура, "Наименование");
			
		Если ВыборкаТоваров.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
			ИЛИ ВыборкаТоваров.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
			ЕстьТовары = Истина;
			НоваяСтрока.Признак = "1";
			
		ИначеЕсли ВыборкаТоваров.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
			ЕстьУслуги = Истина;
			НоваяСтрока.Признак = "3";
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.НомерГТД) Тогда
			СведенияОТаможеннойДекларации = Новый ТаблицаЗначений;
			СведенияОТаможеннойДекларации.Колонки.Добавить("СтранаПроисхожденияКод",
				Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(3)));
			СведенияОТаможеннойДекларации.Колонки.Добавить("ТаможеннаяДекларацияНомер",
				Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(29)));
			СтрокаТД = СведенияОТаможеннойДекларации.Добавить();
			СтрокаТД.ТаможеннаяДекларацияНомер = ВыборкаТоваров.НомерГТД;
			СтрокаТД.СтранаПроисхожденияКод    = ВыборкаТоваров.СтранаПроисхожденияКод;
			НоваяСтрока.СведенияОТаможеннойДекларации   = СведенияОТаможеннойДекларации;
			НоваяСтрока.СтранаПроисхожденияНаименование = ВыборкаТоваров.СтранаПроисхожденияНаименование;
		КонецЕсли;
		
		Сопоставление = Новый Структура;
		Сопоставление.Вставить("Наименование", ВыборкаТоваров.НоменклатураНаименование);
		Если ЗначениеЗаполнено(ВыборкаТоваров.Характеристика) Тогда
			Сопоставление.Вставить("Характеристика", ВыборкаТоваров.ХарактеристикаНаименование);
		КонецЕсли;
		Сопоставление.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмеренияНаименование);
		Сопоставление.Вставить("Артикул", ВыборкаТоваров.НоменклатураАртикул);
		
		//ГосИС.МОТП
		ЭлектронноеВзаимодействиеМОТП.ЗаполнитьСведенияОМаркировке(НоваяСтрока, ВыборкаТоваров, ТаблицаКодовМаркировки);
		//Конец ГосИС.МОТП
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.ИдентификаторНоменклатурыПоставщика) Тогда
			Сопоставление.Вставить("Идентификатор", ВыборкаТоваров.ИдентификаторНоменклатурыПоставщика);
		КонецЕсли;
		Сопоставление.Вставить("НоменклатураИБ", ВыборкаТоваров.НоменклатураИБ);
		Сопоставление.Вставить("ХарактеристикаИБ", ВыборкаТоваров.ХарактеристикаИБ);
		Сопоставление.Вставить("УпаковкаИБ", ВыборкаТоваров.УпаковкаИБ);
		НоваяСтрока.Сопоставление = Сопоставление;
		
		Если ЗаполнениеКодаТовара = "Артикул" Тогда
			НоваяСтрока.ТоварКод = ВыборкаТоваров.НоменклатураАртикул;
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить итоговые стоимостные показатели. Возможные причины:
	|	- не заполнена табличная часть ""Товары""
	|	- не заполнены колонки ""Сумма"", ""НДС"", ""Количество""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", СведенияОТоварах.Итог("СтоимостьТоваровБезНалога"), ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", СведенияОТоварах.Итог("СтоимостьТоваровСНалогом"), ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСуммаНалога", СведенияОТоварах.Итог("СуммаНалога"), ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоКоличество", СведенияОТоварах.Итог("Количество"), ТекстОшибки);
	
	// Обработка ошибки с открытием формы по навигационной ссылке.
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезНавигационнуюСсылку(СведенияОТоварах,
		"ТоварНаименование", "ТоварНаименованиеНавигационнаяСсылка");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезОткрытиеФормы(СведенияОТоварах,
		"ТоварНаименование", "Справочник.Номенклатура.ФормаОбъекта",, Новый Структура("Ключ", "Номенклатура"));
	
	// Обработка ошибки через механизм сообщений пользователю.
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ТоварКод", "Номенклатура", "Объект.Код");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ТоварНаименование", "Номенклатура", "Объект.НаименованиеПолное");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"Количество", ВыборкаТоваров.Ссылка, "Объект.Товары.Количество",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ЦенаЗаЕдиницуИзмерения", ВыборкаТоваров.Ссылка, "Объект.Товары.Цена",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"СтоимостьТоваровБезНалога", ВыборкаТоваров.Ссылка, "Объект.Товары.Сумма",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"СуммаНалога", ВыборкаТоваров.Ссылка, "Объект.Товары.СуммаНДС",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ЕдиницаИзмеренияНаименование", "ЕдиницаИзмерения", "Объект.Наименование");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ЕдиницаИзмеренияКод", "ЕдиницаИзмерения", "Объект.Код");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"Признак", "Номенклатура", "Объект.ВидНоменклатуры");
	
	// Обработка ошибки через упрощенный механизм
	ТекстОшибки = НСтр("ru = 'для формирования электронного документа необходимо указать налоговую ставку'");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"НалоговаяСтавка",,, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, СведенияОТоварах, "СведенияОТоварах");
	
	ТекстОшибки = НСтр("ru = 'Возникла ошибка при заполнении страны происхождения в сведениях по ГТД. Возможные причины:
	|	- не заполнена колонка ""Страна происхождения""
	|	- указанной страны нет в классификаторе стран мира'");
	ЭлектронноеВзаимодействие.ДобавитьВРеквизитОбработкуОшибки(ДеревоДанных,
		"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.СтранаПроисхожденияКод", ТекстОшибки);
	
	Если СтруктураЭД.Функция <> "СЧФ" Тогда 
		
		ТолькоУслуги = Истина;
		СоставСодержания = Новый Массив;
		СоставСодержания.Добавить(НСтр("ru = 'Товары переданы.'"));
		ТолькоУслуги = Ложь;
		СодержаниеОперации = СтрСоединить(СоставСодержания, " ");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", СодержаниеОперации);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоДанныхУПД_2019(СтруктураДанных, СтруктураЭД, ДеревоДанных)
	
	ВыборкаШапки = СтруктураДанных.ВыборкаШапки;
	
	СведенияОПродавце = Неопределено; 
	Если ЗначениеЗаполнено(ВыборкаШапки.Организация) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Организация, СведенияОПродавце);
		
		Если ТипЗнч(ВыборкаШапки.Ссылка) <> Тип("ДокументСсылка.СчетФактураВыданный")
			И ЗначениеЗаполнено(ВыборкаШапки.БанковскийСчетОрганизации) Тогда
			ЗаполнитьРеквизитыБанковскогоСчета(СведенияОПродавце, ВыборкаШапки.БанковскийСчетОрганизации,
				"БанковскиеСчетаОрганизаций");
		КонецЕсли;
		
		ТаблицаПродавцов = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
			ДеревоДанных, "СведенияОПродавце");
		ТаблицаПродавцов.Колонки.Добавить("СведенияОбУчастнике");
		ДанныеПродавца = ПолучитьДанныеУчастникаУПД(СведенияОПродавце);
		СтрокаПродавца = ТаблицаПродавцов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПродавца, ДанныеПродавца);
		ДобавитьОбработкуОшибокВТаблицуУчастниковУПД(ТаблицаПродавцов);
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаПродавцов, "СведенияОПродавце");
		
		СоставительДокументаНаименование = СведенияОПродавце.ПолноеНаименование
			+ ?(ЗначениеЗаполнено(СведенияОПродавце.КПП),
				СтрШаблон(НСтр("ru = ', ИНН/КПП %1/%2'"), СведенияОПродавце.ИНН, СведенияОПродавце.КПП),
				СтрШаблон(НСтр("ru = ', ИНН %1'"), СведенияОПродавце.ИНН));
		
		ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
			"Объект.Организация");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СоставительДокументаНаименование", СоставительДокументаНаименование, ПараметрыОбработкиОшибок);
	КонецЕсли;
	
	СведенияОПокупателе = Неопределено;
	Если ЗначениеЗаполнено(ВыборкаШапки.Контрагент) Тогда
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Контрагент, СведенияОПокупателе);
		
		ТаблицаПокупателей = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
			ДеревоДанных, "СведенияОПокупателе");
		ТаблицаПокупателей.Колонки.Добавить("СведенияОбУчастнике");
		ДанныеПокупателя = ПолучитьДанныеУчастникаУПД(СведенияОПокупателе);
		СтрокаПокупателя = ТаблицаПокупателей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПокупателя, ДанныеПокупателя);
		ДобавитьОбработкуОшибокВТаблицуУчастниковУПД(ТаблицаПокупателей);
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаПокупателей, "СведенияОПокупателе");
	КонецЕсли;
	
	// Обработка ошибки с указанием расширенного текста сообщения вместо параметров обработки ошибки.
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод",
		ВыборкаШапки.ВалютаКод, ТекстОшибки);
	
	ТаблицаГрузоотправителей = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		ДеревоДанных, "СведенияОГрузоотправителе");
	ТаблицаГрузоотправителей.Колонки.Добавить("СведенияОбУчастнике");
	
	Если ЗначениеЗаполнено(ВыборкаШапки.Грузоотправитель) Тогда
		СведенияОГрузоотправителе = Неопределено; 
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Грузоотправитель, 
			СведенияОГрузоотправителе);
		ДанныеГрузоотправителя = ПолучитьДанныеУчастникаУПД(СведенияОГрузоотправителе);
		СтрокаГрузоотправителя = ТаблицаГрузоотправителей.Добавить();
		СтрокаГрузоотправителя.Грузоотправитель = ДанныеГрузоотправителя;
		СтрокаГрузоотправителя.СведенияОбУчастнике = СведенияОГрузоотправителе;
	Иначе
		ТаблицаГрузоотправителей.Добавить().ОнЖе = Истина;
	КонецЕсли;
	
	ДобавитьОбработкуОшибокВТаблицуУчастниковУПД(ТаблицаГрузоотправителей, "Грузоотправитель");
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузоотправителей, "СведенияОГрузоотправителе");
	
	ТаблицаГрузополучателей = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		ДеревоДанных, "СведенияОГрузополучателе");
	ТаблицаГрузополучателей.Колонки.Добавить("СведенияОбУчастнике");
	
	Если ЗначениеЗаполнено(ВыборкаШапки.Грузополучатель) Тогда
		СведенияОГрузополучателе = Неопределено; 
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Грузополучатель, 
			СведенияОГрузополучателе);
		ДанныеГрузополучателя = ПолучитьДанныеУчастникаУПД(СведенияОГрузополучателе);
		ЗаполнитьЗначенияСвойств(ТаблицаГрузополучателей.Добавить(), ДанныеГрузополучателя);
	Иначе
		ДанныеГрузополучателя = ПолучитьДанныеУчастникаУПД(СведенияОПокупателе);
		ЗаполнитьЗначенияСвойств(ТаблицаГрузополучателей.Добавить(), ДанныеГрузополучателя);
	КонецЕсли;
	
	ДобавитьОбработкуОшибокВТаблицуУчастниковУПД(ТаблицаГрузополучателей);
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузополучателей, "СведенияОГрузополучателе");
	
	Если СтруктураЭД.Функция <> "СЧФ" Тогда
		
		Если ЗначениеЗаполнено(ВыборкаШапки.Перевозчик) Тогда
			СведенияОПеревозчике = Неопределено; 
			ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(ВыборкаШапки.Перевозчик, СведенияОПеревозчике);
			ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПеревозчике, "СведенияОПеревозчике", "ПочтовыйАдрес");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.ДатаОтгрузки) Тогда
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
				"Объект.ДатаОтгрузки");
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаОтгрузкиТоваров",
				ВыборкаШапки.ДатаОтгрузки, ПараметрыОбработкиОшибок);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.СведенияОТранспортировкеИГрузе) Тогда
			ПараметрыОбработкиОшибок = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ВыборкаШапки.Ссылка, 
				"Объект.СведенияОТранспортировкеИГрузе");
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОТранспортировке",
				ВыборкаШапки.СведенияОТранспортировкеИГрузе, ПараметрыОбработкиОшибок);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.НомерТранспортнойНакладной)
			ИЛИ ЗначениеЗаполнено(ВыборкаШапки.ДатаТранспортнойНакладной) Тогда
			
			ТранспортнаяНакладная = Новый ТаблицаЗначений;
			ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяНомер");
			ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяДата");
			
			НоваяСтрока = ТранспортнаяНакладная.Добавить();
			НоваяСтрока.ТранспортнаяНакладнаяНомер = ВыборкаШапки.НомерТранспортнойНакладной;
			НоваяСтрока.ТранспортнаяНакладнаяДата  = ВыборкаШапки.ДатаТранспортнойНакладной;
			
			// Дополним таблицу транспортных накладных параметрами обработки ошибок.
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока,
				"ТранспортнаяНакладнаяНомер", ВыборкаШапки.Ссылка, "Объект.НомерТранспортнойНакладной");
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока,
				"ТранспортнаяНакладнаяДата", ВыборкаШапки.Ссылка, "Объект.ДатаТранспортнойНакладной");
			
			ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТранспортнаяНакладная, "ТранспортнаяНакладная");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбстоятельстваФормированияСФ = "1";
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ОбстоятельстваФормированияСФ", 
		ОбстоятельстваФормированияСФ, ПараметрыОбработкиОшибок);
	
	
	СведенияОТоварах = Новый ТаблицаЗначений;
	СведенияОТоварах.Колонки.Добавить("НомерСтроки");
	СведенияОТоварах.Колонки.Добавить("НомерСтрокиИсходногоДокумента");
	СведенияОТоварах.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	СведенияОТоварах.Колонки.Добавить("ТоварНаименование");
	СведенияОТоварах.Колонки.Добавить("ЕдиницаИзмеренияКод");
	СведенияОТоварах.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	СведенияОТоварах.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	СведенияОТоварах.Колонки.Добавить("ЦенаЗаЕдиницуИзмерения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	СведенияОТоварах.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("НалоговаяСтавка");
	СведенияОТоварах.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	СведенияОТоварах.Колонки.Добавить("СведенияОТаможеннойДекларации", Новый ОписаниеТипов("ТаблицаЗначений"));
	СведенияОТоварах.Колонки.Добавить("СтранаПроисхожденияНаименование", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	СведенияОТоварах.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
	СведенияОТоварах.Колонки.Добавить("Номенклатура");
	СведенияОТоварах.Колонки.Добавить("ЕдиницаИзмерения");
	СведенияОТоварах.Колонки.Добавить("ТоварНаименованиеНавигационнаяСсылка");
	СведенияОТоварах.Колонки.Добавить("Сопоставление");
	СведенияОТоварах.Колонки.Добавить("ИдентификаторСтроки");
	СведенияОТоварах.Колонки.Добавить("СведенияОМаркировке");
	
	ЕстьТовары = Ложь;
	ЕстьУслуги = Ложь;
	ЕстьРаботы = Ложь;
	ЕстьПрава  = Ложь;
	
	НомерСтроки = 1;
	ВыборкаТоваров = СтруктураДанных.ВыборкаТоваров;
	
	ЗаполнениеКодаТовара = СтруктураЭД.ВариантыЗаполненияПолей.ТоварКод;
	
	//ГосИС.МОТП
	ТаблицаКодовМаркировки = Неопределено;
	СтруктураДанных.Свойство("Маркировка", ТаблицаКодовМаркировки);
	//Конец ГосИС.МОТП
	
	Пока ВыборкаТоваров.Следующий() Цикл
		
		НоваяСтрока = СведенияОТоварах.Добавить();
		НоваяСтрока.НомерСтроки = НомерСтроки;
		НоваяСтрока.НомерСтрокиИсходногоДокумента = ВыборкаТоваров.НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		
		НоваяСтрока.ТоварКод                     = ВыборкаТоваров.НоменклатураКод;
		НоваяСтрока.ТоварНаименование            = ПредставлениеНоменклатурыДляЭД(ВыборкаТоваров);
		НоваяСтрока.ЕдиницаИзмеренияКод          = ВыборкаТоваров.ЕдиницаИзмеренияКод;
		НоваяСтрока.ЕдиницаИзмеренияНаименование = ВыборкаТоваров.ЕдиницаИзмеренияНаименование;
		НоваяСтрока.Количество                   = ВыборкаТоваров.Количество;
		НоваяСтрока.ЦенаЗаЕдиницуИзмерения       = ?(ВыборкаТоваров.Количество = 0, 0,
			Окр(ВыборкаТоваров.СуммаБезНДС / ВыборкаТоваров.Количество, 2));
		НоваяСтрока.СтоимостьТоваровБезНалога    = ВыборкаТоваров.СуммаБезНДС;
		НоваяСтрока.НалоговаяСтавка          = ВыборкаТоваров.СтавкаНДС;
		НоваяСтрока.СуммаНалога              = ВыборкаТоваров.СуммаНДС;
		НоваяСтрока.СтоимостьТоваровСНалогом = ВыборкаТоваров.СуммаСНДС;
		
		НоваяСтрока.Номенклатура = ВыборкаТоваров.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения = ВыборкаТоваров.ЕдиницаИзмерения;
		НоваяСтрока.ТоварНаименованиеНавигационнаяСсылка = ПолучитьНавигационнуюСсылку(
			ВыборкаТоваров.Номенклатура, "Наименование");
		
		Если ВыборкаТоваров.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
			ИЛИ ВыборкаТоваров.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
			ЕстьТовары = Истина;
			НоваяСтрока.Признак = "1";
			
		ИначеЕсли ВыборкаТоваров.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
			ЕстьУслуги = Истина;
			НоваяСтрока.Признак = "3";
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.НомерГТД) Тогда
			СведенияОТаможеннойДекларации = Новый ТаблицаЗначений;
			СведенияОТаможеннойДекларации.Колонки.Добавить("СтранаПроисхожденияКод",
				Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(3)));
			СведенияОТаможеннойДекларации.Колонки.Добавить("ТаможеннаяДекларацияНомер",
				Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(29)));
			СтрокаТД = СведенияОТаможеннойДекларации.Добавить();
			СтрокаТД.ТаможеннаяДекларацияНомер = ВыборкаТоваров.НомерГТД;
			СтрокаТД.СтранаПроисхожденияКод    = ВыборкаТоваров.СтранаПроисхожденияКод;
			НоваяСтрока.СведенияОТаможеннойДекларации   = СведенияОТаможеннойДекларации;
			НоваяСтрока.СтранаПроисхожденияНаименование = ВыборкаТоваров.СтранаПроисхожденияНаименование;
		КонецЕсли;
		
		//ГосИС.МОТП
		ЭлектронноеВзаимодействиеМОТП.ЗаполнитьСведенияОМаркировке_2019(НоваяСтрока, ВыборкаТоваров, ТаблицаКодовМаркировки);
		//Конец ГосИС.МОТП
		
		Сопоставление = Новый Структура;
		Сопоставление.Вставить("Наименование", ВыборкаТоваров.НоменклатураНаименование);
		Если ЗначениеЗаполнено(ВыборкаТоваров.Характеристика) Тогда
			Сопоставление.Вставить("Характеристика", ВыборкаТоваров.ХарактеристикаНаименование);
		КонецЕсли;
		Сопоставление.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмеренияНаименование);
		Сопоставление.Вставить("Артикул", ВыборкаТоваров.НоменклатураАртикул);
		
		Если ЗначениеЗаполнено(ВыборкаТоваров.ИдентификаторНоменклатурыПоставщика) Тогда
			Сопоставление.Вставить("Идентификатор", ВыборкаТоваров.ИдентификаторНоменклатурыПоставщика);
		КонецЕсли;
		Сопоставление.Вставить("НоменклатураИБ", ВыборкаТоваров.НоменклатураИБ);
		Сопоставление.Вставить("ХарактеристикаИБ", ВыборкаТоваров.ХарактеристикаИБ);
		Сопоставление.Вставить("УпаковкаИБ", ВыборкаТоваров.УпаковкаИБ);
		НоваяСтрока.Сопоставление = Сопоставление;
		
		Если ЗаполнениеКодаТовара = "Артикул" Тогда
			НоваяСтрока.ТоварКод = ВыборкаТоваров.НоменклатураАртикул;
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить итоговые стоимостные показатели. Возможные причины:
	|	- не заполнена табличная часть ""Товары""
	|	- не заполнены колонки ""Сумма"", ""НДС"", ""Количество""'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", СведенияОТоварах.Итог("СтоимостьТоваровБезНалога"), ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", СведенияОТоварах.Итог("СтоимостьТоваровСНалогом"), ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСуммаНалога", СведенияОТоварах.Итог("СуммаНалога"), ТекстОшибки);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоКоличество", СведенияОТоварах.Итог("Количество"), ТекстОшибки);
	
	// Обработка ошибки с открытием формы по навигационной ссылке.
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезНавигационнуюСсылку(СведенияОТоварах,
		"ТоварНаименование", "ТоварНаименованиеНавигационнаяСсылка");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезОткрытиеФормы(СведенияОТоварах,
		"ТоварНаименование", "Справочник.Номенклатура.ФормаОбъекта",, Новый Структура("Ключ", "Номенклатура"));
	
	// Обработка ошибки через механизм сообщений пользователю.
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ТоварКод", "Номенклатура", "Объект.Код");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ТоварНаименование", "Номенклатура", "Объект.НаименованиеПолное");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"Количество", ВыборкаТоваров.Ссылка, "Объект.Товары.Количество",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ЦенаЗаЕдиницуИзмерения", ВыборкаТоваров.Ссылка, "Объект.Товары.Цена",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"СтоимостьТоваровБезНалога", ВыборкаТоваров.Ссылка, "Объект.Товары.Сумма",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"СуммаНалога", ВыборкаТоваров.Ссылка, "Объект.Товары.СуммаНДС",, "НомерСтрокиИсходногоДокумента");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ЕдиницаИзмеренияНаименование", "ЕдиницаИзмерения", "Объект.Наименование");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"ЕдиницаИзмеренияКод", "ЕдиницаИзмерения", "Объект.Код");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"Признак", "Номенклатура", "Объект.ВидНоменклатуры");
	
	// Обработка ошибки через упрощенный механизм
	ТекстОшибки = НСтр("ru = 'для формирования электронного документа необходимо указать налоговую ставку'");
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СведенияОТоварах,
		"НалоговаяСтавка",,, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, СведенияОТоварах, "СведенияОТоварах");
	
	ТекстОшибки = НСтр("ru = 'Возникла ошибка при заполнении страны происхождения в сведениях по ГТД. Возможные причины:
	|	- не заполнена колонка ""Страна происхождения""
	|	- указанной страны нет в классификаторе стран мира'");
	ЭлектронноеВзаимодействие.ДобавитьВРеквизитОбработкуОшибки(ДеревоДанных,
		"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.СтранаПроисхожденияКод", ТекстОшибки);
	
	Если СтруктураЭД.Функция <> "СЧФ" Тогда 
		
		ТолькоУслуги = Истина;
		СоставСодержания = Новый Массив;
		СоставСодержания.Добавить(НСтр("ru = 'Товары переданы.'"));
		ТолькоУслуги = Ложь;
		СодержаниеОперации = СтрСоединить(СоставСодержания, " ");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", СодержаниеОперации);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПредставлениеНоменклатурыДляЭД(СвойстваНоменклатуры)
	
	Если ЗначениеЗаполнено(СвойстваНоменклатуры.ХарактеристикаНаименование) Тогда
		ПредставлениеНоменклатуры = СтрШаблон(НСтр("ru = '%1 (%2)'"),
			СвойстваНоменклатуры.НоменклатураНаименование, СвойстваНоменклатуры.ХарактеристикаНаименование);
	Иначе
		ПредставлениеНоменклатуры = СвойстваНоменклатуры.НоменклатураНаименование;
	КонецЕсли;
	
	Возврат ПредставлениеНоменклатуры;
	
КонецФункции

Функция ПолучитьДанныеУчастникаУПД(Знач СведенияОбУчастнике)
	
	КлючДанных = Неопределено;
	СведенияОбУчастнике.Свойство("Ссылка", КлючДанных);
	
	Данные = Новый Структура("СведенияОбУчастнике", СведенияОбУчастнике);
	
	Данные.Вставить("ТипУчастника", Новый Структура);
	
	Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		
		Данные.ТипУчастника.Вставить("ЮЛ", Новый Структура);
		Данные.ТипУчастника.ЮЛ.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
		Данные.ТипУчастника.ЮЛ.Вставить("ИНН", СведенияОбУчастнике.ИНН);
		Данные.ТипУчастника.ЮЛ.Вставить("КПП", СведенияОбУчастнике.КПП);
		
	ИначеЕсли СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		
		Данные.ТипУчастника.Вставить("ИЛ", Новый Структура);
		Данные.ТипУчастника.ИЛ.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
		
	Иначе
		
		Данные.ТипУчастника.Вставить("ИП", Новый Структура);
		Данные.ТипУчастника.ИП.Вставить("ИНН", СведенияОбУчастнике.ИНН);
		Данные.ТипУчастника.ИП.Вставить("Фамилия", СведенияОбУчастнике.Фамилия);
		Данные.ТипУчастника.ИП.Вставить("Имя", СведенияОбУчастнике.Имя);
		Данные.ТипУчастника.ИП.Вставить("Отчество", СведенияОбУчастнике.Отчество);
		
		Если ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
			
			Свидетельство = СтрШаблон(НСтр("ru = 'Свидетельство №%1 от %2'"),
				СведенияОбУчастнике.СвидетельствоСерияНомер,
				Формат(СведенияОбУчастнике.СвидетельствоДатаВыдачи, "ДЛФ=D"));
				
			Данные.ТипУчастника.ИП.Вставить("СвидетельствоОГосРегистрации", Свидетельство);
			Данные.ТипУчастника.ИП.Вставить("Отчество", СведенияОбУчастнике.Отчество);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
	Если ТипЗнч(КлючДанных) = Тип("СправочникСсылка.Организации") Тогда
		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	КонецЕсли;
	
	ВидКонтактнойИнформации = ?(ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Контрагенты"),
		Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	
	Данные.Вставить("Адрес", Новый Структура);
	Данные.Адрес.Вставить("АвтоматическиЗаполняемый", Новый Структура);
	Данные.Адрес.АвтоматическиЗаполняемый.Вставить("ОбъектКонтактнойИнформации", КлючДанных);
	Данные.Адрес.АвтоматическиЗаполняемый.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформации);
	
	Данные.Вставить("КонтактныеСведения", Новый Структура);
	
	Телефон = "";
	Если СведенияОбУчастнике.Свойство("Телефоны", Телефон) И ЗначениеЗаполнено(Телефон) Тогда
		Данные.КонтактныеСведения.Вставить("Телефон", Телефон);
	КонецЕсли;
	
	ЭлектроннаяПочта = "";
	Если СведенияОбУчастнике.Свойство("ЭлектроннаяПочта", ЭлектроннаяПочта) И ЗначениеЗаполнено(ЭлектроннаяПочта) Тогда
		Данные.КонтактныеСведения.Вставить("ЭлектроннаяПочта", ЭлектроннаяПочта);
	КонецЕсли;
	
	Данные.Вставить("БанковскиеРеквизиты", Новый Структура);
	
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
		
		Данные.БанковскиеРеквизиты.Вставить("НомерСчета", НомерСчета);
		
		Банк = ""; БИК = ""; КоррСчет = "";
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			
			Данные.БанковскиеРеквизиты.Вставить("НаименованиеБанка", Банк);
			
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			
			Данные.БанковскиеРеквизиты.Вставить("БИКБанка", БИК);
			
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
			
			Данные.БанковскиеРеквизиты.Вставить("КорреспондентскийСчетБанка", КоррСчет);
			
		КонецЕсли;
	КонецЕсли;
	
	КодПоОКПО = "";
	Если СведенияОбУчастнике.Свойство("КодПоОКПО", КодПоОКПО) И ЗначениеЗаполнено(КодПоОКПО) Тогда
		
		Данные.Вставить("КодОКПО", КодПоОКПО);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Процедура ДобавитьОбработкуОшибокВТаблицуУчастниковУПД(ТаблицаУчастников, ПутьКУчастнику = "")
	
	ПутьКПолю = ?(ЗначениеЗаполнено(ПутьКУчастнику), ПутьКУчастнику + ".", "");
	
	Для каждого СтрокаТаблицы Из ТаблицаУчастников Цикл
		
		Сведения = СтрокаТаблицы.СведенияОбУчастнике;
		Если Не ЗначениеЗаполнено(Сведения) Тогда
			Продолжить;
		КонецЕсли;
		
		Участник = ?(ЗначениеЗаполнено(ПутьКУчастнику), СтрокаТаблицы[ПутьКУчастнику], СтрокаТаблицы);
		
		Если Участник.ТипУчастника.Свойство("ЮЛ") Тогда
			
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ЮЛ.НаименованиеОрганизации", Сведения.Ссылка, "Объект.НаименованиеПолное");
				
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ЮЛ.ИНН", Сведения.Ссылка, "Объект.ИНН");
				
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ЮЛ.ИНН", Сведения.Ссылка, "Объект.КПП");
				
		ИначеЕсли Участник.ТипУчастника.Свойство("ИЛ") Тогда
			
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ИЛ.НаименованиеОрганизации", Сведения.Ссылка, "Объект.НаименованиеПолное");
				
		ИначеЕсли Участник.ТипУчастника.Свойство("ИП") Тогда
			
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ИП.ИНН", Сведения.Ссылка, "Объект.ИНН");
				
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ИП.Фамилия", Сведения.Ссылка, "Объект.НаименованиеПолное");
				
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ИП.Имя", Сведения.Ссылка, "Объект.НаименованиеПолное");
				
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "ТипУчастника.ИП.Отчество", Сведения.Ссылка, "Объект.НаименованиеПолное");
				
			Если ТипЗнч(Сведения.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
				
				ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
					ПутьКПолю + "ТипУчастника.ИП.СвидетельствоОГосРегистрации", Сведения.Ссылка, "Объект.СвидетельствоСерияНомер");
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПостфиксПоляАдрес = ?(ТипЗнч(Сведения.Ссылка) = Тип("СправочникСсылка.Контрагенты"), "Контрагента", "Организации");
		
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
			ПутьКПолю + "Адрес.АвтоматическиЗаполняемый", Сведения.Ссылка, "КонтактнаяИнформацияПолеЮрАдрес" + ПостфиксПоляАдрес);
			
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
			ПутьКПолю + "КонтактныеСведения.Телефон", Сведения.Ссылка);
			
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
			ПутьКПолю + "КонтактныеСведения.ЭлектроннаяПочта", Сведения.Ссылка);
			
		НомерСчета = "";
		Если Сведения.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
			
			Если Сведения.Свойство("БанковскийСчетСсылка") Тогда
				ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
					ПутьКПолю + "БанковскиеРеквизиты.НомерСчета", Сведения.БанковскийСчетСсылка, "Объект.НомерСчета");
			КонецЕсли;
				
			Банк = ""; БИК = ""; КоррСчет = "";
			Если Сведения.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
				Если Сведения.Свойство("БанкСсылка") Тогда
					ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
						ПутьКПолю + "БанковскиеРеквизиты.НаименованиеБанка", Сведения.БанкСсылка, "Объект.Наименование");
				КонецЕсли;
			КонецЕсли;
			Если Сведения.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
				Если Сведения.Свойство("БанкСсылка") Тогда
					ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
						ПутьКПолю + "БанковскиеРеквизиты.БИКБанка", Сведения.БанкСсылка, "Объект.Код");
				КонецЕсли;
			КонецЕсли;
			Если Сведения.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
				Если Сведения.Свойство("БанкСсылка") Тогда
					ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
						ПутьКПолю + "БанковскиеРеквизиты.КорреспондентскийСчетБанка", Сведения.БанкСсылка, "Объект.КоррСчет");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		КодПоОКПО = "";
		Если Сведения.Свойство("КодПоОКПО", КодПоОКПО) И ЗначениеЗаполнено(КодПоОКПО) Тогда
			ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокаТаблицы,
				ПутьКПолю + "КодОКПО", Сведения.Ссылка, "Объект.КодПоОКПО");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


// Сохраняет в регистр сведений НоменклатураПоставщиков данные о номенклатуре контрагента
// и устанавливает их соответствие номенклатуре информационной базы.
//
// Параметры:
//  НоменклатураКонтрагента - Структура - номенклатура контрагента. См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента.
//  НоменклатураИБ - Структура - номенклатура ИБ. См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//                               Если Неопределено, то данные о номенклатуре контрагента сохраняются
//                               без сопоставления с номенклатурой информационной базы.
//
Процедура ЗаписатьСоответствияВНоменклатуруПоставщиков(Знач НоменклатураКонтрагента, Знач НоменклатураИБ = Неопределено, Замещать = Истина) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НоменклатураИБ.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НоменклатураИБ.Упаковка) = Тип("СправочникСсылка.УпаковкиНоменклатуры") Тогда
		Упаковка = НоменклатураИБ.Упаковка;
	Иначе
		Упаковка = Справочники.УпаковкиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	НаборНоменклатураПоставщиков = РегистрыСведений.НоменклатураПоставщиков.СоздатьНаборЗаписей();
	НаборНоменклатураПоставщиков.Отбор.Поставщик.Установить(НоменклатураКонтрагента.Владелец);
	НаборНоменклатураПоставщиков.Отбор.Идентификатор.Установить(НоменклатураКонтрагента.Идентификатор);
	НаборНоменклатураПоставщиков.Отбор.Номенклатура.Установить(НоменклатураИБ.Номенклатура);
	НаборНоменклатураПоставщиков.Отбор.Характеристика.Установить(НоменклатураИБ.Характеристика);
	НаборНоменклатураПоставщиков.Отбор.Упаковка.Установить(Упаковка);
	НаборНоменклатураПоставщиков.Прочитать();
	
	Если ЗначениеЗаполнено(НаборНоменклатураПоставщиков) И Не Замещать Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьНоменклатураПоставщиков = Неопределено;
	Если ЗначениеЗаполнено(НаборНоменклатураПоставщиков) Тогда
		ЗаписьНоменклатураПоставщиков = НаборНоменклатураПоставщиков[0];
	Иначе
		ЗаписьНоменклатураПоставщиков = НаборНоменклатураПоставщиков.Добавить();
	КонецЕсли;
	
	ЗаписьНоменклатураПоставщиков.Поставщик = НоменклатураКонтрагента.Владелец;
	ЗаписьНоменклатураПоставщиков.Номенклатура = НоменклатураИБ.Номенклатура;
	ЗаписьНоменклатураПоставщиков.Характеристика = НоменклатураИБ.Характеристика;
	ЗаписьНоменклатураПоставщиков.Упаковка = Упаковка;
	ЗаписьНоменклатураПоставщиков.Идентификатор = НоменклатураКонтрагента.Идентификатор;
	ЗаписьНоменклатураПоставщиков.Цена = ЗаписьНоменклатураПоставщиков.Цена;
	ЗаписьНоменклатураПоставщиков.ДатаПоследнегоПоступления = ЗаписьНоменклатураПоставщиков.ДатаПоследнегоПоступления;
	ЗаписьНоменклатураПоставщиков.Артикул = НоменклатураКонтрагента.Артикул;
	
	НаборНоменклатураПоставщиков.Записать();
	
КонецПроцедуры

Функция ТаблицаТоваров()
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаСсылка");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("Количество");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКодПоОКЕИ");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКоэффициент");
	ТаблицаТоваров.Колонки.Добавить("НДСУчтеноВСумме");
	ТаблицаТоваров.Колонки.Добавить("Цена");
	ТаблицаТоваров.Колонки.Добавить("ЦенаПродажи");
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаПродажи");
	ТаблицаТоваров.Колонки.Добавить("Сумма");
	ТаблицаТоваров.Колонки.Добавить("СуммаВознаграждения");
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС");
	ТаблицаТоваров.Колонки.Добавить("ДополнительныеРеквизиты");
	ТаблицаТоваров.Колонки.Добавить("ПокупательСсылка");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	ТаблицаТоваров.Колонки.Добавить("ИдентификаторНоменклатурыПоставщика");
	
	Возврат ТаблицаТоваров;
	
КонецФункции

Функция СтруктураАдресаФНС()
	
	СтруктураАдреса = Новый Структура;
	
	СтруктураАдреса.Вставить("Индекс");
	СтруктураАдреса.Вставить("КодРегион");
	СтруктураАдреса.Вставить("Район");
	СтруктураАдреса.Вставить("Город");
	СтруктураАдреса.Вставить("НаселПункт");
	СтруктураАдреса.Вставить("Улица");
	СтруктураАдреса.Вставить("Дом");
	СтруктураАдреса.Вставить("Корпус");
	СтруктураАдреса.Вставить("Кварт");
	СтруктураАдреса.Вставить("ОбязательныеПоля", "КодРегион");
	
	ТипыАдресов = Новый СписокЗначений;
	ТипыАдресов.Добавить(СтруктураАдреса, "Структурированный", Истина);
	ТипыАдресов.Добавить(Новый Структура("КодСтраны, АдресСтрокой"), "Произвольный", Ложь);
	ТипыАдресов.Добавить(Новый Структура("КодСтраны, АдресСтрокой"), "Иностранный", Ложь);
	
	Возврат ТипыАдресов;
	
КонецФункции

Функция ВыполнятьПроверкуСопоставленияНоменклатуры()
	
	Возврат Ложь;
	
КонецФункции

Функция ДатаПолученияТовараПриемкиРабот(СтруктураЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТ_Основание
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий.ДокументыОснования КАК ЭлектронныйДокументВходящийДокументыОснования
	|ГДЕ
	|	ЭлектронныйДокументВходящийДокументыОснования.Ссылка = &ВладелецЭД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваров.Дата КАК Дата
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|		ПО ПоступлениеТоваров.Ссылка = ВТ_Основание.ДокументОснование";
	Запрос.УстановитьПараметр("ВладелецЭД", СтруктураЭД.ВладелецЭД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Дата) Тогда
		ДатаПолучения = Выборка.Дата;
	Иначе
		ДатаПолучения = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат ДатаПолучения;
	
Конецфункции

Функция ЭтоКорректировочныйДокумент(СсылкаНаОбъект) Экспорт
	
	Результат = Ложь;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный")
		ИЛИ ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Если СсылкаНаОбъект.Пустая() Тогда
			Результат = Ложь;
		Иначе
			Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "Исправление");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗавершениеЗагрузкиИЗаписьДокумента(СсылкаНаВладельца, ДокументОбъект, Записывать)
	
	Если Записывать Тогда
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			СтрокаОшибки = ОписаниеОшибки();
			ТекстСообщения = НСтр("ru = 'Не удалось записать документ %1 по причине:'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + Символы.ПС + СтрокаОшибки);
		КонецПопытки;
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Иначе // если функция вызвана из формы "Обработки.ОбменСКонтрагентами.ФормаЗагрузкиПросмотраЭД"
		СсылкаНаВладельца = ДокументОбъект;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


