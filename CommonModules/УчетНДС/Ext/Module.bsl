
#Область ПрограммныйИнтерфейс

// Заполняет список выбора для колонки СтавкаНДС.
//
// Параметры:
//  Элементы 	- ЭлементыФормы - Все элементы переданной формы.
//  Дата 		- Дата - дата на которую необходимо получить актуальную ставку НДС.
//
Процедура ЗаполнитьСписокВыбораСтавокНДС(Элементы, Дата) Экспорт 
	
	Ставка20_18 = СтавкаНДСПоУмолчанию(Дата);
	
	Элементы.ТоварыСтавкаНДС.СписокВыбора.Очистить();
	
	МассивСтавокНДС = Новый Массив;
	МассивСтавокНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
	МассивСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС10);
	МассивСтавокНДС.Добавить(Ставка20_18);
	
	Элементы.ТоварыСтавкаНДС.СписокВыбора.ЗагрузитьЗначения(МассивСтавокНДС);
	
КонецПроцедуры

// Заменяет переданную ставку НДС на актуальную на указанную дату.
//
// Параметры:
//  СтавкаНДС 	- ПеречислениеСсылка.СтавкиНДС - значение ставки НДС, которое необходимо скорректировать
//  Дата 		- Дата - дата на которую необходимо получить актуальную ставку НДС.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС - значение ставки НДС.
//
Функция СкорректироватьСтавкуНДС(СтавкаНДС, Дата) Экспорт
	
	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118
		Или СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
		
		СтавкаНДС = СтавкаНДСПоУмолчанию(Дата);
			
	КонецЕсли;
	
	Возврат СтавкаНДС;
	
КонецФункции

// Заменяет ставки НДС в табличной части документа на актуальную на дату документа,
// а также пересчитывает соответствующие суммы НДС.
//
// Параметры:
//  Документ - ДокуменОбъект - объект документа, в табличной части которого необходимо заменить ставки НДС
//  ТабличнаяЧасть - ДокументТабличнаяЧасть - табличная часть документа
//  СтруктураПересчетаСуммы - Структура - см. ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ().
//
Процедура СкорректироватьНДСВТЧДокумента(Документ, ТабличнаяЧасть) Экспорт
	
	Если Не ЗначениеЗаполнено(ТабличнаяЧасть) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаАктуальности = ?(ЗначениеЗаполнено(Документ.Дата), Документ.Дата, ТекущаяДатаСеанса());
	
	Для каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		
		СтавкаНДС = СкорректироватьСтавкуНДС(СтрокаТЧ.СтавкаНДС, ДатаАктуальности);
		СтрокаТЧ.СуммаНДС = ОбработкаТабличнойЧастиТоварыСервер.РассчитатьСуммуНДС(СтрокаТЧ.Сумма, СтавкаНДС, Документ.ЦенаВключаетНДС);
			
	КонецЦикла;
	
КонецПроцедуры

// Возвращает законодательно установленную дату переходного периода при учете НДС
Функция ДатаПереходногоПериода() Экспорт
	
	Возврат Дата("20190101");
	
КонецФункции

// Получает признак Применять НДС из регистра ПрименениеСистемНалогообложения по указанным параметрам
//
// Параметры:
//  Дата			 - 	 Дата, на которую необходимо определить систему налогообложения. Необязательный.  
//  Организация		 - 	 Организация, для которой необходимо определить систему налогообложения. Обязательный. 
//  Магазин			 - 	 Магазин, для которого необходимо определить систему налогообложения. Необязательный. 
//						 Если заполнен склад, но не заполнен магазин - берётся из склада. 
//  Склад			 - 	 Склад, для которого необходимо определить систему налогообложения. Необязательный.
//  ТоварнаяГруппа	 - 	 Товарная группа номенклатуры, для которой необходимо определить систему налогообложения. Необязательный.  
// 
// Возвращаемое значение:
//   Булево 
//
Функция ПрименяетсяНДС(Дата = Неопределено, 
					   Организация, 
					   Магазин 		  = Неопределено, 
					   Склад 		  = Неопределено, 
					   ТоварнаяГруппа = Неопределено) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) И НЕ ЗначениеЗаполнено(Магазин) Тогда
		Магазин = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "Магазин");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ПрименениеСистемНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
		|	ПрименениеСистемНалогообложенияСрезПоследних.ОсвобожденОтНДС КАК ОсвобожденОтНДС,
		|	ВЫБОР
		|		КОГДА ПрименениеСистемНалогообложенияСрезПоследних.Магазин <> ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа <> ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|			ТОГДА 1
		|		КОГДА ПрименениеСистемНалогообложенияСрезПоследних.Магазин <> ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа <> ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|			ТОГДА 2
		|		КОГДА ПрименениеСистемНалогообложенияСрезПоследних.Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа <> ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|			ТОГДА 3
		|		КОГДА ПрименениеСистемНалогообложенияСрезПоследних.Магазин <> ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|			ТОГДА 4
		|		КОГДА ПрименениеСистемНалогообложенияСрезПоследних.Магазин <> ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|			ТОГДА 5
		|		КОГДА ПрименениеСистемНалогообложенияСрезПоследних.Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|			ТОГДА 6
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних(
		|			&Дата,
		|			Организация = &Организация
		|				И &Условие) КАК ПрименениеСистемНалогообложенияСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
			
	Если ЗначениеЗаполнено(Магазин) И ЗначениеЗаполнено(Склад) И ЗначениеЗаполнено(ТоварнаяГруппа) Тогда
		
		Условие = " (Магазин = &Магазин
	               	|			ИЛИ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка))
	               	| И (Склад = &Склад
	               	|			ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               	| И (ТоварнаяГруппа = &ТоварнаяГруппа
	               	|			ИЛИ ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка))";									 
		
	ИначеЕсли ЗначениеЗаполнено(Магазин) И НЕ ЗначениеЗаполнено(Склад) И ЗначениеЗаполнено(ТоварнаяГруппа) Тогда
				
		Условие = " (Магазин = &Магазин
	               	|			ИЛИ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка))
	               	| И (Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               	| И (ТоварнаяГруппа = &ТоварнаяГруппа
	               	|			ИЛИ ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка))";									 
			
	ИначеЕсли НЕ ЗначениеЗаполнено(Магазин) И НЕ ЗначениеЗаполнено(Склад) И ЗначениеЗаполнено(ТоварнаяГруппа) Тогда
				
		Условие = " (Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка))
	               	| И (Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               	| И (ТоварнаяГруппа = &ТоварнаяГруппа
	               	|			ИЛИ ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка))";									 
		
	ИначеЕсли ЗначениеЗаполнено(Магазин) И ЗначениеЗаполнено(Склад) И НЕ ЗначениеЗаполнено(ТоварнаяГруппа) Тогда
				
		Условие = " (Магазин = &Магазин
	               	|			ИЛИ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка))
	               	| И (Склад = &Склад
	               	|			ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               	| И (ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка))";									 
					
	ИначеЕсли ЗначениеЗаполнено(Магазин) И НЕ ЗначениеЗаполнено(Склад) И НЕ ЗначениеЗаполнено(ТоварнаяГруппа) Тогда 
		
		Условие = " (Магазин = &Магазин
	               	|			ИЛИ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка))
	               	| И (Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               	| И (ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка))";									 		
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Магазин) И НЕ ЗначениеЗаполнено(Склад) И НЕ ЗначениеЗаполнено(ТоварнаяГруппа) Тогда
		
		Условие = " (Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка))
	               	| И (Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               	| И (ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка))";
		
	Иначе
		
		ТекстОшибки = НСтр("ru = 'Не удалось определить ставку НДС. Настройте применение систем налогообложения для организации ""%1""'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, 
																			  Организация);

		ВызватьИсключение ТекстОшибки;
			
	КонецЕсли;
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", Условие); 
	
	Если ЗначениеЗаполнено(Магазин) Тогда
		Запрос.УстановитьПараметр("Магазин", Магазин);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Запрос.УстановитьПараметр("Склад", Склад);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТоварнаяГруппа) Тогда
		Запрос.УстановитьПараметр("ТоварнаяГруппа", ТоварнаяГруппа);
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("Дата", 			Дата);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если Выборка.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН И Дата < УчетНДС.ДатаПереходногоПериода() Тогда
			Возврат Ложь;
		Иначе
			Возврат НЕ Выборка.ОсвобожденОтНДС;
		КонецЕсли;
		
	Иначе
		
		ТекстОшибки = НСтр("ru = 'Не удалось определить ставку НДС. Настройте применение систем налогообложения для организации ""%1"" на дату %2.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, 
																			  Организация,
																			  Формат(Дата,"ДЛФ=D"));

		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;	
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает значение ставки НДС по умолчанию.
//
// Параметры:
//  Дата - Дата - дата на которую необходимо получить ставку НДС по умолчанию,
//               если дата пустая, то будет получена ставка НДС на текущую дату
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС - значение ставки НДС.
//
Функция СтавкаНДСПоУмолчанию(Дата = Неопределено) Экспорт
	
	ДатаПолучения = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	
	Если ДатаПолучения >= ДатаПереходногоПериода() Тогда
		Возврат Перечисления.СтавкиНДС.НДС20;
	Иначе
		Возврат Перечисления.СтавкиНДС.НДС18;
	КонецЕсли;
	
КонецФункции


#КонецОбласти