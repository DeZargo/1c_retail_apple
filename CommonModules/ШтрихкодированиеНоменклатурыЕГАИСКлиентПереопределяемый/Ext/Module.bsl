
#Область СлужебныйПрограммныйИнтерфейс

// В процедуре нужно реализовать очистку кэшированных штрихкодов для исключения повторной обработки.
//
// Параметры:
//  ДанныеШтрихкодов - Массив - полученные штрихкоды,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ.
//
Процедура ОчиститьКэшированныеШтрихкоды(ДанныеШтрихкодов, КэшированныеЗначения) Экспорт
	
	КэшированныеЗначения.Штрихкоды.Очистить();
	
КонецПроцедуры

// Вызывается после обработки штрихкодов. В процедуре нужно проанализировать полученные штрихкоды на предмет сканирования акцизной марки, а также
// обработать штрихкоды, не привязанные к номенклатуре.
//
// Параметры:
//  Форма - УправляемаяФорма - форма документа, в которой были обработаны штрихкоды,
//  ОбработанныеДанные - Структура - подготовленные ранее данные штрихкодов,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ.
//
Процедура ПослеОбработкиШтрихкодов(Форма, ОбработанныеДанные, КэшированныеЗначения) Экспорт
	
	Перем ОбработанныеШтрихкоды;
	
	Если НЕ ОбработанныеДанные.Свойство("ОбработанныеШтрихкоды", ОбработанныеШтрихкоды) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураПараметровКлиента Из ОбработанныеШтрихкоды Цикл
		СтруктураПараметровКлиента.Вставить("ОбработкаДанныхЕГАИС", Истина);
		СтруктураПараметровКлиента.Вставить("КэшированныеЗначения", КэшированныеЗначения);
		
		ОбработатьДанныеПоКодуКлиент(Форма, СтруктураПараметровКлиента);
	КонецЦикла;
	
КонецПроцедуры

// В функции нужно реализовать подготовку данных для дальнейшей обработки штрихкодов, полученных из ТСД.
//
// Параметры:
//   Форма - УправляемаяФорма - форма документа, в которой происходит обработка,
//   ТаблицаТоваров - Массив - полученные данные из ТСД,
//   КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ,
//   ПараметрыЗаполнения - Структура - параметры заполнения (см. ИнтеграцияЕГАИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти()).
//   СтруктураДействий - Структура - подготовленные данные.
//
Процедура ПодготовитьДанныеДляОбработкиТаблицыТоваровПолученнойИзТСД(
	Форма, ТаблицаТоваров, КэшированныеЗначения, ПараметрыЗаполнения, СтруктураДействий) Экспорт
	
	ПараметрыЗаполненияНоменклатурыЕГАИС = Новый Структура;
	ПараметрыЗаполненияНоменклатурыЕГАИС.Вставить("ЗаполнитьФлагАлкогольнаяПродукция", Ложь);
	ПараметрыЗаполненияНоменклатурыЕГАИС.Вставить("ИмяКолонки", "АлкогольнаяПродукция");
	
	СтруктураДействийСДобавленнымиСтроками = Новый Структура;
	СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьНоменклатуруЕГАИС", ПараметрыЗаполненияНоменклатурыЕГАИС);
	Если ПараметрыЗаполнения.ПересчитатьКоличествоЕдиниц Тогда
		СтруктураДействийСДобавленнымиСтроками.Вставить("ПересчитатьКоличествоЕдиниц");
	КонецЕсли;
	Если ПараметрыЗаполнения.ПересчитатьСумму Тогда
		СтруктураДействийСДобавленнымиСтроками.Вставить("ПересчитатьСумму");
	КонецЕсли;
	Если ПараметрыЗаполнения.ЗаполнитьИндексАкцизнойМарки Тогда
		СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьИндексАкцизнойМарки");
	КонецЕсли;
	
	СтруктураДействийСИзмененнымиСтроками = Новый Структура;
	Если ПараметрыЗаполнения.ПересчитатьКоличествоЕдиниц Тогда
		СтруктураДействийСИзмененнымиСтроками.Вставить("ПересчитатьКоличествоЕдиниц");
	КонецЕсли;
	Если ПараметрыЗаполнения.ПересчитатьСумму Тогда
		СтруктураДействийСИзмененнымиСтроками.Вставить("ПересчитатьСумму");
	КонецЕсли;
	Если ПараметрыЗаполнения.ЗаполнитьИндексАкцизнойМарки Тогда
		СтруктураДействийСИзмененнымиСтроками.Вставить("ЗаполнитьИндексАкцизнойМарки");
	КонецЕсли;
	
	СтруктураДействий = ШтрихкодированиеНоменклатурыКлиентСервер.ПараметрыОбработкиШтрихкодов();
	
	СтруктураДействий.Штрихкоды                               = ТаблицаТоваров;
	СтруктураДействий.СтруктураДействийСДобавленнымиСтроками  = СтруктураДействийСДобавленнымиСтроками;
	СтруктураДействий.СтруктураДействийСИзмененнымиСтроками   = СтруктураДействийСИзмененнымиСтроками;
	СтруктураДействий.ТолькоТовары                            = Истина;
	СтруктураДействий.ШтрихкодыВТЧ                            = ПараметрыЗаполнения.ШтрихкодыВТЧ;
	СтруктураДействий.МаркируемаяПродукцияВТЧ                 = ПараметрыЗаполнения.МаркируемаяПродукцияВТЧ;
	СтруктураДействий.ЗагрузкаИзТСД                           = Истина;
	
КонецПроцедуры

// Вызывается после загрузки данных из ТСД. В процедуре нужно проанализировать полученные штрихкоды на предмет сканирования акцизной марки, а также
// обработать штрихкоды, не привязанные к номенклатуре.
//
// Параметры:
//  Форма - УправляемаяФорма - форма документа, в которой были обработаны штрихкоды,
//  ОбработанныеДанные - Структура - подготовленные ранее данные штрихкодов,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ.
//
Процедура ПослеОбработкиТаблицыТоваровПолученнойИзТСД(Форма, ОбработанныеДанные, КэшированныеЗначения) Экспорт
	
	Если ОбработанныеДанные.Свойство("ТекстПредупреждения") Тогда
		Для Каждого СтрокаПредупреждения Из ОбработанныеДанные.ТекстПредупреждения Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаПредупреждения);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// В процедуре нужно открыть форму регистрации новых штрихкодов номенклатуры.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, откуда производится вызов процедуры,
//  ДанныеШтрихкодов - Массив - элемент массива - структура с ключами:
//   * Штрихкод - Строка - штрихкод номенклатуры,
//   * Количество - Число - количество алкогольной продукции.
//
Процедура ОткрытьФормуРегистрацииШтрихкодовНоменклатуры(Форма, ДанныеШтрихкодов) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("НеизвестныеШтрихкоды", ДанныеШтрихкодов);
	
	ОткрытьФорму(
		"Обработка.РаботаСНоменклатурой.Форма.ПоискНоменклатурыПоШтрихкоду",
		ПараметрыОткрытияФормы,
		Форма,
		Новый УникальныйИдентификатор);
	
КонецПроцедуры

// В процедуре реализуется открытие формы считывания акцизной марки в случае необходимости.
//
//Параметры:
//  Форма -  УправляемаяФорма - форма документа, в которой были обработаны штрихкоды,
//  СтруктураДействий - Структура - подготовленные ранее данные штрихкодов.
//
Процедура ОткрытьФормуСчитыванияАкцизнойМаркиПриНеобходимости(Форма, СтруктураДействий) Экспорт
	
	Если НужноОткрытьФормуУказанияАкцизныхМарокПослеОбработкиШтрихкодов(СтруктураДействий) Тогда
		
		ПараметрыСканирования = ШтрихкодированиеИСКлиентСервер.ИнициализироватьПараметрыСканирования(Форма);
		ПараметрыСканирования.ИдентификаторСтроки = СтруктураДействий.МассивСтрокСАкцизнымиМарками[0];
		
		ПараметрыСканирования.Вставить("Форма", Форма);
		
		СчитываниеАкцизнойМарки = Новый ОписаниеОповещения(
			"ОткрытьФормуСчитыванияАкцизнойМарки",
			ИнтеграцияИСРТКлиент,
			ПараметрыСканирования);
		
	Иначе
		СчитываниеАкцизнойМарки = Неопределено;
	КонецЕсли;
	
	Если СчитываниеАкцизнойМарки <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(СчитываниеАкцизнойМарки);
	КонецЕсли;
	
	Если СтруктураДействий.ТекущаяСтрока <> Неопределено Тогда
		Форма.Элементы.Товары.ТекущаяСтрока = СтруктураДействий.ТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииРТ

Процедура ОбработатьДанныеПоКодуКлиент(Форма, СтруктураПараметровКлиента)
	
	ОткрытаБлокирующаяФорма = Ложь;
	
	Если СтруктураПараметровКлиента.Свойство("НеобходимостьВводаАкцизнойМарки")
		И СтруктураПараметровКлиента.Свойство("АктивизироватьСтроку") Тогда
		
		СтрокаТовара = Форма.Объект.Товары.НайтиПоИдентификатору(СтруктураПараметровКлиента.АктивизироватьСтроку);
		Если СтрокаТовара <> Неопределено Тогда
			АкцизныеМаркиКлиентСервер.ЗаполнитьИндексАкцизнойМарки(СтрокаТовара, "Количество");
		КонецЕсли;
	Иначе
		ПодключаемоеОборудованиеРТКлиент.ОбработатьДанныеПоКоду(Форма, СтруктураПараметровКлиента, ОткрытаБлокирующаяФорма);
	КонецЕсли;
	
	Если НЕ ОткрытаБлокирующаяФорма Тогда
		ЗавершитьОбработкуДанныхПоКодуКлиент(Форма, СтруктураПараметровКлиента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗавершитьОбработкуДанныхПоКодуКлиент(Форма, СтруктураПараметровКлиента)
	
	ИдентификаторСтроки = ПодключаемоеОборудованиеРТКлиент.ЗавершитьОбработкуДанныхПоКодуКлиент(Форма, СтруктураПараметровКлиента);
	
КонецПроцедуры

Процедура ПоказатьВводШтрихкода_Завершение(Штрихкод, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Штрихкод) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Новый Структура("Штрихкод, Количество", Штрихкод, 1));
	КонецЕсли;
	
КонецПроцедуры

// Определяет необходимость открытия формы указания акцизных марок после обработки штрихкодов.
// Форму нужно открывать, если был отсканирован один штрихкод маркируемой алкогольной продукции.
//
// Параметры:
//  ПараметрыОбработкиШтрихкодов - Структура - см. ШтрихкодированиеНоменклатурыКлиент.ПараметрыОбработкиШтрихкодов().
//
// Возвращаемое значение:
//  Булево - Истина, если нужно открыть форму.
//
Функция НужноОткрытьФормуУказанияАкцизныхМарокПослеОбработкиШтрихкодов(ПараметрыОбработкиШтрихкодов) Экспорт
	
	ОдинШтрихкод = Ложь;
	
	Если ТипЗнч(ПараметрыОбработкиШтрихкодов.Штрихкоды) = Тип("Массив") Тогда
		ОдинШтрихкод = ПараметрыОбработкиШтрихкодов.Штрихкоды.Количество() = 1;
	Иначе
		ОдинШтрихкод = Истина;
	КонецЕсли;
	
	Если ОдинШтрихкод
		И ПараметрыОбработкиШтрихкодов.МассивСтрокСАкцизнымиМарками.Количество() = 1 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс

#КонецОбласти
