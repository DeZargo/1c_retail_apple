
#Область ПрограммныйИнтерфейс

// Формирует гиперссылку на документ библиотеки в журналах документов РТ
// 
// Параметры:
// 	Параметры        - Структура                           - параметры формирования гиперссылки
// 	ОбъектМетаданных - ОбъектМетаданныхДокумент            - тип метаданных для гиперссылки
// 	Гиперссылка      - ФорматированнаяСтрока, Неопределено - результат формирования гиперссылки
Процедура СформироватьГиперссылкуСмТакжеВРаботе(Параметры, ОбъектМетаданных, Гиперссылка) Экспорт
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.ТТНВходящаяЕГАИС) Тогда
		Возврат;
	КонецЕсли;
	
	Организация    = Параметры.Организация;
	ТорговыйОбъект = Параметры.Склад;
	
	ТекстЗапроса = "";
	ИнтеграцияЕГАИСПереопределяемый.ТекстЗапросаТТН(ТекстЗапроса, Организация, ТорговыйОбъект, Истина);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1");
	
	ПустыеЗначенияДокументаОснования = Новый Массив;
	ПустыеЗначенияДокументаОснования.Добавить(Неопределено);
	Для Каждого ТипыДокументаПоступления Из Метаданные.ОпределяемыеТипы.ОснованиеТТНВходящаяЕГАИС.Тип.Типы() Цикл
		ПустыеЗначенияДокументаОснования.Добавить(Документы[Метаданные.НайтиПоТипу(ТипыДокументаПоступления).Имя].ПустаяСсылка());
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация",                      Организация);
	Запрос.УстановитьПараметр("ТорговыйОбъект",                   ТорговыйОбъект);
	Запрос.УстановитьПараметр("ПустыеЗначенияДокументаОснования", ПустыеЗначенияДокументаОснования);
	Запрос.УстановитьПараметр("ТребующиеДействия",                Документы.ТТНВходящаяЕГАИС.ВсеТребующиеДействия(Истина));
	
	ТекстГиперссылки = НСтр("ru = 'ТТН ЕГАИС (входящие)'");
	Если Запрос.Выполнить().Пустой() Тогда
		Гиперссылка = Новый ФорматированнаяСтрока(
			ТекстГиперссылки,,
			ЦветаСтиля.НезаполненноеПолеТаблицы,,
			ИнтеграцияЕГАИСКлиентСервер.ИмяФормыРабочегоМестаПоОформлениюДокументаТТНВходящаяЕГАИС());
	Иначе
		Гиперссылка = Новый ФорматированнаяСтрока(
			ТекстГиперссылки,,,,
			ИнтеграцияЕГАИСКлиентСервер.ИмяФормыРабочегоМестаПоОформлениюДокументаТТНВходящаяЕГАИС());
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при загрузке после записи алкогольной продукции в базу.
//
// Параметры:
//  АлкогольнаяПродукция - СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС - загружаемая алкогольная продукция,
//  ЕстьИзменения - Булево - признак наличия изменений.
//
Процедура ПриЗагрузкеАлкогольнойПродукции(АлкогольнаяПродукция, ЕстьИзменения) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Процедура вызывается перед записью организации в базу.
//
// Параметры:
//  Организация - СправочникОбъект.КлассификаторОрганизацийЕГАИС - загружаемая организация.
//
Процедура ПриЗагрузкеОрганизации(Организация) Экспорт
	
	Если ЗначениеЗаполнено(Организация.ИНН) И Не Организация.Сопоставлено Тогда
		Контрагент = ИнтеграцияЕГАИСРТ.КонтрагентПоИННКПП(Организация.ИНН, Организация.КПП);
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			Организация.Контрагент     = Контрагент;
			Организация.ТорговыйОбъект = ПараметрыСеанса.ТекущийМагазин;
			Организация.Сопоставлено   = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при загрузке ТТН (перед записью).
//
// Параметры:
//  ТТН_Объект - ДокументОбъект.ТТНВходящаяЕГАИС - загружаемая ТТН.
//
Процедура ПриЗагрузкеТТНВходящаяЕГАИС(ТТНВходящаяОбъект) Экспорт
	
	// Заполнение склада и организации
	РеквизитыОрганизацииЕГАИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТТНВходящаяОбъект.Грузополучатель, "Контрагент, ТорговыйОбъект");
	
	ТТНВходящаяОбъект.Организация = РеквизитыОрганизацииЕГАИС.Контрагент;
	ТТНВходящаяОбъект.ТорговыйОбъект = РеквизитыОрганизацииЕГАИС.ТорговыйОбъект;
	
	// Заполнение документа основания
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТТНИсходящаяЕГАИС.Ссылка            КАК Ссылка,
	|	ТТНИсходящаяЕГАИС.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
	|ГДЕ
	|	ТТНИсходящаяЕГАИС.Идентификатор = &Идентификатор");
	
	Запрос.УстановитьПараметр("Идентификатор", ТТНВходящаяОбъект.Идентификатор);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ТТНИсходящая = Неопределено;
	
	Если Выборка.Следующий() Тогда
		ТТНИсходящая = Выборка.Ссылка;
		Если ЗначениеЗаполнено(Выборка.ДокументОснование) Тогда
			ТТНВходящаяОбъект.ДокументОснование = Выборка.ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнение серий
	ПараметрыУказанияСерий = ИнтеграцияИС.ПараметрыУказанияСерийФормыОбъекта(ТТНВходящаяОбъект, Документы.ТТНВходящаяЕГАИС);;
	
	Если ЗначениеЗаполнено(ТТНВходящаяОбъект.ДокументОснование) Тогда // скопировать ТЧ из ТТНИсходящая
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТТНИсходящаяЕГАИСТовары.НомерСтроки                   КАК НомерСтроки,
		|	ТТНИсходящаяЕГАИСТовары.АлкогольнаяПродукция          КАК АлкогольнаяПродукция,
		|	ТТНИсходящаяЕГАИСТовары.Номенклатура                  КАК Номенклатура,
		|	ТТНИсходящаяЕГАИСТовары.Характеристика                КАК Характеристика,
		|	ТТНИсходящаяЕГАИСТовары.Серия                         КАК Серия,
		|	ТТНИсходящаяЕГАИСТовары.ИдентификаторУпаковки         КАК ИдентификаторУпаковки,
		|	ТТНИсходящаяЕГАИСТовары.Количество                    КАК Количество,
		|	ТТНИсходящаяЕГАИСТовары.Сумма                         КАК Сумма,
		|	ТТНИсходящаяЕГАИСТовары.Цена                          КАК Цена,
		|	ТТНИсходящаяЕГАИСТовары.НомерПартии                   КАК НомерПартии,
		|	ТТНИсходящаяЕГАИСТовары.Справка2.РегистрационныйНомер КАК НомерСправки2Поставщика
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТТНИсходящаяЕГАИСТовары
		|ГДЕ
		|	ТТНИсходящаяЕГАИСТовары.Ссылка = &Ссылка
		|";
		Запрос.УстановитьПараметр("Ссылка", ТТНИсходящая);
		Результат = Запрос.Выполнить();
		
		ТаблицаТТН = ТТНВходящаяОбъект.Товары.Выгрузить(, "НомерСправки2Поставщика,ИдентификаторСтроки,Справка2");
		
		ТТНВходящаяОбъект.Товары.Загрузить(Результат.Выгрузить());
		
		Для Каждого СтрокаТТН Из ТаблицаТТН Цикл
			СтруктураПоиска = Новый Структура("НомерСправки2Поставщика", СтрокаТТН.НомерСправки2Поставщика);
			СтрокиТовары = ТТНВходящаяОбъект.Товары.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого СтрокаТовары Из СтрокиТовары Цикл
				СтрокаТовары.ИдентификаторСтроки = СтрокаТТН.ИдентификаторСтроки;
				СтрокаТовары.Справка2            = СтрокаТТН.Справка2;
			КонецЦикла;
		КонецЦикла;
		
		//пересчитываем количество и цену, если алкогольная продукция неупакованная
		Для каждого СтрокаТТНВходящая Из ТТНВходящаяОбъект.Товары Цикл
			Если СтрокаТТНВходящая.АлкогольнаяПродукция.ТипПродукции = Перечисления.ТипыПродукцииЕГАИС.Неупакованная Тогда
				Если СтрокаТТНВходящая.Номенклатура.ОбъемДАЛ <> 0 Тогда
					СтрокаТТНВходящая.Количество = СтрокаТТНВходящая.Количество * СтрокаТТНВходящая.Номенклатура.ОбъемДАЛ;
					СтрокаТТНВходящая.Цена       = СтрокаТТНВходящая.Цена / СтрокаТТНВходящая.Номенклатура.ОбъемДАЛ;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Иначе // сгенерировать новые серии
		
		СобственныйТорговыйОбъектЗначениеПоУмолчанию = "";
		ЗначенияПоУмолчаниюНеСопоставленныхОбъектов(, СобственныйТорговыйОбъектЗначениеПоУмолчанию);
		
		КонтекстЗаполненияСерий = ИнтеграцияЕГАИС.КонтекстЗаполненияСерий();
		КонтекстЗаполненияСерий.Объект =                                       ТТНВходящаяОбъект;
		КонтекстЗаполненияСерий.Товары =                                       ТТНВходящаяОбъект.Товары;
		КонтекстЗаполненияСерий.ЗаполнятьБезЗапросаСправок =                   Истина;
		КонтекстЗаполненияСерий.СобственныйТорговыйОбъектЗначениеПоУмолчанию = СобственныйТорговыйОбъектЗначениеПоУмолчанию;
		КонтекстЗаполненияСерий.ПараметрыУказанияСерий =                       ПараметрыУказанияСерий;
		
		ЗаполнитьСгенерироватьСерии(КонтекстЗаполненияСерий, Новый Структура);
			
	КонецЕсли;
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(ТТНВходящаяОбъект, ПараметрыУказанияСерий);
	
	Возврат;
	
КонецПроцедуры

// Процедура вызывается при загрузке акта подтверждения ТТН.
//
// Параметры:
//   ТТНИсходящаяОбъект - ДокументОбъект.ТТНИсходящаяЕГАИС - подтверждаемая ТТН,
//   ОтказОтТТН         - Булево                           - признак загрузки акта отказа от ТТН,
//   ЕстьРасхождения    - Булево                           - признак загрузки акта расхождений.
//
Процедура ПриЗагрузкеАктаПодтвержденияТТН(ТТН_Объект, ОтказОтТТН, ЕстьРасхождения) Экспорт
	
	Если ОтказОтТТН И Не ТТН_Объект = Неопределено Тогда
		ИнтеграцияЕГАИСРТ.ОтменитьПроведениеСвязанныхДокументов(ТТН_Объект.Ссылка);
	КонецЕсли;
	
	Если ЕстьРасхождения Тогда
		
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТТН_Объект.Ссылка, "ВидОперации");
		
		Если ВидОперации <> Перечисления.ВидыОперацийТТНИсходящейЕГАИС.ВозвратПоставщику Тогда
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТТНИсходящаяЕГАИС", ТТН_Объект.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТТНИсходящаяЕГАИСТовары.КоличествоФакт,
		|	ТТНИсходящаяЕГАИСТовары.Справка2 КАК Справка2
		|ПОМЕСТИТЬ ТоварыПоФакту
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТТНИсходящаяЕГАИСТовары
		|ГДЕ
		|	ТТНИсходящаяЕГАИСТовары.Ссылка = &ТТНИсходящаяЕГАИС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщикуТовары.Ссылка КАК Ссылка,
		|	ВозвратТоваровПоставщикуТовары.НомерСтроки,
		|	ТоварыПоФакту.КоличествоФакт
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратТоваровПоставщикуТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыПоФакту КАК ТоварыПоФакту
		|		ПО ВозвратТоваровПоставщикуТовары.Справка2 = ТоварыПоФакту.Справка2
		|ГДЕ
		|	ВозвратТоваровПоставщикуТовары.Ссылка.ТТНИсходящаяЕГАИС = &ТТНИсходящаяЕГАИС
		|	И ВозвратТоваровПоставщикуТовары.КоличествоФакт <> ТоварыПоФакту.КоличествоФакт
		|	И НЕ ВозвратТоваровПоставщикуТовары.Ссылка.ПометкаУдаления
		|ИТОГИ ПО
		|	Ссылка";
		
		ВыборкаДокумент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДокумент.Следующий() Цикл
			Док = ВыборкаДокумент.Ссылка.ПолучитьОбъект();
			Выборка = ВыборкаДокумент.Выбрать();
			Пока Выборка.Следующий() Цикл
				Док.Товары[Выборка.НомерСтроки - 1].КоличествоФакт = Выборка.КоличествоФакт;
			КонецЦикла;
			
			Док.ДополнительныеСвойства.Вставить("ЗагрузкаДанныхИзЕГАИС");
			Если Док.Проведен Тогда
				Док.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				Док.Записать();
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при загрузке подтверждения акта расхождений ТТН.
//
// Параметры:
//  ТТНВходящаяЕГАИС - ДокументСсылка.ТТНВходящаяЕГАИС - подтверждаемая ТТН,
//  ОтказОтАкта - Булево - признак загрузки отказа от акта расхождений ТТН.
//
Процедура ПриЗагрузкеПодтвержденияАктаРасхожденийТТН(ТТНВходящаяЕГАИС, ОтказОтАкта) Экспорт
	
	Если ОтказОтАкта Тогда
		ИнтеграцияЕГАИСРТ.ОтменитьПроведениеСвязанныхДокументов(ТТНВходящаяЕГАИС);
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при изменении статуса обработки документа.
//
// Параметры:
//  ДокументСсылка          - ДокументСсылка                                   - ссылка на документ.
//  ПредыдущийСтатус        - ПеречислениеСсылка.СтатусыОбработки*             - предыдущий статус обработки.
//  НовыйСтатус             - ПеречислениеСсылка.СтатусыОбработки*             - новый статус обработки.
//  ДополнительныеПараметры - (см. ИнтеграцияЕГАИС.ПараметрыОбновленияСтатуса) - параметры обновления статуса.
//
Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НовыйСтатус = Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.ОшибкаПередачи
		ИЛИ НовыйСтатус = Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.ОшибкаПроведенияЕГАИС
		ИЛИ НовыйСтатус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи Тогда
		
		ИнтеграцияЕГАИСРТ.ОтменитьПроведениеСвязанныхДокументов(ДокументСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при отказе от входящей ТТН. Нужно реализовать алгоритм отмены проведения связанных поступлений.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНВходящаяЕГАИС - ссылка на документ.
//
Процедура ОтменитьПроведениеПоступленияТоваровПриОтказеОтТТН(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.ТТНВходящаяЕГАИС = &ТТНВходящаяЕГАИС";
	
	Запрос.УстановитьПараметр("ТТНВходящаяЕГАИС", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ПоступлениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ПоступлениеОбъект.Проведен Тогда
			ПоступлениеОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ПоступлениеОбъект.Записать();
		КонецЕсли;
		
		ПоступлениеОбъект.УстановитьПометкуУдаления(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу штрихкодов алкогольной продукции входящей ТТН.
//
// Параметры:
//  ДокументСсылка     - ДокументСсылка.ТТНВходящаяЕГАИС - ссылка на документ.
//  ЗаполняемаяТаблица - ДанныеФормыКоллеция - заполняемая таблица.
//
Процедура ЗаполнитьТаблицуШтрихкодовНеМаркируемойПродукцииТТН_ЕГАИС(ДокументСсылка, ЗаполняемаяТаблица) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|		ПО ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукции
	|		ПО (КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции = ВидыАлкогольнойПродукции.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ТТНВходящаяЕГАИСТовары.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|			И ТТНВходящаяЕГАИСТовары.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И (ТТНВходящаяЕГАИСТовары.Номенклатура.ЕдиницаИзмерения = ШтрихкодыНоменклатуры.Упаковка
	|				ИЛИ ШтрихкодыНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка))
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.Ссылка = &ДокументСсылка
	|	И НЕ ВидыАлкогольнойПродукции.Маркируемый";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	ЗаполняемаяТаблица.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// Заполняет таблицу сопоставленных справок 2 документа документа ТТН входящая.
//
// Параметры:
//  ДокументСсылка     - ДокументСсылка.ТТНВходящаяЕГАИС - ссылка на документ.
//  ЗаполняемаяТаблица - ДанныеФормыКоллеция - заполняемая таблица.
//
Процедура ЗаполнитьТаблицуСопоставленныхСправок2ТТН_ЕГАИС(ДокументСсылка, ЗаполняемаяТаблица) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТТНВходящаяЕГАИСТовары.Справка2 КАК Справка2,
	|	ТТНВходящаяЕГАИСТовары.Номенклатура КАК Номенклатура,
	|	ТТНВходящаяЕГАИСТовары.Характеристика КАК Характеристика
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.Ссылка = &ДокументСсылка
	|	И ТТНВходящаяЕГАИСТовары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ТТНВходящаяЕГАИСТовары.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	ЗаполняемаяТаблица.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// Получить представление валюты регламентированного учета.
// 
// Параметры:
//  Представление - Строка - Возвращаемое значение, представление валюты.
//
Процедура ПредставлениеВалютыРегламентированногоУчета(Представление) Экспорт
	
	Представление = НСтр("ru='руб.'");
	
КонецПроцедуры

// Процедура проверяет наличие расхождений между ТТН и товарами накладной.
//
// Параметры:
//   ДокументСсылка      - ДокументСсылка.ТТНВходящаяЕГАИС - проверяемая ТТН.
//   ДокументПоступления - ДокументСсылка                  - проверяемое поступление товаров.
//   ЕстьРасхождения      - Булево                         - возвращаемое значение: Истина если есть расхождения.
//
Процедура ПриОпределенииРасхожденийСТТНВходящейЕГАИС(ДокументСсылка, ДокументПоступления, ЕстьРасхождения) Экспорт
	
	Если ТипЗнч(ДокументПоступления) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ЕстьРасхождения = ИнтеграцияЕГАИСРТ.ЕстьРасхожденияМеждуДокументомПеремещениеТоваровИТТНЕГАИС(ДокументСсылка, ДокументПоступления);
	ИначеЕсли ТипЗнч(ДокументПоступления) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ЕстьРасхождения = ИнтеграцияЕГАИСРТ.ЕстьРасхожденияМеждуДокументомПоступлениеТоваровИТТНЕГАИС(ДокументСсылка, ДокументПоступления);
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет текст запроса списка остатков к оформлению документа передача в регистр №2 ЕГАИС.
// 
// Параметры:
//   ТекстЗапроса - Строка - текст запроса.
//
Процедура ПриОпределенииТекстаЗапросаОстатковКПередачеВРегистр2ЕГАИС(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2 КАК Справка2,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.СвободныйОстатокОстаток КАК КПередаче
	|ПОМЕСТИТЬ ВТОстатки
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.Остатки КАК ОстаткиАлкогольнойПродукцииЕГАИСОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|		ПО ОстаткиАлкогольнойПродукцииЕГАИСОстатки.ОрганизацияЕГАИС.Код = НастройкиОбменаЕГАИС.ИдентификаторФСРАР
	|ГДЕ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.СвободныйОстатокОстаток > 0
	|	И ОстаткиАлкогольнойПродукцииЕГАИСОстатки.ОрганизацияЕГАИС.ТорговыйОбъект.СкладПродажи.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.ТорговыйЗал)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизныеМарки.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	АкцизныеМарки.Справка2 КАК Справка2,
	|	КОЛИЧЕСТВО(АкцизныеМарки.АкцизнаяМарка) КАК Количество
	|ПОМЕСТИТЬ АкцизныеМарки
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМарки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстатки КАК втОстатки
	|		ПО (втОстатки.Справка2 = АкцизныеМарки.Справка2)
	|			И (втОстатки.ОрганизацияЕГАИС = АкцизныеМарки.ОрганизацияЕГАИС)
	|ГДЕ
	|	АкцизныеМарки.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ВНаличии), ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.КПостановкеНаБаланс))
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМарки.ОрганизацияЕГАИС,
	|	АкцизныеМарки.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2 КАК Справка2,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.КПередаче - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК КПередаче
	|ИЗ
	|	ВТОстатки КАК ОстаткиАлкогольнойПродукцииЕГАИСОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМарки КАК АкцизныеМарки
	|		ПО ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2 = АкцизныеМарки.Справка2
	|			И ОстаткиАлкогольнойПродукцииЕГАИСОстатки.ОрганизацияЕГАИС = АкцизныеМарки.ОрганизацияЕГАИС
	|ГДЕ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.КПередаче - ЕСТЬNULL(АкцизныеМарки.Количество, 0) > 0";
		
КонецПроцедуры

// Определяет текст запроса для движений по регистру Серии номенклатуры.
// 
// Параметры:
//   ТекстЗапроса - Строка - текст запроса.
//   ИмяТаблицы   - Строка - имя метаданных документа из которого выполняется заполнение.
//
Процедура ПриЗаполненииТекстаЗапросаДвижениеСерийТоваров(ТекстЗапроса, ИмяТаблицы) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура                                     КАК Номенклатура,
	|	ТаблицаСерии.Характеристика                                   КАК Характеристика,
	|	ТаблицаСерии.Серия                                            КАК Серия,
	|	ТаблицаСерии.Количество                                       КАК Количество
	|ИЗ
	|	Документ.%ИмяТаблицы%.Товары КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяТаблицы%", ИмяТаблицы);
	
КонецПроцедуры

// Получить количество возможных документов поступления товаров для сопоставления.
//
// Параметры:
//   Количество     - Число                                      - количество документов
//   Организация    - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - ссылка на собственную организацию,
//   ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС        - ссылка на торговый объект,
//   Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - поставщик.
// 
Процедура ПолучитьКоличествоВозможныхДокументовПоступленияТоваров(Количество, Организация, ТорговыйОбъект, Контрагент) Экспорт
	
	КоличествоНакладных = 0;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ПоступлениеТоваровТовары.Ссылка) КАК Количество
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	|		ПО ТТНВходящаяЕГАИС.ДокументОснование = ПоступлениеТоваровТовары.Ссылка
	|ГДЕ
	|	ПоступлениеТоваровТовары.Ссылка.Организация = &Организация
	|	И ПоступлениеТоваровТовары.Ссылка.Магазин = &ТорговыйОбъект
	|	И ПоступлениеТоваровТовары.Ссылка.ТТНВходящаяЕГАИС = ЗНАЧЕНИЕ(Документ.ТТНВходящаяЕГАИС.ПустаяСсылка)
	|	И ПоступлениеТоваровТовары.Ссылка.Контрагент = &Контрагент
	|	И ПоступлениеТоваровТовары.Номенклатура.АлкогольнаяПродукция
	|	И ТТНВходящаяЕГАИС.Ссылка ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ТорговыйОбъект", ТорговыйОбъект);
	Запрос.УстановитьПараметр("Контрагент",     Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак разрешения розничной продажи без сопоставления с классификатором ЕГАИС.
// 
// Возвращаемое значение:
//  Булево - если Истина, продажа разрешена, иначе - Ложь.
//
Процедура РазрешатьПродажуАлкогольнойПродукцииБезСопоставленияЕГАИС(Разрешать) Экспорт
	
	Разрешать = Истина;
	
КонецПроцедуры

// Процедура инициализирует общие структуры, используемые при проведении документов.
// Вызывается из модуля документов при проведении.
//
Процедура ИнициализироватьДополнительныеСвойстваДляПроведенияЧеков(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Возврат;
	
КонецПроцедуры


#Область ОбработчикиСобытийДокументов

// Вызывается при вводе документа на основании, при выполнении метода Заполнить или при интерактивном вводе нового.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - заполняемый документ,
//  ДанныеЗаполнения - Произвольный - значение, которое используется как основание для заполнения,
//  ТекстЗаполнения - Строка, Неопределено - текст, используемый для заполнения документа,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполненияДокумента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияАктаПостановкиНаБалансЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктСписанияЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияАктаСписанияЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		Если ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1 Тогда
			Документы.АктСписанияЕГАИС.ПодобратьСправки2(ДокументОбъект);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратИзРегистра2ЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияВозвратаИзРегистра2ЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОстаткиЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияОстатковЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПередачаВРегистр2ЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияПередачиВРегистр2ЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияТТНИсходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		Документы.ТТНИсходящаяЕГАИС.ПодобратьСправки2(ДокументОбъект);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекЕГАИС") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияЧекаЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекЕГАИСВозврат") Тогда
		
		ИнтеграцияЕГАИСРТ.ОбработкаЗаполненияЧекаЕГАИСВозврат(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "СтатусПроверкиИПодбора") Тогда
		ДокументОбъект.СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.НеВыполнялось;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается расширением формы при необходимости проверки заполнения реквизитов при записи или при проведении документа в форме,
// а также при выполнении метода ПроверитьЗаполнение.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проверяемый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  ПроверяемыеРеквизиты - Массив - массив путей к реквизитам, для которых будет выполнена проверка заполнения,
//  МассивНепроверяемыхРеквизитов - Массив - массив путей к реквизитам, для которых не будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктСписанияЕГАИС") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратИзРегистра2ЕГАИС") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗапросАкцизныхМарокЕГАИС") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПередачаВРегистр2ЕГАИС") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТТНВходящаяЕГАИС") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Серия");
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
		
		СтатусЕГАИС = Неопределено;
		Если ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СтатусыДокументовЕГАИС.Статус КАК Статус
			|ИЗ
			|	РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
			|ГДЕ
			|	СтатусыДокументовЕГАИС.Документ = &Ссылка");
			Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СтатусЕГАИС = Выборка.Статус;
			КонецЕсли;
			
		КонецЕсли;
		
		РедактированиеФормыДоступно = СтатусЕГАИС = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Черновик
		                            Или СтатусЕГАИС = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи
		                            Или СтатусЕГАИС = Неопределено;
		
		Если РедактированиеФормыДоступно Тогда
			
			КлючевыеРеквизитыТабличнойЧасти = Новый Массив;
			КлючевыеРеквизитыТабличнойЧасти.Добавить("АлкогольнаяПродукция");
			КлючевыеРеквизитыТабличнойЧасти.Добавить("Справка2");
			ОбщегоНазначенияРТ.ПроверитьНаличиеДублейСтрокТЧ(
				ДокументОбъект,
				"Товары",
				КлючевыеРеквизитыТабличнойЧасти,
				Отказ);
			
			ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
			
			ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
			
		Иначе
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоУпаковок");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекЕГАИС") Тогда
		
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийЧекаЕГАИС.ВскрытиеТары Тогда
			МассивНепроверяемыхРеквизитов.Добавить("НомерЧекаККМ");
		КонецЕсли;
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекЕГАИСВозврат") Тогда
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеКоличества(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.ШтрихкодыУпаковокТоваров") Тогда
		
		ИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
		Если Не ИспользоватьХарактеристики Или Не Справочники.Номенклатура.ХарактеристикиИспользуются(ДокументОбъект.Номенклатура) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при проведении документа. Выполняется в транзакции записи.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проводимый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  РежимПроведения - РежимПроведенияДокумента - текущий режим проведения.
//
Процедура ОбработкаПроведения(ДокументОбъект, Отказ, РежимПроведения) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// Возникает перед выполнением записи документа. Вызывается после начала транзакции записи, но до начала записи документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - записываемый документ,
//  Отказ - Булево - признак отказа от записи,
//  РежимЗаписи - РежимЗаписиДокумента - текущий режим записи документа,
//  РежимПроведения - РежимПроведенияДокумента - текущий режим проведения документа.
//
Процедура ПередЗаписью(ДокументОбъект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Отказ ИЛИ ДокументОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктСписанияЕГАИС")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратИзРегистра2ЕГАИС")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОстаткиЕГАИС")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПередачаВРегистр2ЕГАИС") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.ОрганизацияЕГАИС, "Контрагент, ТорговыйОбъект");
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Контрагент) Тогда
			ДокументОбъект.Организация = ЗначенияРеквизитов.Контрагент;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.ТорговыйОбъект) Тогда
			ДокументОбъект.ТорговыйОбъект = ЗначенияРеквизитов.ТорговыйОбъект;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТТНВходящаяЕГАИС") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.Грузополучатель, "Контрагент, ТорговыйОбъект");
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Контрагент) Тогда
			ДокументОбъект.Организация = ЗначенияРеквизитов.Контрагент;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.ТорговыйОбъект) Тогда
			ДокументОбъект.ТорговыйОбъект = ЗначенияРеквизитов.ТорговыйОбъект;
		КонецЕсли;
		
		Если ДокументОбъект.СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.Завершено Тогда
			ЕстьРасхождения    = Ложь;
			ТоварыКоличество   = ДокументОбъект.Товары.Количество();
			ИндексСтрокиТовары = 0;
			
			Пока НЕ ЕстьРасхождения И (ИндексСтрокиТовары < ТоварыКоличество) Цикл
				СтрокаТовары = ДокументОбъект.Товары[ИндексСтрокиТовары];
				ЕстьРасхождения = (СтрокаТовары.Количество <> СтрокаТовары.КоличествоФакт);
				ИндексСтрокиТовары = ИндексСтрокиТовары + 1;
			КонецЦикла;
			
			ДокументОбъект.ЕстьРасхождения = ЕстьРасхождения;
			
		Иначе
			
			ДокументОбъект.ЕстьРасхождения = Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.Грузоотправитель, "Контрагент, ТорговыйОбъект");
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Контрагент) Тогда
			ДокументОбъект.Организация = ЗначенияРеквизитов.Контрагент;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.ТорговыйОбъект) Тогда
			ДокументОбъект.ТорговыйОбъект = ЗначенияРеквизитов.ТорговыйОбъект;
		КонецЕсли;
		
		Если (ТипЗнч(ДокументОбъект.ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") ИЛИ ТипЗнч(ДокументОбъект.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров"))
			И ЗначениеЗаполнено(ДокументОбъект.ДокументОснование) Тогда
			Если ДокументОбъект.Ссылка.Пустая() Тогда
				ТТНСсылка = Документы.ТТНИсходящаяЕГАИС.ПолучитьСсылку();
				ДокументОбъект.УстановитьСсылкуНового(ТТНСсылка);
			Иначе
				ТТНСсылка = ДокументОбъект.Ссылка;
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДокументОснование, "ТТНИсходящаяЕГАИС") <> ТТНСсылка Тогда
				ДокументОснование = ДокументОбъект.ДокументОснование.ПолучитьОбъект();
				ДокументОснование.ТТНИсходящаяЕГАИС = ТТНСсылка;
				ДокументОснование.Записать();
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекЕГАИС") Тогда
		
		Если ДокументОбъект.НомерЧекаККМ = 0 И ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийЧекаЕГАИС.ВскрытиеТары Тогда
			Если ПустаяСтрока(ДокументОбъект.Номер) Тогда
				ДокументОбъект.УстановитьНовыйНомер("");
			КонецЕсли;
			ДокументОбъект.НомерЧекаККМ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДокументОбъект.Номер, Истина, Истина));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Проверяет возможность использования регистр №2 для организации.
//
// Параметры:
//  Использовать     - Булево - Истина, если для организации можно использовать регистр №2.
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - ссылка на организацию в классификаторе ЕГАИС.
// 
Процедура ИспользоватьРегистр2(Использовать, ОрганизацияЕГАИС) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОрганизацииЕГАИС.ТорговыйОбъект.СкладПродажи.ТипСклада КАК ТипСклада
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК ОрганизацииЕГАИС
	|		ПО (ОрганизацииЕГАИС.Код = НастройкиОбменаЕГАИС.ИдентификаторФСРАР)
	|ГДЕ
	|	ОрганизацииЕГАИС.Ссылка = &ОрганизацияЕГАИС");
	
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Использовать = Выборка.ТипСклада = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ТорговыйЗал");
	
КонецПроцедуры

// Записывает соответствие между номенклатурой и классификатором ЕГАИС из табличной части документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - документ, содержащий табличную часть Товары.
//  ЗаписыватьСправку2 - Булево - признак сопоставления по справке 2.
//  СопоставлятьПоИдентификаторуУпаковки - Булево - признак сопоставления по идентификатору.
//  СопоставлятьПоСерии - Булево - признак сопоставления по серии.
//
Процедура ЗаписатьСоответствиеНоменклатуры(ДокументОбъект, ЗаписыватьСправку2 = Ложь, СопоставлятьПоИдентификаторуУпаковки = Ложь, СопоставлятьПоСерии = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РозничныеПродажиСервер.СопоставитьАлкогольнуюПродукциюСНоменклатурой(ДокументОбъект,
		"АлкогольнаяПродукция",
		ЗаписыватьСправку2,
		СопоставлятьПоИдентификаторуУпаковки,
		СопоставлятьПоСерии);
	
КонецПроцедуры

// Заполняет таблицу данными о действующих лицензиях на продажу алкогольной продукции.
//
// Параметры:
//  ТаблицаЛицензий - ТаблицаЗначений - таблица для заполнения. Колонки:
//   * Лицензиат     - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - контрагент, для которого нужно получить лицензию,
//   * ДатаТТН       - Дата - дата документа, на которую нужно получить действующую лицензию,
//   * Наименование  - Строка - представление лицензии,
//   * ДатаНачала    - Дата - дата начала действия лицензии,
//   * ДатаОкончания - Дата - дата окончания действия лицензии,
//   * КемВыдана     - Строка - наименование организации, выдавшей лицензию.
//
Процедура ЗаполнитьТаблицуЛицензийНаАлкоголь(ТаблицаЛицензий) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаЛицензий", ТаблицаЛицензий);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЛицензий.Лицензиат КАК Лицензиат,
	|	ТаблицаЛицензий.ДатаТТН КАК ДатаТТН
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&ТаблицаЛицензий КАК ТаблицаЛицензий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.Лицензиат КАК Лицензиат,
	|	ИсходныеДанные.ДатаТТН КАК ДатаТТН,
	|	МАКСИМУМ(ЛицензииПоставщиковАлкогольнойПродукции.ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ ДатыОкончанияЛицензий
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЛицензииПоставщиковАлкогольнойПродукции КАК ЛицензииПоставщиковАлкогольнойПродукции
	|		ПО ИсходныеДанные.Лицензиат = ЛицензииПоставщиковАлкогольнойПродукции.Владелец
	|			И (ИсходныеДанные.ДатаТТН МЕЖДУ ЛицензииПоставщиковАлкогольнойПродукции.ДатаНачала И ЛицензииПоставщиковАлкогольнойПродукции.ДатаОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Лицензиат,
	|	ИсходныеДанные.ДатаТТН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыОкончанияЛицензий.Лицензиат КАК Лицензиат,
	|	ДатыОкончанияЛицензий.ДатаТТН КАК ДатаТТН,
	|	МАКСИМУМ(ЛицензииПоставщиковАлкогольнойПродукции.Ссылка) КАК Лицензия
	|ПОМЕСТИТЬ ЛицензииНаАлкоголь
	|ИЗ
	|	Справочник.ЛицензииПоставщиковАлкогольнойПродукции КАК ЛицензииПоставщиковАлкогольнойПродукции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыОкончанияЛицензий КАК ДатыОкончанияЛицензий
	|		ПО ЛицензииПоставщиковАлкогольнойПродукции.ДатаОкончания = ДатыОкончанияЛицензий.ДатаОкончания
	|			И ЛицензииПоставщиковАлкогольнойПродукции.Владелец = ДатыОкончанияЛицензий.Лицензиат
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыОкончанияЛицензий.Лицензиат,
	|	ДатыОкончанияЛицензий.ДатаТТН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛицензииНаАлкоголь.Лицензиат КАК Лицензиат,
	|	ЛицензииНаАлкоголь.ДатаТТН КАК ДатаТТН,
	|	ЛицензииНаАлкоголь.Лицензия.Наименование КАК Наименование,
	|	ЛицензииНаАлкоголь.Лицензия.ДатаНачала КАК ДатаНачала,
	|	ЛицензииНаАлкоголь.Лицензия.ДатаОкончания КАК ДатаОкончания,
	|	ЛицензииНаАлкоголь.Лицензия.КемВыдана КАК КемВыдана
	|ИЗ
	|	ЛицензииНаАлкоголь КАК ЛицензииНаАлкоголь";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Лицензиат", Выборка.Лицензиат);
		ПараметрыОтбора.Вставить("ДатаТТН", Выборка.ДатаТТН);
		
		МассивСтрок = ТаблицаЛицензий.НайтиСтроки(ПараметрыОтбора);
		Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// В функции нужно определить значения по умолчанию, которые будут подставляться в
// реквизиты не сопоставленных элементов справочника.
//
// Параметры:
//  СобственнаяОрганизация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - значение по умолчанию для собственной организации,
//  СобственныйТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - значение по умолчанию для собственного торгового объекта,
//  СторонняяОрганизация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - значение по умолчанию для сторонней организации,
//  СтороннийТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - значение по умолчанию для стороннего торгового объекта.
//
Процедура ЗначенияПоУмолчаниюНеСопоставленныхОбъектов(
		СобственнаяОрганизация = Неопределено,
		СобственныйТорговыйОбъект = Неопределено,
		СторонняяОрганизация = Неопределено,
		СтороннийТорговыйОбъект = Неопределено) Экспорт
	
	СобственнаяОрганизация    = Справочники.Организации.ПустаяСсылка();
	СобственныйТорговыйОбъект = Справочники.Магазины.ПустаяСсылка();
	СторонняяОрганизация      = Справочники.Контрагенты.ПустаяСсылка();
	СтороннийТорговыйОбъект   = Неопределено;
	
КонецПроцедуры

// Заполняет данные сопоставления организации ЕГАИС с объектами предприятия полями:
//   * ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС, Неопределено - ссылка на организацию в классификаторе ЕГАИС,
//   * Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - сопоставленная организация предприятия,
//   * ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - сопоставленный торговый объект предприятия,
//   * КПП - Строка - КПП организации,
//   * ИНН - Строка - ИНН организации.
//
// Параметры:
//   ДанныеСопоставления - Структура, Неопределено - данные сопоставления
//   КодВФСРАР           - Строка    - код организации в ФСРАР.
//
Процедура ДанныеСопоставленияОрганизацииЕГАИС(ДанныеСопоставления, КодВФСРАР) Экспорт
	
	ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.НайтиПоКоду(КодВФСРАР);
	Если ОрганизацияЕГАИС = Неопределено ИЛИ ОрганизацияЕГАИС.Пустая() Тогда
		ДанныеСопоставления = Неопределено;
	Иначе
		ДанныеСопоставления = Новый Структура;
		ДанныеСопоставления.Вставить("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
		
		РеквизитыОрганизацииЕГАИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияЕГАИС, "Контрагент, ТорговыйОбъект, ИНН, КПП");
		ДанныеСопоставления.Вставить("ТорговыйОбъект",   РеквизитыОрганизацииЕГАИС.ТорговыйОбъект);
		ДанныеСопоставления.Вставить("Организация",      РеквизитыОрганизацииЕГАИС.Контрагент);
		ДанныеСопоставления.Вставить("ИНН",              РеквизитыОрганизацииЕГАИС.ИНН);
		ДанныеСопоставления.Вставить("КПП",              РеквизитыОрганизацииЕГАИС.КПП);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список значений идентификаторов организаций и их представлений, в формате
//   * Значение - Строка - Идентификатор организации для генерации штрихкода, содержащий символы цифр.
//   * Представление - Строка - Представление идентификатора для отображения пользователю.
//
// Параметры:
//   СписокИдентификаторовОрганизации - СписокЗначений - заполняемый список идентификаторов
//   ВключаяДату - Дата - В состав идентификатора может быть включено представление даты маркировки.
//               - Неопределено - В состав идентификатора представление даты не включается.
//   ТолькоКодФСРАР - Булево - получение идентификатора только по коду ФСРАР
//
Процедура ИдентификаторыОрганизации(СписокИдентификаторовОрганизации, ВключаяДату = Неопределено, ТолькоКодФСРАР = Ложь) Экспорт
	
	СписокИдентификаторовОрганизации = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	1 КАК Порядок,
	|	Организации.ИНН КАК ИдентификаторОрганизации,
	|	&ТипИдентификатораИНН КАК ТипИдентификатора
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|	И НЕ Организации.ИНН = """"
	|	И &ТолькоКодФСРАР = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	2,
	|	НастройкиОбменаЕГАИС.ИдентификаторФСРАР,
	|	&ТипИдентификатораКодФСРАР
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ТипИдентификатора";
	
	Запрос.УстановитьПараметр("ТолькоКодФСРАР", ТолькоКодФСРАР);
	Если ВключаяДату = Неопределено Тогда
		Запрос.УстановитьПараметр("ТипИдентификатораИНН",
		                          НСтр("ru = 'ИНН'"));
		
		Запрос.УстановитьПараметр("ТипИдентификатораКодФСРАР",
		                          НСтр("ru = 'Код ФСРАР'"));
	Иначе
		Запрос.УстановитьПараметр("ТипИдентификатораИНН",
		                          НСтр("ru = 'ИНН + дата'"));
		
		Запрос.УстановитьПараметр("ТипИдентификатораКодФСРАР",
		                          НСтр("ru = 'Код ФСРАР + дата'"));
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонПредставления = НСтр("ru = '%1: %2'");
	
	Пока Выборка.Следующий() Цикл
		
		Если ВключаяДату = Неопределено Тогда
			ИдентификаторОрганизации = Выборка.ИдентификаторОрганизации;
		Иначе
			ИдентификаторОрганизации = Выборка.ИдентификаторОрганизации + Формат(ВключаяДату, "ДФ=yyMMdd"); // Установленный формат даты в GS1-128
		КонецЕсли;
		
		ПредставлениеИдентификатора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонПредставления,
			Выборка.ТипИдентификатора,
			ИдентификаторОрганизации);
		
		СписокИдентификаторовОрганизации.Добавить(ИдентификаторОрганизации, ПредставлениеИдентификатора);
	КонецЦикла;
	
КонецПроцедуры

// Поиск контрагента и торгового объекта контрагента (партнера) по ИНН и/или КПП.
//   Если контрагент (и партнер) определен по ИНН/КПП, заполняет ВозвращаемоеЗначение стуктурой
//   * Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - найденный контрагент,
//   * ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС        - найденный торговый объект контрагента (партнер).
//   Неопределено - контрагент не найден.
//
// Параметры:
//   ВозвращаемоеЗначение - Неопределено, Структура - результат поиска,
//   ИНН                  - Строка                  - ИНН искомого контрагента,
//   КПП                  - Строка                  - КПП искомого контрагента.
//
Процедура КонтрагентТорговыйОбъектПоИННКПП(ВозвращаемоеЗначение, ИНН, КПП) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН = &ИНН
	|	И Контрагенты.КПП = &КПП
	|	И НЕ Контрагенты.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ВозвращаемоеЗначение = Неопределено;
	Иначе
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("Контрагент", РезультатЗапроса.Выгрузить()[0].Ссылка);
		ВозвращаемоеЗначение.Вставить("ТорговыйОбъект");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет контрагента из торгового объекта.
//
// Параметры:
//   Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - контрагент торгового объекта.
//   ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - Торговый объект, для которого необходимо получить контрагента.
//
Процедура КонтрагентТорговогоОбъекта(Контрагент, ТорговыйОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТорговыйОбъект", ТорговыйОбъект);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Контрагент КАК Контрагент
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = &ТорговыйОбъект
	|	И НЕ КлассификаторОрганизацийЕГАИС.ПометкаУдаления
	|	И КлассификаторОрганизацийЕГАИС.Сопоставлено";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Контрагент = РезультатЗапроса.Выгрузить()[0].Контрагент;
	Иначе
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет торговый объект из контрагента.
//
// Параметры:
//   ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС        - Торговый объект.
//   Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - Контрагент, ддля которго необходимо получить торговый объект.
//
Процедура ТорговыйОбъектКонтрагента(ТорговыйОбъект, Контрагент) Экспорт
	
	ТорговыйОбъект = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.Контрагент = &Контрагент
	|	И НЕ КлассификаторОрганизацийЕГАИС.ПометкаУдаления
	|	И КлассификаторОрганизацийЕГАИС.Сопоставлено";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТорговыйОбъект = РезультатЗапроса.Выгрузить()[0].ТорговыйОбъект;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает фактический адрес торгового объекта для чека ЕГАИС.
//
// Параметры:
//   Адрес          - Строка                              - адрес торгового объекта.
//   ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - ссылка на торговый объект для определения адреса.
//
Процедура АдресТорговогоОбъекта(Адрес, ТорговыйОбъект) Экспорт
	
	Адрес = ОбщегоНазначенияРТ.АдресМагазина(ТорговыйОбъект);
	
КонецПроцедуры

// Заполняет признак использования торговых объектов для контрагентов.
//
// Параметры:
//   Использовать - Булево - признак использования торговых объектов.
//
Процедура ИспользоватьТорговыеОбъектыКонтрагентов(Использовать) Экспорт
	
	Использовать = Ложь;
	
КонецПроцедуры

// Возвращает метаданные документа поступления товаров, который создается на основании входящей ТТН.
//
Процедура ОпределитьДокументПоступлениеТоваров(ОбъектМетаданных) Экспорт
	
	ОбъектМетаданных = Метаданные.Документы.ПоступлениеТоваров;
	
КонецПроцедуры

// Возвращает имя документа перемещения товаров, который создается на основании входящей ТТН.
//
Процедура ИспользуетсяПеремещениеТоваров(Используется) Экспорт
	
	Используется = Истина;
	
КонецПроцедуры

// Проверить сопоставление классификаторов, заполнить входящую структуру "РезультатПроверки" значениями:
//   * ЕстьНеСопоставленныеТовары - Булево - Признак наличия несопоставленных товаров.
//   * НеСопоставленныеТовары     - Строка - Адрес по временном хранилище.
//
// Параметры:
//   РезультатПроверки       - Структура      - Результат проверки документа на несопоставленные товары.
//   ДокументСсылка          - ДокументСсылка - Документ, для которого необходимо проверить соответствие классификаторов.
//   УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы открытого документа.
//
Процедура ПроверитьСопоставлениеКлассификаторовПрикладногоДокумента(РезультатПроверки, ДокументСсылка, УникальныйИдентификатор) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьНеСопоставленныеТовары", Ложь);
	Результат.Вставить("НеСопоставленныеТовары", "");
	
	ИмяДокумента = ДокументСсылка.Метаданные().Имя;
	Если ИмяДокумента = "СборкаТоваров" Тогда
		ИмяТаблицы = "Документ" + "." + ДокументСсылка.Метаданные().Имя;
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	ТабличнаяЧасть.Номенклатура,
		|	ТабличнаяЧасть.Характеристика
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	ИмяТаблицы КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
		|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	Товары.Номенклатура,
		|	Товары.Характеристика
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура   = Товары.Номенклатура
		|		 И СоответствиеНоменклатурыЕГАИС.Характеристика = Товары.Характеристика
		|ГДЕ
		|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL");
	Иначе
		ИмяТаблицы = "Документ" + "." + ДокументСсылка.Метаданные().Имя + "." + "Товары";
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТабличнаяЧасть.НомерСтроки,
		|	ТабличнаяЧасть.Номенклатура,
		|	ТабличнаяЧасть.Характеристика
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	ИмяТаблицы КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
		|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	Товары.Номенклатура,
		|	Товары.Характеристика
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура   = Товары.Номенклатура
		|		 И СоответствиеНоменклатурыЕГАИС.Характеристика = Товары.Характеристика
		|ГДЕ
		|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL");
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяТаблицы", ИмяТаблицы);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	НеСопоставленныеТовары = Запрос.Выполнить().Выгрузить();
	Результат.ЕстьНеСопоставленныеТовары = НеСопоставленныеТовары.Количество() > 0;
	
	Если Результат.ЕстьНеСопоставленныеТовары Тогда
		Результат.НеСопоставленныеТовары = ПоместитьВоВременноеХранилище(НеСопоставленныеТовары, УникальныйИдентификатор);
	КонецЕсли;
		
КонецПроцедуры

// Заполняет текст запроса ТТН входящих динамического списка, отображаемого в форме списка документов.
//
// Параметры:
//   ТекстЗапроса - Строка - текст запроса динамического списка,
//   Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - организация, по которой нужно отобрать документы,
//   ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - торговый объект, по которому нужно отобрать документы,
//   ОжидаетсяОформление- Булево - если Истина, будут отобраны только документы, ожидающие оформления.
//
Процедура ТекстЗапросаТТН(ТекстЗапроса, Организация = Неопределено, ТорговыйОбъект = Неопределено, ОжидаетсяОформление = Ложь) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументТТНВходящаяЕГАИС.Ссылка,
	|	ДокументТТНВходящаяЕГАИС.ПометкаУдаления,
	|	ДокументТТНВходящаяЕГАИС.Номер,
	|	ДокументТТНВходящаяЕГАИС.Дата,
	|	ДокументТТНВходящаяЕГАИС.Проведен,
	|	ДокументТТНВходящаяЕГАИС.ТорговыйОбъект,
	|	ДокументТТНВходящаяЕГАИС.Организация,
	|	ДокументТТНВходящаяЕГАИС.Идентификатор,
	|	ДокументТТНВходящаяЕГАИС.ИдентификаторЕГАИС,
	|	ДокументТТНВходящаяЕГАИС.Упакована,
	|	ДокументТТНВходящаяЕГАИС.НомерТТН,
	|	ДокументТТНВходящаяЕГАИС.ДатаТТН,
	|	ДокументТТНВходящаяЕГАИС.ДатаОтгрузки,
	|	ДокументТТНВходящаяЕГАИС.Грузоотправитель,
	|	ДокументТТНВходящаяЕГАИС.Грузополучатель,
	|	ДокументТТНВходящаяЕГАИС.Поставщик,
	|	ЕСТЬNULL(ДокументТТНВходящаяЕГАИС.Поставщик.Контрагент, ДокументТТНВходящаяЕГАИС.Грузоотправитель.Контрагент) КАК ПоставщикКонтрагент,
	|	ДокументТТНВходящаяЕГАИС.Основание,
	|	ДокументТТНВходящаяЕГАИС.Комментарий,
	|	ДокументТТНВходящаяЕГАИС.ТипДоставки,
	|	ДокументТТНВходящаяЕГАИС.Перевозчик,
	|	ДокументТТНВходящаяЕГАИС.Автомобиль,
	|	ДокументТТНВходящаяЕГАИС.Прицеп,
	|	ДокументТТНВходящаяЕГАИС.Заказчик,
	|	ДокументТТНВходящаяЕГАИС.Водитель,
	|	ДокументТТНВходящаяЕГАИС.ПунктПогрузки,
	|	ДокументТТНВходящаяЕГАИС.ПунктРазгрузки,
	|	ДокументТТНВходящаяЕГАИС.Перенаправление,
	|	ДокументТТНВходящаяЕГАИС.Экспедитор,
	|	ДокументТТНВходящаяЕГАИС.СуммаДокумента,
	|	ДокументТТНВходящаяЕГАИС.МоментВремени,
	|	ДокументТТНВходящаяЕГАИС.ЕстьРасхождения,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваров.Ссылка ЕСТЬ NULL И ПеремещениеТоваров.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВведеноПоступление,
	|	ВЫБОР
	|		КОГДА НЕ ПеремещениеТоваров.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПеремещениеТоваров.Ссылка
	|		КОГДА НЕ ПоступлениеТоваров.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПоступлениеТоваров.Ссылка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	|	КОНЕЦ КАК ПоступлениеТоваров,
	|	ВЫБОР
	|		КОГДА НЕ ПеремещениеТоваров.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПеремещениеТоваров.Номер
	|		КОГДА НЕ ПоступлениеТоваров.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПоступлениеТоваров.Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ПоступлениеТоваровПредставление,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваров.Ссылка ЕСТЬ NULL И ПеремещениеТоваров.Ссылка ЕСТЬ NULL
	|			И НЕ СтатусыДокументовЕГАИС.Статус В (
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаКПередаче),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаПереданВУТМ),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаОбрабатываетсяЕГАИС),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаОшибка))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено)
	|		КОГДА (НЕ ПоступлениеТоваров.Ссылка ЕСТЬ NULL ИЛИ НЕ ПеремещениеТоваров.Ссылка ЕСТЬ NULL)
	|			И НЕ СтатусыДокументовЕГАИС.Статус В(
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаКПередаче),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаПереданВУТМ),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаОбрабатываетсяЕГАИС),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаОшибка))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.Оформлено)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеТребуется)
	|	КОНЕЦ КАК ОформлениеПоступления,
	|	ВЫБОР
	|		КОГДА СтатусыДокументовЕГАИС.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустойСтатус,
	|
	|	СтатусыДокументовЕГАИС.Статус              КАК СтатусЕГАИС,
	|	СтатусыДокументовЕГАИС.ДальнейшееДействие1 КАК ДальнейшееДействиеЕГАИС1,
	|	СтатусыДокументовЕГАИС.ДальнейшееДействие2 КАК ДальнейшееДействиеЕГАИС2,
	|	СтатусыДокументовЕГАИС.ДальнейшееДействие3 КАК ДальнейшееДействиеЕГАИС3
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ДокументТТНВходящаяЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДокументТТНВходящаяЕГАИС.ДокументОснование = ПоступлениеТоваров.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|		ПО ДокументТТНВходящаяЕГАИС.ДокументОснование = ПеремещениеТоваров.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ДокументТТНВходящаяЕГАИС.Ссылка)
	|ГДЕ
	|	&УсловиеОтбора";
	
	УсловиеОтбора = "";
	Если ЗначениеЗаполнено(Организация) Тогда
		УсловиеОтбора = УсловиеОтбора + "ДокументТТНВходящаяЕГАИС.Организация = &Организация";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТорговыйОбъект) Тогда
		Если ЗначениеЗаполнено(УсловиеОтбора) Тогда
			УсловиеОтбора = УсловиеОтбора + " И ";
		КонецЕсли;
		УсловиеОтбора = УсловиеОтбора + "ДокументТТНВходящаяЕГАИС.ТорговыйОбъект = &ТорговыйОбъект";
	КонецЕсли;
	
	Если ОжидаетсяОформление Тогда
		Если ЗначениеЗаполнено(УсловиеОтбора) Тогда
			УсловиеОтбора = УсловиеОтбора + " И ";
		КонецЕсли;
		УсловиеОтбора = УсловиеОтбора +
		"(СтатусыДокументовЕГАИС.ДальнейшееДействие1 В(&ТребующиеДействия))";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловиеОтбора) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", УсловиеОтбора);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", "ИСТИНА");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет текст запроса для формирования временной таблицы коэффициентов пересчета базовых единиц измерения
//   номенклатуры в единицы ЕГАИС:
//   - для упакованного товара: коэффициент пересчета в штуки (бутылки)
//   - для неупакованного товара: коэффициент пересчета в декалитры.
// Временная таблица используется при проведении документов по регистру ОстаткиЕГАИС и при передаче данных в УТМ.
//
// Параметры:
//   ТекстЗапроса        - Строка - Заполняемый текст запроса.
//   ИмяТаблицыТовары    - Строка - Имя таблицы с колонками: АлкогольнаяПродукция, Номенклатура, Характеристика.
//   ИмяВременнойТаблицы - Строка - Имя результирующей временной таблицы.
//
Процедура ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(ТекстЗапроса, ИмяТаблицыТовары = "ВТТовары", ИмяВременнойТаблицы = "ВТКоэффициентыПересчетаВЕдиницыЕГАИС") Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА Таблица.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ПроверятьОбъемДАЛ,
	|	МАКСИМУМ(ЕСТЬNULL(Таблица.Номенклатура.ОбъемДАЛ, 0)) КАК ОбъемДАЛ,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА Таблица.Номенклатура.ВидНоменклатуры.ПродаетсяВРозлив
	|				ТОГДА ЕСТЬNULL(Таблица.Номенклатура.ОбъемДАЛ, 0)
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Коэффициент
	|ПОМЕСТИТЬ ШаблонИмяВременнойТаблицы
	|ИЗ
	|	ШаблонТаблицаТовары КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.АлкогольнаяПродукция,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Серия";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ШаблонТаблицаТовары",       ИмяТаблицыТовары);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ШаблонИмяВременнойТаблицы", ИмяВременнойТаблицы);
		
КонецПроцедуры

#Область МенюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов.
//
Процедура ДобавитьКомандуАнализРасхожденийПриПоступленииАлкогольнойПродукцииВТТНЕГАИС(КомандыОтчетов) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

// Заполняет в переданной таблице значений реквизиты: Крепость, ВидПродукции и Объем.
//
// Параметры:
//  ТаблицаНоменклатуры - ТаблицаЗначений - таблица номенклатуры с колонками: НомерСтроки, Номенклатура.
//
Процедура ЗаполнитьРеквизитыАлкогольнойПродукции(ТаблицаНоменклатуры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ
	|	Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТь
	|	Товары.НомерСтроки                                          КАК НомерСтроки,
	|	Товары.Номенклатура.Крепость                                КАК Крепость,
	|	Товары.Номенклатура.ВидАлкогольнойПродукцииЕГАИС            КАК ВидПродукции,
	|	ВЫРАЗИТЬ(Товары.Номенклатура.ОбъемДАЛ * 10 КАК Число(10,3)) КАК Объем
	|ИЗ
	|	Товары КАК Товары
	|");
	
	Запрос.УстановитьПараметр("Товары", ТаблицаНоменклатуры);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаНоменклатуры.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаТаблицы <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка, "Крепость, ВидПродукции, Объем");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область СопоставлениеКлассификаторовЕГАИС

// Процедура определяет текст запроса сопоставления организаций ЕГАИС из входящих ТТН контрагентам 
//   Ожидаемые свойства текста запроса:
//    * Используется входящий параметр &СписокТТН
//    * В последнем запросе пакета возвращается таблица организаций ЕГАИС без найденных сопоставлений
//    * В предпоследнем запросе пакета возвращается таблица найденных сопоставлений
//    * В предпредпоследнем запросе пакета возвращается таблица контрагентов для сопоставления
// Параметры:
//   ТекстЗапроса - Строка - текст запроса.
//
Процедура ПриЗаполненииТекстаЗапросаСопоставленияОрганизацийЕГАИСПоТТН(ТекстЗапроса) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.ИНН КАК ИНН,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.КПП КАК КПП,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.Наименование КАК Наименование,
	|	ПОДСТРОКА(ВложенныйЗапрос.ОрганизацияЕГАИС.НаименованиеПолное, 1, 255) КАК НаименованиеПолное,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.Сопоставлено КАК Сопоставлено
	|ПОМЕСТИТЬ ВтОрганизацииЕГАИС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТТНВходящаяЕГАИС.Грузоотправитель КАК ОрганизацияЕГАИС
	|	ИЗ
	|		Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|	ГДЕ
	|		ТТНВходящаяЕГАИС.Ссылка В(&СписокТТН)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТТНВходящаяЕГАИС.Грузополучатель
	|	ИЗ
	|		Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|	ГДЕ
	|		ТТНВходящаяЕГАИС.Ссылка В(&СписокТТН)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТТНВходящаяЕГАИС.Поставщик
	|	ИЗ
	|		Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|	ГДЕ
	|		ТТНВходящаяЕГАИС.Ссылка В(&СписокТТН)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.Импортер = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.АлкогольнаяПродукция.Производитель
	|			ИНАЧЕ ТабличнаяЧасть.АлкогольнаяПродукция.Импортер
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка В(&СписокТТН)) КАК ВложенныйЗапрос
	|ГДЕ
	|	НЕ ВложенныйЗапрос.ОрганизацияЕГАИС = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|	И НЕ ВложенныйЗапрос.ОрганизацияЕГАИС.СоответствуетОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОрганизацииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВтОрганизацииЕГАИС.Наименование КАК НаименованиеЕГАИС,
	|	Контрагенты1.Ссылка КАК Контрагент1,
	|	НЕОПРЕДЕЛЕНО КАК ТорговыйОбъект1,
	|	Контрагенты2.Ссылка КАК Контрагент2,
	|	НЕОПРЕДЕЛЕНО КАК ТорговыйОбъект2
	|ПОМЕСТИТЬ ВтСопоставления
	|ИЗ
	|	ВтОрганизацииЕГАИС КАК ВтОрганизацииЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты1
	|		ПО ВтОрганизацииЕГАИС.ИНН = Контрагенты1.ИНН
	|			И ВтОрганизацииЕГАИС.КПП = Контрагенты1.КПП
	|			И (НЕ Контрагенты1.ИНН = """")
	|			И (НЕ Контрагенты1.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты2
	|		ПО ВтОрганизацииЕГАИС.ИНН = Контрагенты2.ИНН
	|			И (НЕ Контрагенты2.ИНН = """")
	|			И (НЕ Контрагенты2.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОрганизацииЕГАИС.Наименование КАК Наименование,
	|	ОрганизацииЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ОрганизацииЕГАИС.Контрагент КАК Контрагент,
	|	ОрганизацииЕГАИС.Сопоставлено КАК Сопоставлено,
	|	ОрганизацииЕГАИС.ИНН КАК ИНН,
	|	ОрганизацииЕГАИС.КПП КАК КПП,
	|	ОрганизацииЕГАИС.НаименованиеПолное КАК НаименованиеПолное
	|ИЗ
	|	ВтОрганизацииЕГАИС КАК ОрганизацииЕГАИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сопоставления.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ЕСТЬNULL(Сопоставления.Контрагент1, Сопоставления.Контрагент2) КАК Контрагент,
	|	ЕСТЬNULL(Сопоставления.ТорговыйОбъект1, Сопоставления.ТорговыйОбъект2) КАК ТорговыйОбъект
	|ИЗ
	|	ВтСопоставления КАК Сопоставления
	|ГДЕ
	|	Сопоставления.Контрагент2 ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сопоставления.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	Сопоставления.НаименованиеЕГАИС КАК НаименованиеЕГАИС
	|ИЗ
	|	ВтСопоставления КАК Сопоставления
	|ГДЕ
	|	Сопоставления.Контрагент2 ЕСТЬ NULL";
	
КонецПроцедуры

// Процедура определяет текст запроса сопоставления алкогольной продукции ЕГАИС из входящих ТТН номенклатуре
//   Ожидаемые свойства текста запроса:
//    * Используется входящий параметр &СписокТТН
//    * В предпоследнем запросе пакета возвращается таблица алкогольной продукции с колонками
//     ** АлкогольнаяПродукция - СправочникСсылка.КлассификаторАлкогольнойПродукции - ссылка на алкогольную продукцию,
//     ** Наименование         - Строка                                             - ее наименование.
//    * В последнем запросе пакета возвращается таблица номенклатуры с колонками
//     ** Номенклатура         - ОпределяемыйТип.Номенклатура - ссылка на номенклатуру алкогольной продукции,
//     ** Наименование         - Строка                       - ее наименование.
//    * В одном из запросов пакета создается временная таблица втНоменклатура с колонками
//     ** Номенклатура,
//     ** Наименование,
//     ** ВидАлкогольнойПродукции,
//     ** ПроизводительИмпортерДляДекларацийАлко,
//     ** ОбъемДАЛ,
//     ** Крепость.
//    * В одном из запросов пакета создается временная таблица втДанныеТТН с колонками
//     ** АлкогольнаяПродукция,
//     ** ИдентификаторУпаковки,
//     ** Количество,
//     ** Номенклатура,
//     ** Характеристика,
//     ** Серия,
//     ** Наименование,
//     ** НаименованиеПолное,
//     ** Объем,
//     ** Крепость,
//     ** Производитель,
//     ** Импортер,
//     ** ПроизводительИБ,
//     ** ИмпортерИБ.
//
// Параметры:
//   ТекстЗапроса - Строка - текст запроса.
//
Процедура ПриЗаполненииТекстаЗапросаСопоставленияНоменклатурыПоТТН(ТекстЗапроса) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
	|	Товары.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	Товары.Номенклатура          КАК Номенклатура,
	|	Товары.Характеристика        КАК Характеристика,
	|	Товары.Серия                 КАК Серия
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В (&СписокТТН)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ втАлкогольнаяПродукция
	|ИЗ
	|	втТовары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАлкогольнаяПродукция.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Количество
	|ПОМЕСТИТЬ СопоставленоПозиций
	|ИЗ
	|	втАлкогольнаяПродукция КАК втАлкогольнаяПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО (СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = втАлкогольнаяПродукция.АлкогольнаяПродукция)
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	втАлкогольнаяПродукция.АлкогольнаяПродукция
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.ИдентификаторУпаковки                                      КАК ИдентификаторУпаковки,
	|	Товары.АлкогольнаяПродукция                                       КАК АлкогольнаяПродукция,
	|	Товары.АлкогольнаяПродукция.Объем                                 КАК Объем,
	|	ЕСТЬNULL(СопоставленоПозиций.Количество, 0)                       КАК Количество,
	|	Товары.АлкогольнаяПродукция.Крепость                              КАК Крепость,
	|	Товары.АлкогольнаяПродукция.Наименование                          КАК Наименование,
	|	ПОДСТРОКА(Товары.АлкогольнаяПродукция.НаименованиеПолное, 1, 255) КАК НаименованиеПолное,
	|	Товары.АлкогольнаяПродукция.ВидПродукции                          КАК ВидПродукции,
	|	Товары.АлкогольнаяПродукция.Производитель.Контрагент              КАК ПроизводительИБ,
	|	Товары.АлкогольнаяПродукция.Импортер.Контрагент                   КАК ИмпортерИБ,
	|	Товары.АлкогольнаяПродукция.Производитель                         КАК Производитель,
	|	Товары.АлкогольнаяПродукция.Импортер                              КАК Импортер,
	|	Товары.Номенклатура                                               КАК Номенклатура,
	|	Товары.Характеристика                                             КАК Характеристика,
	|	Товары.Серия                                                      КАК Серия
	|ПОМЕСТИТЬ втДанныеТТН
	|ИЗ
	|	втТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленоПозиций КАК СопоставленоПозиций
	|		ПО (СопоставленоПозиций.АлкогольнаяПродукция = Товары.АлкогольнаяПродукция)
	|		
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка                                 КАК Номенклатура,
	|	СправочникНоменклатура.Наименование                           КАК Наименование,
	|	СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС           КАК ВидАлкогольнойПродукции,
	|	СправочникНоменклатура.ПроизводительИмпортерАлкогольнойПродукции КАК ПроизводительИмпортер,
	|	СправочникНоменклатура.ОбъемДАЛ                               КАК ОбъемДАЛ,
	|	СправочникНоменклатура.Крепость                               КАК Крепость
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|ГДЕ
	|	СправочникНоменклатура.АлкогольнаяПродукция
	|	И НЕ СправочникНоменклатура.ВидНоменклатуры.ПродаетсяВРозлив
	|	И НЕ СправочникНоменклатура.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеТТН.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	втДанныеТТН.Наименование         КАК Наименование
	|ИЗ
	|	втДанныеТТН КАК втДанныеТТН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура КАК Номенклатура,
	|	втНоменклатура.Наименование КАК Наименование
	|ИЗ
	|	втНоменклатура КАК втНоменклатура
	|";
	
КонецПроцедуры

// Процедура определяет текст запроса сопоставления организаций ЕГАИС из входящих данных (товаров и организаций) контрагентам 
//   Ожидаемые свойства текста запроса:
//    * Используется входящий параметр &Товары: таблица с колонками
//     ** АлкогольнаяПродукция,
//     ** ИдентификаторУпаковки,
//     ** Номенклатура,
//     ** Характеристика.
//    * Используется входящий параметр &ОрганизацииЕГАИС: таблица с колонками
//     ** ОрганизацияЕГАИС
//    * В последнем запросе пакета возвращается таблица организаций ЕГАИС без найденных сопоставлений
//    * В предпоследнем запросе пакета возвращается таблица найденных сопоставлений
//    * В предпредпоследнем запросе пакета возвращается таблица контрагентов для сопоставления
// Параметры:
//   ТекстЗапроса - Строка - текст запроса.
//
Процедура ПриЗаполненииТекстаЗапросаСопоставленияОрганизацийЕГАИСПоВходящимДанным(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(Товары.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция,
	|	Товары.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ втТаблицаТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС
	|ПОМЕСТИТЬ втТаблицаОрганизацииЕГАИС
	|ИЗ
	|	&ОрганизацииЕГАИС КАК ОрганизацииЕГАИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.ИНН КАК ИНН,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.КПП КАК КПП,
	|	ПОДСТРОКА(ВложенныйЗапрос.ОрганизацияЕГАИС.Адрес, 1, 1000) КАК Адрес,
	|	ПОДСТРОКА(ВложенныйЗапрос.ОрганизацияЕГАИС.ПредставлениеАдреса, 1, 1000) КАК ПредставлениеАдреса,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.Наименование КАК Наименование,
	|	ПОДСТРОКА(ВложенныйЗапрос.ОрганизацияЕГАИС.НаименованиеПолное, 1, 255) КАК НаименованиеПолное,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС.Сопоставлено КАК Сопоставлено
	|ПОМЕСТИТЬ ВтОрганизацииЕГАИС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОрганизацииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС
	|	ИЗ
	|		втТаблицаОрганизацииЕГАИС КАК ОрганизацииЕГАИС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.Импортер = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.АлкогольнаяПродукция.Производитель
	|			ИНАЧЕ ТабличнаяЧасть.АлкогольнаяПродукция.Импортер
	|		КОНЕЦ
	|	ИЗ
	|		втТаблицаТовары КАК ТабличнаяЧасть) КАК ВложенныйЗапрос
	|ГДЕ
	|	НЕ ВложенныйЗапрос.ОрганизацияЕГАИС = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|	И НЕ ВложенныйЗапрос.ОрганизацияЕГАИС.СоответствуетОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОрганизацииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВтОрганизацииЕГАИС.Наименование КАК НаименованиеЕГАИС,
	|	Контрагенты1.Ссылка КАК Контрагент1,
	|	НЕОПРЕДЕЛЕНО КАК ТорговыйОбъект1,
	|	Контрагенты2.Ссылка КАК Контрагент2,
	|	НЕОПРЕДЕЛЕНО КАК ТорговыйОбъект2
	|ПОМЕСТИТЬ ВтСопоставления
	|ИЗ
	|	ВтОрганизацииЕГАИС КАК ВтОрганизацииЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты1
	|		ПО ВтОрганизацииЕГАИС.ИНН = Контрагенты1.ИНН
	|			И ВтОрганизацииЕГАИС.КПП = Контрагенты1.КПП
	|			И (НЕ Контрагенты1.ИНН = """")
	|			И (НЕ Контрагенты1.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты2
	|		ПО ВтОрганизацииЕГАИС.ИНН = Контрагенты2.ИНН
	|			И (НЕ Контрагенты2.ИНН = """")
	|			И (НЕ Контрагенты2.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОрганизацииЕГАИС.Наименование КАК Наименование,
	|	ОрганизацииЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ОрганизацииЕГАИС.Контрагент КАК Контрагент,
	|	ОрганизацииЕГАИС.Сопоставлено КАК Сопоставлено,
	|	ОрганизацииЕГАИС.ИНН КАК ИНН,
	|	ОрганизацииЕГАИС.КПП КАК КПП,
	|	ОрганизацииЕГАИС.Адрес КАК Адрес,
	|	ОрганизацииЕГАИС.ПредставлениеАдреса КАК ПредставлениеАдреса,
	|	ОрганизацииЕГАИС.НаименованиеПолное КАК НаименованиеПолное
	|ИЗ
	|	ВтОрганизацииЕГАИС КАК ОрганизацииЕГАИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сопоставления.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ЕСТЬNULL(Сопоставления.Контрагент1, Сопоставления.Контрагент2) КАК Контрагент,
	|	ЕСТЬNULL(Сопоставления.ТорговыйОбъект1, Сопоставления.ТорговыйОбъект2) КАК ТорговыйОбъект
	|ИЗ
	|	ВтСопоставления КАК Сопоставления
	|ГДЕ
	|	Сопоставления.Контрагент2 ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сопоставления.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	Сопоставления.НаименованиеЕГАИС КАК НаименованиеЕГАИС
	|ИЗ
	|	ВтСопоставления КАК Сопоставления
	|ГДЕ
	|	Сопоставления.Контрагент2 ЕСТЬ NULL";
	
КонецПроцедуры

// Процедура определяет текст запроса сопоставления алкогольной продукции ЕГАИС из входящих данных номенклатуре
//   Ожидаемые свойства текста запроса:
//    * Используется входящий параметр &Товары: таблица с колонками
//     ** АлкогольнаяПродукция,
//     ** ИдентификаторУпаковки,
//     ** Номенклатура,
//     ** Характеристика,
//     ** Серия.
//    * В предпоследнем запросе пакета возвращается таблица алкогольной продукции с колонками
//     ** АлкогольнаяПродукция - СправочникСсылка.КлассификаторАлкогольнойПродукции - ссылка на алкогольную продукцию,
//     ** Наименование         - Строка                                             - ее наименование.
//    * В последнем запросе пакета возвращается таблица номенклатуры с колонками
//     ** Номенклатура         - ОпределяемыйТип.Номенклатура - ссылка на номенклатуру алкогольной продукции,
//     ** Наименование         - Строка                       - ее наименование.
//    * В одном из запросов пакета создается временная таблица втНоменклатура с колонками
//     ** Номенклатура,
//     ** Наименование,
//     ** ВидАлкогольнойПродукции,
//     ** ПроизводительИмпортерДляДекларацийАлко,
//     ** ОбъемДАЛ,
//     ** Крепость.
//    * В одном из запросов пакета создается временная таблица втДанныеТТН с колонками
//     ** АлкогольнаяПродукция,
//     ** ИдентификаторУпаковки,
//     ** Количество,
//     ** Номенклатура,
//     ** Характеристика,
//     ** Серия,
//     ** Наименование,
//     ** НаименованиеПолное,
//     ** Объем,
//     ** Крепость,
//     ** Производитель,
//     ** Импортер,
//     ** ПроизводительИБ,
//     ** ИмпортерИБ.
//
// Параметры:
//   ТекстЗапроса - Строка - текст запроса.
//
Процедура ТекстЗапросаСопоставлениеНоменклатурыПоВходящимДанным(ТекстЗапроса) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(Товары.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция,
	|	Товары.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ втАлкогольнаяПродукция
	|ИЗ
	|	втТовары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАлкогольнаяПродукция.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ СопоставленоПозиций
	|ИЗ
	|	втАлкогольнаяПродукция КАК втАлкогольнаяПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО (СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = втАлкогольнаяПродукция.АлкогольнаяПродукция)
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	втАлкогольнаяПродукция.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Товары.АлкогольнаяПродукция.Объем КАК Объем,
	|	Товары.АлкогольнаяПродукция.Крепость КАК Крепость,
	|	Товары.АлкогольнаяПродукция.Наименование КАК Наименование,
	|	ПОДСТРОКА(Товары.АлкогольнаяПродукция.НаименованиеПолное, 1, 255) КАК НаименованиеПолное,
	|	Товары.АлкогольнаяПродукция.ВидПродукции КАК ВидПродукции,
	|	Товары.АлкогольнаяПродукция.Производитель.Контрагент КАК ПроизводительИБ,
	|	Товары.АлкогольнаяПродукция.Импортер.Контрагент КАК ИмпортерИБ,
	|	Товары.АлкогольнаяПродукция.Производитель КАК Производитель,
	|	Товары.АлкогольнаяПродукция.Импортер КАК Импортер,
	|	ЕСТЬNULL(СопоставленоПозиций.Количество, 0) КАК Количество,
	|	ВЫБОР
	|		КОГДА Товары.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА СоответствиеНоменклатурыЕГАИС.Номенклатура
	|		ИНАЧЕ Товары.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Товары.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА СоответствиеНоменклатурыЕГАИС.Характеристика
	|		ИНАЧЕ Товары.Характеристика
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА СоответствиеНоменклатурыЕГАИС.Серия
	|		ИНАЧЕ Товары.Серия
	|	КОНЕЦ КАК Серия
	|ПОМЕСТИТЬ втДанныеТТН
	|ИЗ
	|	втТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленоПозиций КАК СопоставленоПозиций
	|		ПО (СопоставленоПозиций.АлкогольнаяПродукция = Товары.АлкогольнаяПродукция)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО Товары.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|			И (СопоставленоПозиций.Количество = 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	СправочникНоменклатура.Наименование КАК Наименование,
	|	СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС КАК ВидАлкогольнойПродукции,
	|	СправочникНоменклатура.ПроизводительИмпортерАлкогольнойПродукции КАК ПроизводительИмпортер,
	|	СправочникНоменклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	СправочникНоменклатура.Крепость КАК Крепость
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|ГДЕ
	|	СправочникНоменклатура.АлкогольнаяПродукция
	|	И НЕ СправочникНоменклатура.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеТТН.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	втДанныеТТН.Наименование КАК Наименование
	|ИЗ
	|	втДанныеТТН КАК втДанныеТТН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура КАК Номенклатура,
	|	втНоменклатура.Наименование КАК Наименование
	|ИЗ
	|	втНоменклатура КАК втНоменклатура";
	
КонецПроцедуры


#КонецОбласти

// Заполняет таблицу алкогольной продукции документа-основания с колонками:
//   * Номенклатура
//   * Характеристика
//   * Серия
//   * Количество
// 
// Параметры:
// 	ДокументОснование - ДокументСсылка                 - документ-основание.
// 	ДокументЕГАИС     - ОпределяемыйТип.ДокументыЕГАИС - документ по которому требуется получить данные документа-основания.
// 	ТаблицаДанных     - ТаблицаЗначений                - заполняемые данные основания
//
Процедура СформироватьДанныеДокументаОснования(ДокументОснование, ДокументЕГАИС, ТаблицаДанных) Экспорт
	
	ИнтеграцияЕГАИСРТ.СформироватьДанныеДокументаОснования(ДокументОснование, ДокументЕГАИС, ТаблицаДанных);
		
КонецПроцедуры

// Обрабатывает таблицу штриховых кодов и получает для каждого из них данные в информационной базе.
// На входе имеется таблица, в которой заполнены только колонки "Штриховой код" и "Количество".
// В процедуре устанавливается значение "Истина" в одной из других колонок.
//
// Параметры:
//  ТаблицаНеАкцизныеМарки               - ТаблицаЗначение - имеет слудующие колонки:
//   * ШтриховойКод                      - Строка                              - штриховой код полученный с ТСД.
//   * Количество                        - Число                               - сколько раз был считан данный штрихкод.
//   * ШтрихКодУпаковки                  - Справочник.ШтрихкодыУпаковокТоваров - ссылка на имеющуюся в базе упаковку.
//   * ЭтоИмеющаясяВБазеУпаковка         - Булево                              - если это имеющаяся в базе упаковка.
//   * ЭтоУпаковка                       - Булево                              - если это новая упаковка.
//   * ЭтоПартионнаяАлкогольнаяПродукция - Булево                              - если это штрихкод партионной алкогольной продукции
//   * ЭтоПрочаяНоменклатура             - Булево                              - если это штрихкод не идентифицирует алкогольную продукцию.
//
Процедура ОпределитьТипыШтриховыхКодовНеМаркируемойПродукцииТСД(ТаблицаНеАкцизныеМарки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ШтриховыеКоды.ШтриховойКод                      КАК ШтриховойКод,
	|	ШтриховыеКоды.Количество                        КАК Количество,
	|	ШтриховыеКоды.ШтрихКодУпаковки                  КАК ШтрихКодУпаковки,
	|	ШтриховыеКоды.ЭтоУпаковка                       КАК ЭтоУпаковка,
	|	ШтриховыеКоды.ЭтоИмеющаясяВБазеУпаковка         КАК ЭтоИмеющаясяВБазеУпаковка,
	|	ШтриховыеКоды.ЭтоПартионнаяАлкогольнаяПродукция КАК ЭтоПартионнаяАлкогольнаяПродукция,
	|	ШтриховыеКоды.ЭтоПрочаяНоменклатура             КАК ЭтоПрочаяНоменклатура
	|ПОМЕСТИТЬ ШтриховыеКоды
	|ИЗ
	|	&ШтриховыеКоды КАК ШтриховыеКоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ШтриховыеКоды.ШтриховойКод                                                                            КАК ШтриховойКод,
	|	ШтриховыеКоды.Количество                                                                              КАК Количество,
	|	ЕСТЬNULL(ШтрихкодыУпаковокТоваров.Ссылка, ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)) КАК ШтрихКодУпаковки,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.Ссылка ЕСТЬ NULL
	|				И ШтрихкодыУпаковокТоваров.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                                                  КАК ЭтоУпаковка,
	|	ВЫБОР
	|		КОГДА ШтрихкодыУпаковокТоваров.ТипУпаковки <> ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                                                  КАК ЭтоИмеющаясяВБазеУпаковка,
	|	ВЫБОР
	|		КОГДА ВидыАлкогольнойПродукции.Маркируемый = ЛОЖЬ
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                                                  КАК ЭтоПартионнаяАлкогольнаяПродукция,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.АлкогольнаяПродукция = ЛОЖЬ
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                                                  КАК ЭтоПрочаяНоменклатура
	|ИЗ
	|	ШтриховыеКоды КАК ШтриховыеКоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ШтриховыеКоды.ШтриховойКод = ШтрихкодыНоменклатуры.Штрихкод
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО (ШтрихкодыНоменклатуры.Владелец = СправочникНоменклатура.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукции
	|		ПО (СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС = ВидыАлкогольнойПродукции.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ПО ШтриховыеКоды.ШтриховойКод = ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода";
	
	Запрос.УстановитьПараметр("ШтриховыеКоды", ТаблицаНеАкцизныеМарки);
	
	ТаблицаНеАкцизныеМарки = Запрос.Выполнить().Выгрузить();

КонецПроцедуры

#КонецОбласти

#Область ПанельОбменСЕГАИС

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаАктПостановкиНаБалансЕГАИСОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ПОМЕСТИТЬ
	|	ВремТаблВРегистры1И2
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.АктПостановкиНаБалансЕГАИС.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И ((%УсловиеОрганизация%) ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	ПравоДоступаПересортицаТоваров   = ПравоДоступа("Чтение", Метаданные.Документы.ПересортицаТоваров);
	ПравоДоступаОприходованиеТоваров = ПравоДоступа("Чтение", Метаданные.Документы.ОприходованиеТоваров);
	
	ЛевоеСоединение = "";
	Если ПравоДоступаПересортицаТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ПересортицаТоваров КАК ДокументПересортицаТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументПересортицаТоваров.Ссылка
		|";
	КонецЕсли;
	
	Если ПравоДоступаОприходованиеТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ОприходованиеТоваров КАК ДокументОприходованиеТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументОприходованиеТоваров.Ссылка
		|";
	КонецЕсли;
	
	Если ПравоДоступаПересортицаТоваров
		Или ПравоДоступаОприходованиеТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ПО
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&ОрганизацияЕГАИС)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	УсловиеСсылка = "";
	УсловиеОрганизация = "";
	
	Если ПравоДоступаПересортицаТоваров
		И ПравоДоступаОприходованиеТоваров Тогда

		УсловиеСсылка = "(ДокументПересортицаТоваров.Ссылка ЕСТЬ НЕ NULL ИЛИ ДокументОприходованиеТоваров.Ссылка ЕСТЬ НЕ NULL)";
		УсловиеОрганизация = "ДокументПересортицаТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент ИЛИ ДокументОприходованиеТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент";
		
	ИначеЕсли ПравоДоступаПересортицаТоваров Тогда
		
		УсловиеСсылка = "ДокументПересортицаТоваров.Ссылка ЕСТЬ НЕ NULL";
		УсловиеОрганизация = "ДокументПересортицаТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент";
		
	ИначеЕсли ПравоДоступаОприходованиеТоваров Тогда
		
		УсловиеСсылка = "ДокументОприходованиеТоваров.Ссылка ЕСТЬ НЕ NULL";
		УсловиеОрганизация = "ДокументОприходованиеТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", УсловиеСсылка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", УсловиеОрганизация);
	
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РАЗЛИЧНЫЕ 0.5 КАК КоличествоДокументов
	|ПОМЕСТИТЬ
	|	ВремТаблВРегистр3
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|ГДЕ
	|	АкцизныеМаркиЕГАИС.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.КПостановкеНаБаланс)
	|	И (АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС В(&ОрганизацияЕГАИС) ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|	И НЕ АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС.Ссылка ЕСТЬ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВРегистры1И2.КоличествоДокументов, 0) + ЕСТЬNULL(ВРегистр3.КоличествоДокументов, 0) КАК КоличествоДокументов
	|ИЗ
	|	ВремТаблВРегистры1И2 КАК ВРегистры1И2
	|	ПОЛНОЕ СОЕДИНЕНИЕ ВремТаблВРегистр3 КАК ВРегистр3
	|	ПО ИСТИНА
	//|;
	//|/////////////////////////////////////////////////////////////////////////////
	|";
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаАктПостановкиНаБалансЕГАИСОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаАктПостановкиНаБалансЕГАИСОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры


// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаАктСписанияЕГАИСОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.АктСписанияЕГАИС.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И (%УсловиеОрганизация% ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|";
	
	ПравоДоступаСписаниеТоваров         = ПравоДоступа("Чтение", Метаданные.Документы.СписаниеТоваров);
	ПравоДоступаПересортицаТоваров      = ПравоДоступа("Чтение", Метаданные.Документы.ПересортицаТоваров);
	ПравоДоступаОтчетОРозничныхПродажах = ПравоДоступа("Чтение", Метаданные.Документы.ОтчетОРозничныхПродажах);
	ПравоДоступаСборкаТоваров           = ПравоДоступа("Чтение", Метаданные.Документы.СборкаТоваров);
	
	УсловияСсылка = Новый Массив;
	УсловияОрганизация = Новый Массив;
	
	ЛевоеСоединение = "";
	Если ПравоДоступаСписаниеТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.СписаниеТоваров КАК ДокументСписаниеТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументСписаниеТоваров.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументСписаниеТоваров.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументСписаниеТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ПравоДоступаПересортицаТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ПересортицаТоваров КАК ДокументПересортицаТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументПересортицаТоваров.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументПересортицаТоваров.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументПересортицаТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ПравоДоступаОтчетОРозничныхПродажах Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ОтчетОРозничныхПродажах КАК ДокументОтчетОРозничныхПродажах
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументОтчетОРозничныхПродажах.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументОтчетОРозничныхПродажах.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументОтчетОРозничныхПродажах.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ПравоДоступаСборкаТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.СборкаТоваров КАК ДокументСборкаТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументСборкаТоваров.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументСборкаТоваров.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументСборкаТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЛевоеСоединение) Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ПО
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&ОрганизацияЕГАИС)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	Если УсловияСсылка.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", СтрСоединить(УсловияСсылка, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", "");
	КонецЕсли;
	
	Если УсловияОрганизация.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", СтрСоединить(УсловияОрганизация, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", "");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаАктСписанияЕГАИСОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаАктСписанияЕГАИСОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры


// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаВозвратИзРегистра2ЕГАИСОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.ВозвратИзРегистра2ЕГАИС.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И (%УсловиеОрганизация% ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	ПравоДоступаТТНИсходящаяЕГАИС = ПравоДоступа("Чтение", Метаданные.Документы.ТТНИсходящаяЕГАИС);
	
	ЛевоеСоединение = "";
	Если ПравоДоступаТТНИсходящаяЕГАИС Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ТТНИсходящаяЕГАИС КАК ДокументТТНИсходящаяЕГАИС
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументТТНИсходящаяЕГАИС.Ссылка
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ПО
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&ОрганизацияЕГАИС)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	УсловиеСсылка = "";
	УсловиеОрганизация = "";
	
	Если ПравоДоступаТТНИсходящаяЕГАИС Тогда
		
		УсловиеСсылка = "ДокументТТНИсходящаяЕГАИС.Ссылка ЕСТЬ НЕ NULL";
		УсловиеОрганизация = "ДокументТТНИсходящаяЕГАИС.Грузоотправитель = КлассификаторОрганизацийЕГАИС.Контрагент";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", УсловиеСсылка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", УсловиеОрганизация);
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаВозвратИзРегистра2ЕГАИСОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаВозвратИзРегистра2ЕГАИСОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры


// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаПередачаВРегистр2ЕГАИСОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.ПередачаВРегистр2ЕГАИС.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И (%УсловиеОрганизация% ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|";
	
	ПравоДоступаТТНВходящаяЕГАИС = ПравоДоступа("Чтение", Метаданные.Документы.ТТНВходящаяЕГАИС);
	
	УсловияСсылка = Новый Массив;
	УсловияОрганизация = Новый Массив;
	
	ЛевоеСоединение = "";
	Если ПравоДоступаТТНВходящаяЕГАИС Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ТТНВходящаяЕГАИС КАК ДокументТТНВходящаяЕГАИС
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументТТНВходящаяЕГАИС.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументТТНВходящаяЕГАИС.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументТТНВходящаяЕГАИС.Грузополучатель В(&ОрганизацияЕГАИС)");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	Если УсловияСсылка.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", СтрСоединить(УсловияСсылка, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", "");
	КонецЕсли;
	
	Если УсловияОрганизация.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", СтрСоединить(УсловияОрганизация, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", "");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаПередачаВРегистр2ЕГАИСОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаПередачаВРегистр2ЕГАИСОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаТТНИсходящаяЕГАИСОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.ТТНИсходящаяЕГАИС.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И (%УсловиеОрганизация% ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|";
	
	ПравоДоступаВозвратТоваровПоставщику          = ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровПоставщику);
	ПравоДоступаПеремещениеТоваров                = ПравоДоступа("Чтение", Метаданные.Документы.ПеремещениеТоваров);
	ПравоДоступаПередачаТоваровМеждуОрганизациями = ПравоДоступа("Чтение", Метаданные.Документы.ПередачаТоваровМеждуОрганизациями);
	
	УсловияСсылка = Новый Массив;
	УсловияОрганизация = Новый Массив;
	
	ЛевоеСоединение = "";
	Если ПравоДоступаВозвратТоваровПоставщику Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ВозвратТоваровПоставщику КАК ДокументВозвратТоваровПоставщику
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументВозвратТоваровПоставщику.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументВозвратТоваровПоставщику.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументВозвратТоваровПоставщику.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ПравоДоступаПеремещениеТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ПеремещениеТоваров КАК ДокументПеремещениеТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументПеремещениеТоваров.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументПеремещениеТоваров.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументПеремещениеТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ПравоДоступаПередачаТоваровМеждуОрганизациями Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДокументПередачаТоваровМеждуОрганизациями
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументПередачаТоваровМеждуОрганизациями.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументПередачаТоваровМеждуОрганизациями.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументПередачаТоваровМеждуОрганизациями.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЛевоеСоединение) Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ПО
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&ОрганизацияЕГАИС)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	Если УсловияСсылка.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", СтрСоединить(УсловияСсылка, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", "");
	КонецЕсли;
	
	Если УсловияОрганизация.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", СтрСоединить(УсловияОрганизация, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", "");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаТТНИсходящаяЕГАИСОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаТТНИсходящаяЕГАИСОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаЧекЕГАИСОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.ЧекЕГАИС.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И (%УсловиеОрганизация% ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|";
	
	ПравоДоступаРеализацияТоваров = ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваров);
	ПравоДоступаСборкаТоваров     = ПравоДоступа("Чтение", Метаданные.Документы.СборкаТоваров);
	
	УсловияСсылка = Новый Массив;
	УсловияОрганизация = Новый Массив;
	
	ЛевоеСоединение = "";
	Если ПравоДоступаРеализацияТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.РеализацияТоваров КАК ДокументРеализацияТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументРеализацияТоваров.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументРеализацияТоваров.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументРеализацияТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ПравоДоступаСборкаТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.СборкаТоваров КАК ДокументСборкаТоваров
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументСборкаТоваров.Ссылка
		|";
		УсловияСсылка.Добавить("ДокументСборкаТоваров.Ссылка ЕСТЬ НЕ NULL");
		УсловияОрганизация.Добавить("ДокументСборкаТоваров.Организация = КлассификаторОрганизацийЕГАИС.Контрагент");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЛевоеСоединение) Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ПО
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&ОрганизацияЕГАИС)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	Если УсловияСсылка.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", СтрСоединить(УсловияСсылка, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", "");
	КонецЕсли;
	
	Если УсловияОрганизация.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", СтрСоединить(УсловияОрганизация, " ИЛИ "));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", "");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаЧекЕГАИСОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаЧекЕГАИСОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры


// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаЧекЕГАИСВозвратОформите(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Документ)
	|	* КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОформленияДокументовЕГАИС.Основание) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%)
	|	И СтатусыОформленияДокументовЕГАИС.Документ = ЗНАЧЕНИЕ(Документ.ЧекЕГАИСВозврат.ПустаяСсылка)
	|	И СтатусыОформленияДокументовЕГАИС.СтатусОформления В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.НеОформлено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры)
	|	)
	|	И (%УсловиеОрганизация% ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|";
	
	ПравоДоступаВозвратТоваровОтПокупателя = ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровОтПокупателя);
	
	ЛевоеСоединение = "";
	Если ПравоДоступаВозвратТоваровОтПокупателя Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ВозвратТоваровОтПокупателя КАК ДокументВозвратТоваровОтПокупателя
		|ПО
		|	СтатусыОформленияДокументовЕГАИС.Основание = ДокументВозвратТоваровОтПокупателя.Ссылка
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ПО
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&ОрганизацияЕГАИС)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	УсловиеСсылка = "";
	УсловиеОрганизация = "";
	
	Если ПравоДоступаВозвратТоваровОтПокупателя Тогда
		
		УсловиеСсылка = "ДокументВозвратТоваровОтПокупателя.Ссылка ЕСТЬ НЕ NULL";
		УсловиеОрганизация = "ДокументВозвратТоваровОтПокупателя.Организация = КлассификаторОрганизацийЕГАИС.Контрагент";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", УсловиеСсылка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", УсловиеОрганизация);
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаЧекЕГАИСВозвратОтработайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяет текст запроса для отображения требующих действий документов в основной панели ЕГАИС
//   Возвращаемое поле результата запроса: КоличествоДокументов
// Параметры:
//   ТекстЗапроса - заполняемый текст запроса
//
Процедура ТекстЗапросаЧекЕГАИСВозвратОжидайте(ТекстЗапроса) Экспорт
	
	Возврат;
	
КонецПроцедуры


#КонецОбласти

#Область Статусы

// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.АктПостановкиНаБалансЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияАктПостановкиНаБалансЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияАктПостановкиНаБалансЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИС(ДокументОснование);
	
КонецПроцедуры


// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.АктСписанияЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияАктСписанияЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияАктСписанияЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияАктСписанияЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияАктСписанияЕГАИС(ДокументОснование);
	
КонецПроцедуры


// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.ВозвратИзРегистра2ЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияВозвратИзРегистра2ЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияВозвратИзРегистра2ЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияВозвратИзРегистра2ЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияВозвратИзРегистра2ЕГАИС(ДокументОснование);
	
КонецПроцедуры


// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.ПередачаВРегистр2ЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияПередачаВРегистр2ЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияПередачаВРегистр2ЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияПередачаВРегистр2ЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияПередачаВРегистр2ЕГАИС(ДокументОснование);
	
КонецПроцедуры


// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.ТТНИсходящаяЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияТТНИсходящаяЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияТТНИсходящаяЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияТТНИсходящаяЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияТТНИсходящаяЕГАИС(ДокументОснование);
	
КонецПроцедуры


// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.ТТНВходящаяЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияТТНВходящаяЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияТТНВходящаяЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияТТНВходящаяЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияТТНВходящаяЕГАИС(ДокументОснование);
	
КонецПроцедуры

// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.ЧекЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияЧекЕГАИС(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияЧекЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияЧекЕГАИС(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияЧекЕГАИС(ДокументОснование);
	
КонецПроцедуры


// Выполняет расчет статуса.
//
// Параметры:
//   Источник - ДокументСсылка.ЧекЕГАИСВозврат - Документ, по которому требуется рассчитать статус оформления.
//   Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияЧекЕГАИСВозврат(Источник, Отказ) Экспорт
	
	ИнтеграцияЕГАИСРТ.РассчитатьСтатусОформленияЧекЕГАИСВозврат(Источник, Отказ);
	
КонецПроцедуры

// Переопределяет запрос для получения статуса оформления.
//
// Параметры:
//   Запрос - Запрос для получения статуса оформления.
//   ДокументОснование - ДокументСсылка - Документ основание.
//
Процедура ЗапросСтатусаОформленияЧекЕГАИСВозврат(Запрос, ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияЕГАИСРТ.ЗапросСтатусаОформленияЧекЕГАИСВозврат(ДокументОснование);
	
КонецПроцедуры


// Устанавливает признак "Не используется" для необрабатываемых статусов
// 
// Параметры:
//   НеИспользуется - Булево                               - устанавливаемый признак,
//   Статус         - ПеречислениеСсылка.СтатусыОбработки* - проверяемый статус.
//
Процедура ПриОпределенииИспользуемыхСтатусовДокумента(НеИспользуется, Статус) Экспорт
	
	Возврат;
	
КонецПроцедуры


#КонецОбласти

#Область Серии

// Заполняет, при необходимости создает серии в документе ЕГАИС или его выделенных строках
// 
// Параметры:
// 	Контекст  - (см. ИнтеграцияЕГАИС.КонтекстЗаполненияСерий) - структура входящих данных для заполнения/генерации серий
// 	Результат - (см. ИнтеграцияЕГАИС.РезультатЗаполненияСерий) - результат заполнения серий
//
Процедура ЗаполнитьСгенерироватьСерии(Контекст, Результат) Экспорт
	
	ПараметрыЗаполнения = Неопределено;
	ПараметрыЗаполнения = ИнтеграцияЕГАИСРТ.ПараметрыЗаполненияСерий();
	ПараметрыЗаполнения.ОрганизацияЕГАИС = Контекст.Объект.Грузополучатель;
	ПараметрыЗаполнения.ЗаполнятьБезЗапросаСправок = Контекст.ЗаполнятьБезЗапросаСправок;
	ПараметрыЗаполнения.ПараметрыУказанияСерий = Контекст.ПараметрыУказанияСерий;
	
	Если ТипЗнч(Контекст.Объект.ТорговыйОбъект) = ТипЗнч(Контекст.СобственныйТорговыйОбъектЗначениеПоУмолчанию) Тогда
		ЗаполнитьПараметрЗаполненияСклад(Контекст.Объект, ПараметрыЗаполнения);
	КонецЕсли;
	
	СтруктураДействий = Контекст.СтруктураДействий;
	Если (СтруктураДействий = Неопределено) Тогда
		СтруктураДействий = Новый Структура;
	КонецЕсли;
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
		Новый Структура("ПараметрыУказанияСерий, Склад", Контекст.ПараметрыУказанияСерий, ПараметрыЗаполнения.Склад));
		
	РезультатЗаполнения = ИнтеграцияЕГАИСРТ.ЗаполнитьСгенерироватьСерии( 
		Контекст.Объект.Товары,
		Контекст.МассивСтрок,
		ПараметрыЗаполнения,
		СтруктураДействий);
		
	ЗаполнитьЗначенияСвойств(Результат, РезультатЗаполнения);
	
КонецПроцедуры

// Заполняет свойство "Склад" в структуре параметров заполнения серий
//
// Параметры:
// 	Объект - ДокументОбъект, ДанныеФормыКоллекция - объект, содержащий значение склада
//  ПараметрыЗаполнения - Структура - Структура параметров заполнения серий
//
Процедура ЗаполнитьПараметрЗаполненияСклад(Объект, ПараметрыЗаполнения) Экспорт

	
КонецПроцедуры // ЗаполнитьПараметрЗаполненияСклад()


#КонецОбласти

#Область СлужебныеПроцедурыИФункцииРТ

// Заполняет свойство "Склад" в структуре параметров заполнения серий
//
// Параметры:
// 	Объект - ДокументОбъект, ДанныеФормыКоллекция - объект, содержащий значение склада
//  ПараметрыЗаполнения - Структура - Структура параметров заполнения серий
//
Процедура ЗаполнитьПараметрЗаполненияСкладМагазин(Объект, ПараметрыЗаполнения) Экспорт

	ПараметрыУказанияСерий = ПараметрыЗаполнения.ПараметрыУказанияСерий;
	
	Если ПараметрыУказанияСерий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяПоляСклад)
		И ОбщегоНазначенияРТКлиентСервер.ЕстьРеквизитОбъекта(Объект, ПараметрыУказанияСерий.ИмяПоляСклад) Тогда
		ПараметрыЗаполнения.Склад = Объект[ПараметрыУказанияСерий.ИмяПоляСклад];
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.Свойство("ИмяПоляМагазин") И ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяПоляМагазин) 
		И ОбщегоНазначенияРТКлиентСервер.ЕстьРеквизитОбъекта(Объект, ПараметрыУказанияСерий.ИмяПоляМагазин) Тогда
		ПараметрыЗаполнения.Магазин = Объект[ПараметрыУказанияСерий.ИмяПоляМагазин];
	КонецЕсли; 
	
КонецПроцедуры // ЗаполнитьПараметрЗаполненияСклад()


// Заполняет свойство "ТТНВходящаяЕГАИС" в документе "Перемещение товаров"
//
Процедура ПриСозданииТТНВходящаяНаОснованииПеремещенеТоваровОбработчик(Источник) Экспорт
	
	Если Не Источник.ДокументОснование = Неопределено И 
		ТипЗнч(Источник.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
		
		ПеремещениеСсылка = Источник.ДокументОснование;
		
		УстановитьПривилегированныйРежим(Истина);
		
		ПеремещениеОбъект = ПеремещениеСсылка.ПолучитьОбъект();
		ПеремещениеОбъект.ТТНВходящаяЕГАИС = Источник.Ссылка;
		ПеремещениеОбъект.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеНаОсновании

// Заполняет коэффициент пересчета неупакованной продукции, при вызове заполнения библиотечных документов из прикладных,
//   если коэффициент пересчета отличается от 1
//
// Параметры:
//   ИмяПоля    - Строка - поле коэффициента пересчета неупакованной алкогольной продукции
//   ИмяТаблицы - Строка - имя таблицы запроса-источника
//
Процедура ПолеКоэффициентПересчетаНеупакованнойПродукции(ИмяПоля, ИмяТаблицы = "ТабличнаяЧасть") Экспорт
	
	ИмяПоля = ИнтеграцияЕГАИСРТ.ПолеКоэффициентПересчетаНеупакованнойПродукции(ИмяТаблицы);
	
КонецПроцедуры

#КонецОбласти


