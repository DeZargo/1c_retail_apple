#Область ПрограммныйИнтерфейс

// Функция - Определяет СНО для организации на указанную дату
//
// Параметры:
//  Организация	 - 	 организация, у которой необходимо определить СНО 
//  Дата		 - 	 дата, на которую необходимо определит СНО организации.
//                   Если не указано, СНО определяется на текущую дату сеанса.
// 
// Возвращаемое значение:
//   Перечисление.ТипыСистемНалогообложенияККТ, Неопределено. Система налогообложения организации. Если не удалось найти, возвращает Неопределено. 
//
Функция СистемаНалогообложенияОрганизации(Организация, Дата = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрименениеСистемНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
		|	ПрименениеСистемНалогообложенияСрезПоследних.ОсвобожденОтНДС КАК ОсвобожденОтНДС
		|ИЗ
		|	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних(
		|			&ДатаСНО,
		|			Организация = &Организация
		|				И Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)) КАК ПрименениеСистемНалогообложенияСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСНО", 		Дата);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСНО = РезультатЗапроса.Выбрать();
	
	Если ВыборкаСНО.Следующий() Тогда
		
		Результат = Новый Структура("СистемаНалогообложения, ОсвобожденОтНДС");
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаСНО);
		
		Возврат Результат;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Функция - Разрешено удалить
//
// Параметры:
//  Период		 - 	 дата записи, которую нужно проверить 
//  Организация	 - 	 организация, по которой проходит проверка 
// 
// Возвращаемое значение:
//   Булево. Истина - разрешено удалять, Ложь - запрещено удалять 
//
Функция РазрешеноУдалятьЗапись(Период, Организация) Экспорт
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		ОрганизацияПомеченаНаУдаление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ПометкаУдаления");
		
		Если ОрганизацияПомеченаНаУдаление Тогда
			Возврат Истина;
		КонецЕсли;
		
	#КонецЕсли
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложения.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения КАК ПрименениеСистемНалогообложения
	|ГДЕ
	|	ПрименениеСистемНалогообложения.Период <> &Период
	|	И ПрименениеСистемНалогообложения.Организация = &Организация
	|	И ПрименениеСистемНалогообложения.Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|	И ПрименениеСистемНалогообложения.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	И ПрименениеСистемНалогообложения.ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Период", 		Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РазрешеноУдалять = НЕ РезультатЗапроса.Пустой();
	
	Возврат РазрешеноУдалять;
	
КонецФункции

// Функция - Организация использует Патентную систему налогообложения
//
// Параметры:
//  Организация	 - 	 организация, по которой проходит проверка 
// 
// Возвращаемое значение:
//   Булево. Истина - использует Патент, Ложь - не использует патент
//
Функция ОрганизацияИспользуетПатент(Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложения.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения КАК ПрименениеСистемНалогообложения
	|ГДЕ
	|	ПрименениеСистемНалогообложения.Организация = &Организация
	|	И ПрименениеСистемНалогообложения.СистемаНалогообложения = &СистемаНалогообложения";
	
	Запрос.УстановитьПараметр("Организация", 			Организация);
	Запрос.УстановитьПараметр("СистемаНалогообложения", Перечисления.ТипыСистемНалогообложенияККТ.Патент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОрганизацияИспользуетПатент = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОрганизацияИспользуетПатент;
	
КонецФункции

// Возвращает систему налогообложения переданных документов
//
// Параметры:
//  МассивДокументов	 - 	 массив документов у которых есть реквизит СистемаНалогообложения.
//							 Могут быть переданы документы разных типов.
// 
// Возвращаемое значение:
//  Соответствие
//      * Ключ     - ссылка на документ,
//      * Значение - значение Системы налогообложения
//
Функция СистемаНалогообложенияДокументов(МассивДокументов) Экспорт
	
	ЗначенияСНОДокументов = Новый Соответствие;
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		// Разделение документов по типам
		РазбивкаПоТипам = ОбщегоНазначенияРТ.СоответствиеМассивовПоТипамОбъектов(МассивДокументов);
		
		// Определение значений реквизита
		Для каждого ТипДокументов Из РазбивкаПоТипам Цикл
			
			Если НЕ ОбщегоНазначения.ЕстьРеквизитОбъекта(
					"СистемаНалогообложения", ТипДокументов.Значение[0].Метаданные()) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначенияСНО = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
				ТипДокументов.Значение, "СистемаНалогообложения");
			
			Для каждого Документ Из ЗначенияСНО Цикл
				
				ЗначенияСНОДокументов.Вставить(Документ.Ключ, Документ.Значение);
				
			КонецЦикла;
			
		КонецЦикла;
		
	#КонецЕсли
	
	Возврат ЗначенияСНОДокументов
	
КонецФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Магазин)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти
