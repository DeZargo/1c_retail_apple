#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры); 
	
	ЗначенияЗаполнения 					= Параметры.ЗначенияЗаполнения;
	
	Элементы.Организация.Доступность 	= НЕ ЗначенияЗаполнения.Свойство("Организация");
	Элементы.Магазин.Доступность 		= НЕ ЗначенияЗаполнения.Свойство("Магазин");
	Элементы.Склад.Доступность 			= НЕ ЗначенияЗаполнения.Свойство("Склад");
	Элементы.ТоварнаяГруппа.Доступность = НЕ ЗначенияЗаполнения.Свойство("ТоварнаяГруппа");
	Элементы.Склад.ТолькоПросмотр		= НЕ (ЗначениеЗаполнено(Запись.Магазин) И ЗначениеЗаполнено(Запись.Организация));
	
	Если Параметры.Свойство("ВводСНООрганизации") Тогда
		ВводСНООрганизации					= Истина;
		ЭтотОбъект.Заголовок 				= "Система налогообложения организации (" + Запись.Организация + ")";
		ЭтотОбъект.АвтоЗаголовок 			= Ложь;
		Элементы.Организация.Доступность	= Ложь;
		Элементы.Магазин.Видимость 			= Ложь;
		Элементы.Склад.Видимость 			= Ложь;
		Элементы.ТоварнаяГруппа.Видимость 	= Ложь;
	КонецЕсли;
	
	НастроитьСписокСНО();
	ЗаполнитьСНО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Запись.Склад) Тогда
		
		РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Склад, "Организация, Магазин");
		
		Если РеквизитыСклада.Организация <> Запись.Организация Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Значение поля ""Склад"" не соответствует выбранной организации'"),,, "Запись.Склад");
			Отказ = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.Магазин) И РеквизитыСклада.Магазин <> Запись.Магазин Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Значение поля ""Склад"" не соответствует выбранному магазину'"),,, "Запись.Склад");
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.Магазин) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'При заполненом значении поля ""Склад"" обязательно должено быть заполнено поле ""Магазин""'"),,, "Запись.Магазин");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СистемаНалогообложенияСтрока <> "УСН" Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "УСНОбъектНалогообложения");
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ВводСНООрганизации Тогда
		Оповестить("ИзменениеСНООрганизации", Запись.Организация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	УстановитьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	ЗаполнитьОрганизациюМагазинИзСклада();
	
	Если ЗначениеЗаполнено(Запись.Склад) Тогда
		Элементы.Магазин.АвтоОтметкаНезаполненного 	= Истина;
	Иначе
		Элементы.Магазин.АвтоОтметкаНезаполненного 	= Ложь;
		Элементы.Магазин.ОтметкаНезаполненного		= Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	УстановитьСНОВЗаписи();
	
	Если СистемаНалогообложенияСтрока <> "Общая" И СистемаНалогообложенияСтрока <> "ЕСН" Тогда
		Запись.ОсвобожденОтНДС = Истина;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура УСНОбъектНалогообложенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	УстановитьСНОВЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	НастроитьСписокСНО();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	
	Элементы.Склад.ТолькоПросмотр = НЕ (ЗначениеЗаполнено(Запись.Магазин) И ЗначениеЗаполнено(Запись.Организация));
	Элементы.УСНОбъектНалогообложения.Видимость = СистемаНалогообложенияСтрока = "УСН";
	
	Элементы.ОсвобожденОтНДС.Доступность = СистемаНалогообложенияСтрока = "Общая"
														ИЛИ СистемаНалогообложенияСтрока = "ЕСН";

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСНО()
	
	Если ЗначениеЗаполнено(Запись.СистемаНалогообложения) Тогда
		Если Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
			СистемаНалогообложенияСтрока 	= "УСН";
			УСНОбъектНалогообложения 		= "Доход";
		ИначеЕсли Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход Тогда
			СистемаНалогообложенияСтрока	= "УСН";
			УСНОбъектНалогообложения 		= "Доход минус расход";
		ИначеЕсли Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
			СистемаНалогообложенияСтрока 	= "Общая";
		ИначеЕсли Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД Тогда
			СистемаНалогообложенияСтрока 	= "ЕНВД";
		ИначеЕсли Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
			СистемаНалогообложенияСтрока 	= "Патент";	
		ИначеЕсли Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН Тогда
			СистемаНалогообложенияСтрока 	= "ЕСН";
		КонецЕсли;
	КонецЕсли;
	
	Элементы.УСНОбъектНалогообложения.Видимость = СистемаНалогообложенияСтрока = "УСН";
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСНОВЗаписи()
	
	Если СистемаНалогообложенияСтрока = "УСН" И УСНОбъектНалогообложения = "Доход" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.УСНДоход");
	ИначеЕсли СистемаНалогообложенияСтрока = "УСН" И УСНОбъектНалогообложения = "Доход минус расход" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.УСНДоходРасход"); 
	ИначеЕсли СистемаНалогообложенияСтрока = "Общая" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ОСН"); 
	ИначеЕсли СистемаНалогообложенияСтрока = "ЕНВД" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ЕНВД"); 
	ИначеЕсли СистемаНалогообложенияСтрока = "Патент" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.Патент"); 
	ИначеЕсли СистемаНалогообложенияСтрока = "ЕСН"Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ЕСН"); 
	Иначе
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ПустаяСсылка");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОрганизациюМагазинИзСклада()
	
	Если ЗначениеЗаполнено(Запись.Склад) И (НЕ ЗначениеЗаполнено(Запись.Организация) ИЛИ НЕ ЗначениеЗаполнено(Запись.Магазин)) Тогда
		
		РеквизитыСклада = ОбщегоНазначенияРТВызовСервера.ПолучитьЗначенияРеквизитовОбъекта(Запись.Склад, Новый Структура("Организация, Магазин"));
		
		Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
			Запись.Организация = РеквизитыСклада.Организация;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.Магазин) Тогда
			Запись.Магазин = РеквизитыСклада.Магазин;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСписокСНО()
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "ЮрФизЛицо");
	СписокСНО = Элементы.СистемаНалогообложения.СписокВыбора;
	СНОПатент = СписокСНО.НайтиПоЗначению("Патент");
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		
		Если СНОПатент = Неопределено Тогда
			СписокСНО.Добавить("Патент", НСтр("ru = 'Патентная система налогообложения'"));
		КонецЕсли; 
		
	Иначе
		
		Если СНОПатент <> Неопределено Тогда
			СписокСНО.Удалить(СНОПатент);
		КонецЕсли;
		
		Если СистемаНалогообложенияСтрока = "Патент" Тогда
			СистемаНалогообложенияСтрока = "";
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти